#Область Переменные

&НаКлиенте
Перем СцТ_ГлавнаяФорма;

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПутьКФормам                 = Параметры.ПутьКФормам;
	
	ВидТестируемогоКлиента           = Параметры.ВидТестируемогоКлиента;
	ИмяПользователя                  = Параметры.ИмяПользователя;
	ПарольПользователяИБ             = Параметры.ПарольПользователяИБ;
	ПортТестируемогоКлиента          = Параметры.ПортТестируемогоКлиента;
	КомпьютерИБ                      = Параметры.КомпьютерИБ;
	ДругаяИБ                         = Параметры.ДругаяИБ;
	СтрокаПодключенияКИБ             = Параметры.СтрокаПодключенияКИБ;
	ДопПараметрыКоманднойСтроки      = Параметры.ДопПараметрыКоманднойСтроки;
	URLИБ                            = Параметры.URLИБ;
	ЭтоКлиентСервернаяИБ             = Параметры.ЭтоКлиентСервернаяИБ;
	ВыполнятьЗамерПроизводительности = Параметры.ВыполнятьЗамерПроизводительности;
	СцТ_РежимВыполнения              = Параметры.СцТ_РежимВыполнения;
	
	Элементы.ВыполнятьЗамерПроизводительности.Видимость = СцТ_РежимВыполнения;
	
	СписокВыбора = Элементы.ИмяПользователя.СписокВыбора;
	СцТ_ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	СписокВыбора.Добавить(СцТ_ТекущийПользователь.Имя,
						  ?(ПустаяСтрока(СцТ_ТекущийПользователь.ПолноеИмя),
							НСтр("ru = 'Текущий пользователь'"),
							СцТ_ТекущийПользователь.ПолноеИмя + НСтр("ru = ' (текущий)'")));
	
	Попытка
		СцТ_ПользователиИБ = ПользователиИнформационнойБазы.ПолучитьПользователей();
		Для каждого СцТ_Пользователь Из СцТ_ПользователиИБ Цикл
			Если СписокВыбора.НайтиПоЗначению(СцТ_Пользователь.Имя) = Неопределено Тогда
				СписокВыбора.Добавить(СцТ_Пользователь.Имя, СцТ_Пользователь.ПолноеИмя);
			КонецЕсли;
		КонецЦикла;
	Исключение
	КонецПопытки;
	
	Если СписокВыбора.НайтиПоЗначению(ИмяПользователя) = Неопределено Тогда
		Если ПустаяСтрока(ИмяПользователя) Тогда
			СписокВыбора.Добавить("", НСтр("ru = '<не указан>'"));
		Иначе
			СписокВыбора.Добавить(ИмяПользователя, ИмяПользователя + НСтр("ru = ' (отсутствует в информационной базе)'"));
		КонецЕсли;
	КонецЕсли;
	
	СписокВыбора.СортироватьПоПредставлению();
	
	ИспользоватьБраузерПоУмолчанию = Истина;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СцТ_ГлавнаяФорма = ВладелецФормы;
	УправлениеЭлементамиФормыДляРазныхИБ();
	УправлениеВидимостьюСтраницВидаКлиента();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_Шапка

&НаКлиенте
Процедура ВидТестируемогоКлиентаПриИзменении(Элемент)
	
	УправлениеВидимостьюСтраницВидаКлиента();
	Если (ВидТестируемогоКлиента = 2) Тогда
		ВыполнятьЗамерПроизводительности = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяПользователяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Элемент.Списоквыбора.НайтиПоЗначению(Текст) = Неопределено Тогда
		Элемент.Списоквыбора.Вставить(0, Текст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДругаяИБПриИзменении(Элемент)
	
	УправлениеЭлементамиФормыДляРазныхИБ();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЗапуститьКлиента(Команда)
	
	Если ВидТестируемогоКлиента = 2 И ПустаяСтрока(URLИБ) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не задан URL для подключения к информационной базе'");
		Сообщение.Поле = "URLИБ";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ДругаяИБ И ПустаяСтрока(СтрокаПодключенияКИБ) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не задана строка соединения с информационной базой'");
		Сообщение.Поле = "СтрокаПодключенияКИБ";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапуска = СцТ_ПараметрыЗапускаТП();
	ВозвращаемаяСтруктура = Новый Структура;
	ВозвращаемаяСтруктура.Вставить("РежимЗапуска"    , "Запуск");
	ВозвращаемаяСтруктура.Вставить("ПараметрыЗапуска", ПараметрыЗапуска);
	
	ОписаниеЗапущеногоПриложения = СцТ_ОписаниеЗапущеногоПриложения();
	
	Если ОписаниеЗапущеногоПриложения <> Неопределено Тогда
		
		ВозвращаемаяСтруктура.Вставить("ОписаниеЗапущеногоПриложения", ОписаниеЗапущеногоПриложения);
		
	КонецЕсли;
	
	Закрыть(ВозвращаемаяСтруктура);
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

&НаКлиенте
Функция СцТ_ОписаниеЗапущеногоПриложения()
	
	Если ВидТестируемогоКлиента = 2 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Попытка установить соединение с существующим приложением
	Попытка
		//СцТ_Контроллер = Вычислить("Новый ТестируемоеПриложение(""127.0.0.1"", ПортТестируемогоКлиента)");
		СцТ_Контроллер = Вычислить("Новый ТестируемоеПриложение(КомпьютерИБ, ПортТестируемогоКлиента)");
		СцТ_Контроллер.УстановитьСоединение();
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	// Если соединение установлено успешно, тогда определить заголовк главного окна запущеного приложения
	// или найти окно авторизации, если главное окно отсутствует и отображено окно авторизации
	
	ЗаголовокГлавногоОкна    = "";
	ЗаголовокОкнаАвторизации = НСтр("ru = '1С:Предприятие. Доступ к информационной базе'");
	
	ОсновноеОкно    = Неопределено;
	ОкноАвторизации = Неопределено;
	Попытка
		
		ОкнаПриложения = СцТ_Контроллер.НайтиОбъекты(Тип("ТестируемоеОкноКлиентскогоПриложения"));
		Если ОкнаПриложения.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Для каждого ТекОкно Из ОкнаПриложения Цикл
			Если ТекОкно.Основное Тогда
				ЗаголовокГлавногоОкна = ТекОкно.Заголовок;
				ОсновноеОкно          = ТекОкно;
				Прервать;
			ИначеЕсли ТекОкно.Заголовок = ЗаголовокОкнаАвторизации Тогда
				ОкноАвторизации = ТекОкно;
			КонецЕсли;
		КонецЦикла;
		
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Если ОсновноеОкно <> Неопределено Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("СцТ_Контроллер"   , СцТ_Контроллер);
		Результат.Вставить("ОсновноеОкно" , ОсновноеОкно);
		Результат.Вставить("ЗаголовокОкна", ЗаголовокГлавногоОкна);
		
		Возврат Результат;
		
	ИначеЕсли ОкноАвторизации <> Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("СцТ_Контроллер"   , СцТ_Контроллер);
		Результат.Вставить("ОсновноеОкно" , ОкноАвторизации);
		Результат.Вставить("ЗаголовокОкна", ЗаголовокОкнаАвторизации);
		
		Возврат Результат;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция СцТ_ПараметрыЗапускаТП()
	
	ПараметрыЗапускаКлиента = Новый Структура;
	ПараметрыЗапускаКлиента.Вставить("ВидТестируемогоКлиента"          , ВидТестируемогоКлиента);
	ПараметрыЗапускаКлиента.Вставить("ИмяПользователя"                 , ИмяПользователя);
	ПараметрыЗапускаКлиента.Вставить("ПарольПользователяИБ"            , ПарольПользователяИБ);
	ПараметрыЗапускаКлиента.Вставить("ПортТестируемогоКлиента"         , ПортТестируемогоКлиента);
	ПараметрыЗапускаКлиента.Вставить("КомпьютерИБ"                     , КомпьютерИБ);
	ПараметрыЗапускаКлиента.Вставить("ДругаяИБ"                        , ДругаяИБ);
	ПараметрыЗапускаКлиента.Вставить("СтрокаПодключенияКИБ"            , СтрокаПодключенияКИБ);
	ПараметрыЗапускаКлиента.Вставить("ДопПараметрыКоманднойСтроки"     , ДопПараметрыКоманднойСтроки);
	ПараметрыЗапускаКлиента.Вставить("URLИБ"                           , URLИБ);
	ПараметрыЗапускаКлиента.Вставить("ИспользоватьБраузерПоУмолчанию"  , ИспользоватьБраузерПоУмолчанию);
	ПараметрыЗапускаКлиента.Вставить("ВыполнятьЗамерПроизводительности", ВыполнятьЗамерПроизводительности);
	
	Возврат ПараметрыЗапускаКлиента;
	
КонецФункции

&НаКлиенте
Процедура УправлениеЭлементамиФормыДляРазныхИБ()
	
	Если ДругаяИБ Тогда
		Элементы.СтрокаПодключенияКИБ.Доступность = Истина;
		Элементы.КомпьютерИБ.Доступность          = Истина;
		Элементы.СтраницыПользователя.ТекущаяСтраница = Элементы.СтраницаПользователяДругойИБ;
	Иначе
		Элементы.СтрокаПодключенияКИБ.Доступность = Ложь;
		Элементы.КомпьютерИБ.Доступность          = Ложь;
		Элементы.СтраницыПользователя.ТекущаяСтраница = Элементы.СтраницаПользователяЭтойИБ;
		
		СтрокаПодключенияКИБ = СтрокаСоединенияИнформационнойБазы();
		
		Если СцТ_ГлавнаяФорма.ПараметрыСоединенияСИБ.КлиентСерверная Тогда
			КомпьютерИБ = СцТ_ГлавнаяФорма.ПараметрыСоединенияСИБ.Кластер;
		Иначе
			КомпьютерИБ = "127.0.0.1";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостьюСтраницВидаКлиента()
	
	Если (ВидТестируемогоКлиента = 2) Тогда
		Элементы.СтраницыВидаКлиента.ТекущаяСтраница = Элементы.СтраницыВидаКлиентаВеб;
	Иначе
		Элементы.СтраницыВидаКлиента.ТекущаяСтраница = Элементы.СтраницыВидаКлиентаНеВеб;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


