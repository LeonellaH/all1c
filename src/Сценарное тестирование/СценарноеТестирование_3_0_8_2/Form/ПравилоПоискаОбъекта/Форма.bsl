
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВариантПоискаОбъекта = Параметры.ВариантПоискаОбъекта;
	
	Элементы.ГруппаПоИмени.Видимость       = Параметры.ОтображатьИмяОбъекта;
	Элементы.ИскатьПоЗаголовку.Доступность = Элементы.ГруппаПоИмени.Видимость;
	
	ИскатьПоИмени     = (Лев(ВариантПоискаОбъекта, 1) = "1");
	ИскатьПоЗаголовку = (Сред(ВариантПоискаОбъекта, 2, 1) = "1");
	УчитыватьРасположение = (Прав(ВариантПоискаОбъекта, 1) = "1");
	
	ИмяОбъекта = Параметры.ИмяОбъекта;
	Если Лев(ИмяОбъекта, 1) = "$" Тогда
		ИмяОбъекта = "";
	КонецЕсли;
	
	ЗаголовокОбъекта = Параметры.ЗаголовокОбъекта;
	
	Если НЕ Параметры.ОтображатьУчитыватьРасположение Тогда
		УчитыватьРасположение                    = Ложь;
		Элементы.УчитыватьРасположение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_Шапка

&НаКлиенте
Процедура ИскатьПоИмениПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если НЕ ИскатьПоИмени И НЕ ИскатьПоЗаголовку Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не выбран способ поиска объекта по имени или/и по заголовку.'");
		Сообщение.Поле  = "ИскатьПоЗаголовку";
		Сообщение.Сообщить();
		Возврат;
		
	ИначеЕсли ИскатьПоИмени И ПустаяСтрока(ИмяОбъекта) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Поле ""Имя объекта"" не заполнено.'");
		Сообщение.Поле  = "ИмяОбъекта";
		Сообщение.Сообщить();
		Возврат;
		
	ИначеЕсли ИскатьПоЗаголовку И ПустаяСтрока(ЗаголовокОбъекта) Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Поле ""Заголовок объекта"" не заполнено.'");
		Сообщение.Поле  = "ЗаголовокОбъекта";
		Сообщение.Сообщить();
		Возврат;
		
	КонецЕсли;
	
	ВозвращаемаяСтруктура = Новый Структура("ВариантПоискаОбъекта,ИмяОбъекта,ЗаголовокОбъекта",
		?(ИскатьПоИмени, "1", "0") + ?(ИскатьПоЗаголовку, "1", "0") + ?(УчитыватьРасположение, "1", "0"),
		ИмяОбъекта,
		ЗаголовокОбъекта);
	
	Закрыть(ВозвращаемаяСтруктура);
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	
	Элементы.ИмяОбъекта.Доступность       = ИскатьПоИмени;
	Элементы.ЗаголовокОбъекта.Доступность = ИскатьПоЗаголовку;
	Если НЕ ИскатьПоЗаголовку Тогда
		УчитыватьРасположение = Ложь;
	КонецЕсли;
	
	Элементы.УчитыватьРасположение.Доступность = Элементы.ЗаголовокОбъекта.Доступность;
	
КонецПроцедуры


#КонецОбласти