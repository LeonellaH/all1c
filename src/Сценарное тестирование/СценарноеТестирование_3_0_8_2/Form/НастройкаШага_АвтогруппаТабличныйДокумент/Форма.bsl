
#Область Переменные

// Форма - владелец текущей формы (для краткости и наглядности вместо "ЭтаФорма.ВладелецФормы")
&НаКлиенте
Перем СцТ_ГлавнаяФорма Экспорт;

// Указатель на форму (главная или макрошага), из которой была открыта эта форма
&НаКлиенте
Перем ВызвавшаяФорма Экспорт;

// Указатель на ветку в дереве. Заполненяется, если шаг не новый
&НаКлиенте
Перем ДанныеШага Экспорт;

// Идентификатор узла в дереве. Заполнено если шаг не новый
&НаКлиенте
Перем ИдентификаторУзла Экспорт;

// Содержит интерактивный контейнер, соответствующий интерактивному шагу:
//	форма или таблица формы
&НаКлиенте
Перем ИнтерактивныйКонтейнер Экспорт;

// Окно-объект, которому подчинена текущая форма
&НаКлиенте
Перем ОкноВыбраннойФормы Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимСовместимости838 Тогда
		УстановитьДействие("ПередЗакрытием", "ПередЗакрытиеБезРежимаСовместимости");
	КонецЕсли;
	
	// Если форма открыта под внешним управлением, то не вся функциональность
	// должна работать
	Если Параметры.ОткрытаПодВнешнимУправлением Тогда
		
		ФормаОткрытаПодВнешнимУправлением = Истина;
		
		Элементы.ФормаОбновитьПоДаннымФормы.Доступность = Ложь;
		Элементы.ВыбратьЭлемент.Доступность             = Ложь;
		Элементы.АвтогруппаТД_Обновить.Доступность      = Ложь;
		
	КонецЕсли;
	///
	
	ЭтаОбработка = РеквизитФормыВЗначение("Объект");
	ПутьКФормам  = ЭтаОбработка.Метаданные().ПолноеИмя() + ".Форма.";
	
	// Управление доступностью
	ЭтотОбъект.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Элементы.ПрименитьИЗакрыть.Доступность  = НЕ Параметры.ТолькоПросмотр;
	Элементы.ПрименитьИзменения.Доступность = НЕ Параметры.ТолькоПросмотр;
	
	// основные атрибуты шага (ОА)
	// атрибуты, для редактирования, которых не требуется открытие формы настройки
	// или свернутые для хранения атрибуты
	ОА_ТипШага                          = Параметры.ТипШага;
	ОА_НомерШага                        = Параметры.НомерШага;
	ОА_Активность                       = Параметры.Активность;
	ОА_ОписательПоложенияШагаВДереве    = Параметры.ОписательПоложенияШагаВДереве;
	ОА_ЭтоНовый                         = Параметры.ЭтоНовый;
	ОА_СтруктураДанныхШага              = Параметры.СтруктураДанныхШага;
	
	// 1. Раскрытие структуры шага и определение типа шага
	Если ОА_СтруктураДанныхШага = Неопределено  Тогда
		Возврат;
	КонецЕсли;
	
	ДШ_Наименование                 = ОА_СтруктураДанныхШага.Наименование;
	ДШ_ТипМетаданных                = ОА_СтруктураДанныхШага.ТипМетаданных;
	ДШ_ИмяМетаданных                = ОА_СтруктураДанныхШага.ИмяМетаданных;
	ДШ_Автоописание                 = ОА_СтруктураДанныхШага.Автоописание;
	ДШ_Описание                     = ОА_СтруктураДанныхШага.Описание;
	ДШ_СкрытьАвтоописание           = ОА_СтруктураДанныхШага.СкрытьАвтоописание;
	ДШ_ОбСсылка                     = ОА_СтруктураДанныхШага.ОбСсылка;
	ДШ_ПредставлениеОбъекта         = ОА_СтруктураДанныхШага.ПредставлениеОбъекта;
	ДШ_ПредставлениеИмениМетаданных = ОА_СтруктураДанныхШага.ПредставлениеИмениМетаданных;
	ДШ_ИмяФормы                     = ОА_СтруктураДанныхШага.ИмяФормы;
	ДШ_ВыполнитьВручную             = ОА_СтруктураДанныхШага.ВыполнитьВручную;
	ДШ_Комментарий                  = ОА_СтруктураДанныхШага.Комментарий;
	ДШ_ЗаголовокФормы               = ОА_СтруктураДанныхШага.ЗаголовокФормы;
	ДШ_ДанныеКартинки               = ОА_СтруктураДанныхШага.ДанныеКартинки;
	
	Если ТипЗнч(ОА_СтруктураДанныхШага.ДеревоЗначения) = Тип("Структура") Тогда
		// Данные дерева переданы в виде структуры из главной формы обработки
		СцТ_ЗаполнитьДеревоФормыИзДереваСтруктур(ДеревоЗначения,
			ОА_СтруктураДанныхШага.ДеревоЗначения);
	ИначеЕсли ТипЗнч(ОА_СтруктураДанныхШага.ДеревоЗначения) = Тип("ХранилищеЗначения") Тогда
		// Данные из конфигурации могут поступить в виде данных ХЗ
		Попытка
			ДЗ_НаСервере = ОА_СтруктураДанныхШага.ДеревоЗначения.Получить();
			ПреобразоватьЗначенияВДереве(ДЗ_НаСервере, Истина);
			ЗначениеВРеквизитФормы(ДЗ_НаСервере, "ДеревоЗначения");
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	
	// Признак, что наименование установлено вручную
	НаименованиеШагаИзмененоВручную = СцТ_ЗначениеВДереве(
		ДеревоЗначения,
		"ИСТЗ_НаименованиеРучное",
		Ложь);
		
	// Картинки для закладок описания и комментария
	АдресКартинкиКомментария = ПоместитьВоВременноеХранилище(ЭтаОбработка.ПолучитьМакет("КартинкаКомментария"));
	КартинкаКомментария = Новый Картинка(ПолучитьИзВременногоХранилища(АдресКартинкиКомментария));
	
	Элементы.Страница_РедактируемоеОписание.Картинка = ?(
		ПустаяСтрока(ДШ_Описание),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
	Элементы.Страница_Комментарий.Картинка = ?(
		ПустаяСтрока(ДШ_Комментарий),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
	СцТ_НастроитьФормуДляШага();
	
	// Заполнение поведения при ошибке для группового шага
	СцТ_ЗаполнитьДействиеГрупповогоШагаПриОшибке(ЭтотОбъект);
	
	// Модифицированность
	Модифицированность = ОА_ЭтоНовый;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.ЭлементФормы_Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_УдалениеШаговСценария" Тогда
		
		Если ОА_ЭтоНовый Тогда
			Возврат;
		КонецЕсли;
		
		Если Параметр.НайтиПоЗначению(ДанныеШага.ПолучитьИдентификатор()) <> Неопределено Тогда
			Модифицированность = Ложь;
			Если Открыта() Тогда
				Закрыть();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли  ИмяСобытия = "СцТ_ПеремещениеУзловСценария" Тогда
		
		Если ОА_ЭтоНовый Тогда
			Возврат;
		КонецЕсли;
		
		НовыйУзел = Параметр[ДанныеШага];
		Если НовыйУзел <> Неопределено Тогда
			ДанныеШага = НовыйУзел;
			ИдентификаторУзла = ДанныеШага.ПолучитьИдентификатор();
			ДШ_НомерШага      = ДанныеШага.НомерШага;
			СцТ_СформироватьПредставлениеРодителя();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "СцТ_ОбновленыДанныеГрупповогоШага" Тогда
		
		Если Параметр = ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя Тогда
			СцТ_СформироватьПредставлениеРодителя();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "СцТ_ИзмениласьНумерацияШагов" Тогда
		
		Если ОА_ЭтоНовый Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			ДанныеШага = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ИдентификаторУзла);
			ОА_НомерШага = ДанныеШага.НомерШага;
			СцТ_СформироватьПредставлениеРодителя();
		Исключение
		КонецПопытки;
		
	ИначеЕсли ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("СцТ_ОбработатьЗапросОСохраненииДанныхШага", ЭтотОбъект),
			НСтр("ru = 'Данные шага были изменены. Применить изменения?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиеБезРежимаСовместимости(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		Если НЕ ЗавершениеРаботы Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("СцТ_ОбработатьЗапросОСохраненииДанныхШага", ЭтотОбъект),
				НСтр("ru = 'Данные шага были изменены. Применить изменения?'"),
				РежимДиалогаВопрос.ДаНетОтмена);
			
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Данные группового шага ""Табличный документ"" были изменены. При закрытии все изменения будут утеряны'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриЗакрытии()
	
	Попытка
		СцТ_ГлавнаяФорма.СцТ_ОткрытыеФормыНастройкиШагов.Удалить(ОА_ОписательПоложенияШагаВДереве.ИдентификаторШага);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СохранитьИЗакрытьФорму(Команда)
	
	Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	
	Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сохранение шага'"), , НСтр("ru = 'Данные шага сохранены.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАвтоназвание(Команда)
	
	СцТ_ОбновитьНаименование_НаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеШага(Команда)
	
	// Всегда отображаются имеено хранимые данные, которые сохранены в дереве шагов
	Если ОА_ЭтоНовый Тогда
		ТекстПредупреждения = НСтр("ru = 'Данные шага еще не записаны. Отобразить структуру хранения данных невозможно. Сохранить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("СцТ_ПослеОтветаНаВопросОСохраненииНового", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстПредупреждения, РежимДиалогаВопрос.ДаНет);
		Возврат;
	ИначеЕсли НЕ ОА_ЭтоНовый И Модифицированность Тогда
		ТекстПредупреждения = НСтр("ru = 'Данные шага были изменены. Сохранить изменения перед отображением структуры?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("СцТ_ПослеОтветаНаВопросОСохраненииСуществующего", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстПредупреждения, РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
	КонецЕсли;
	
	СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АктивностьПриИзменении(Элемент)
	
	Если ОА_Активность = 2 Тогда
		ОА_Активность = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ПроверочноеНаименование = СцТ_СформироватьОписаниеШага_НаКлиенте(Истина);
	
	НаименованиеШагаИзмененоВручную = (ДШ_Наименование <> ПроверочноеНаименование);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("СцТ_ОбновитьЗаголовкиЗакладок", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("СцТ_ОбновитьЗаголовкиЗакладок", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеГруппыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ОА_ЭтоНовый Тогда
		РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
	Иначе
		РодительУзла = ДанныеШага.ПолучитьРодителя();
	КонецЕсли;
	
	Если РодительУзла <> Неопределено Тогда
		СцТ_ГлавнаяФорма.СцТ_ОткрытьФормуНастройкиШага(РодительУзла, ВызвавшаяФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПриОшибкеВПодчиненныхШагахПриИзменении(Элемент)
	
	УправлениеДоступностьюПеременнойОшибкиГрупповогоШага(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиОповещений

// Обрабатывает нажатие пользователем кнопки в диалоге вопроса о сохранении
// данных шага перед закрытием формы
// Параметры
//	КодВозврата - код нажатой кнопки (Да, Нет, Отмена);
//	ДопДанные - дополнительные данные;
//
&НаКлиенте
Процедура СцТ_ОбработатьЗапросОСохраненииДанныхШага(КодВозврата, ДопДанные) Экспорт
	
	Если КодВозврата = КодВозвратаДиалога.Да Тогда
		Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли КодВозврата = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
		
		// При Отмене ничего не выполняется
		
	КонецЕсли;
	
КонецПроцедуры

// После ответа на вопрос диалога о Сохранении нового шага перед
// отображением хранимых данных
&НаКлиенте
Процедура СцТ_ПослеОтветаНаВопросОСохраненииНового(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	// Обработка нажатия "ДА"
	Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
		СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не удалось сохранить шаг'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

// После ответа на вопрос диалога о Сохранении уже существующего модифицированного шага перед
// отображением хранимых данных
&НаКлиенте
Процедура СцТ_ПослеОтветаНаВопросОСохраненииСуществующего(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		// Показываем, что хранится
		СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
		
	Иначе
		
		Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
			СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось сохранить изменения в шаге'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ВспомогательныеПроцедурыИФункции

#Область НастрокаПриЗагрузкеДанныхШага

// Индивидуальная настройка формы для шага
//
&НаСервере
Процедура СцТ_НастроитьФормуДляШага()
	
	ЗаполняемыйСписокВыбора = Элементы.ЭлементФормы_Тип.СписокВыбора;
	ЗаполняемыйСписокВыбора.Очистить();
	ЗаполняемыйСписокВыбора.Добавить("ТабличныйДокумент", НСтр("ru = 'Табличный документ'"));
	
	УзелОписанияТД = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ТабличныйДокумент");
	
	Если УзелОписанияТД <> Неопределено Тогда
		
		ЭлементФормы_Имя           = УзелОписанияТД.Значение;
		ЭлементФормы_Заголовок     = УзелОписанияТД.Синоним;
		ЭлементФормы_Тип           = УзелОписанияТД.ТипУзла;
		
		ВариантХраненияТД  = СцТ_ЗначениеВДереве(УзелОписанияТД, "ВариантХраненияТД", 0);
		
		Если ВариантХраненияТД = 0 Тогда
			// Хранится в виде дерева значений
			КоличествоСтрок   = СцТ_ЗначениеВДереве(УзелОписанияТД, "КоличествоСтрокТД"  , 1);
			КоличествоКолонок = СцТ_ЗначениеВДереве(УзелОписанияТД, "КоличествоКолонокТД", 1);
			
			СцТ_ОбновитьТаблДокПоДаннымФормы_НаСервере("", "ТД", "ТабличныйДокумент");
			
		ИначеЕсли ВариантХраненияТД = 1 Тогда
			// Хранится в виде файла в двоичных данных
			КоличествоСтрок   = СцТ_ЗначениеВДереве(УзелОписанияТД, "КоличествоСтрокТД"  , 0);
			КоличествоКолонок = СцТ_ЗначениеВДереве(УзелОписанияТД, "КоличествоКолонокТД", 0);
			
			ПрочитатьДанныеЗагруженногоТД();
			
			
		ИначеЕсли ВариантХраненияТД = 2 Тогда
			// Хранится путь к внешнему файлу
			КоличествоСтрок   = СцТ_ЗначениеВДереве(УзелОписанияТД, "КоличествоСтрокТД"  , 0);
			КоличествоКолонок = СцТ_ЗначениеВДереве(УзелОписанияТД, "КоличествоКолонокТД", 0);
			
			ИмяВнешнегоФайла = СцТ_ЗначениеВДереве(УзелОписанияТД, "ФайлТабличногоДокумента", "");
			
		КонецЕсли;
		
	Иначе
		ВариантХраненияТД = 0;
		
		КоличествоСтрок   = 1;
		КоличествоКолонок = 1;
		
	КонецЕсли;
	
	ЗаполнитьПутьКЭлементуФормы(УзелОписанияТД, ПутьКЭлементуФормы);
	
	ВариантПоискаОбъекта = СцТ_ЗначениеВДереве(ДеревоЗначения, "ВариантПоискаОбъекта", "100");
	
	УправлениеВидиомстьюНаСтраницеАвтогруппыТД(Ложь);
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страница_ДанныеШага;
	
КонецПроцедуры

&НаСервере
Процедура СцТ_ЗаполнитьДействиеГрупповогоШагаПриОшибке(ЭтотОбъект)
	
	СписокВыбора = Элементы.ДействиеПриОшибке.СписокВыбора;
	СписокВыбора.Добавить("Остановить", НСтр("ru = 'Остановить выполнение'"));
	СписокВыбора.Добавить("Продолжить", НСтр("ru = 'Продолжить выполнение'"));
	
	// Параметры действий при ошибке находятся в корне
	ДействиеПриОшибке = СцТ_ЗначениеВДереве(
		ДеревоЗначения,
		"ДействиеПриОшибке",
		"Остановить");
		
	ПеременнаяПриОшибке = СцТ_ЗначениеВДереве(
		ДеревоЗначения,
		"ПеременнаяПриОшибке",
		"");
	
	УправлениеДоступностьюПеременнойОшибкиГрупповогоШага(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СохранениеШагаВСценарии

// Сохранение реализовано в виде функции, т.к. потребуется
// ее выполнение в обработчике ПередЗакрытием
//
&НаКлиенте
Функция СцТ_ПрименитьИзменения_НаКлиенте() Экспорт
	
	// Проверка, что владелец формы еще не закрыт
	Если НЕ ВладелецФормы.Открыта() Тогда
		ТекстСообщения = НСтр("ru = 'Форма с деревом шагов уже закрыта. Применить изменения невозможно'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	// Проверка правильности данных
	Отказ = Ложь;
	
	Если ПустаяСтрока(ДШ_Наименование) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не заполнено наименование.'");
		
		СП = Новый СообщениеПользователю;
		СП.Текст = ТекстСообщения;
		СП.ПутьКДанным = "ДШ_Наименование";
		СП.Сообщить();
		
		Отказ = Истина;
			
	КонецЕсли;
	
	Если ПустаяСтрока(ЭлементФормы_Имя) Тогда
		ТекстСообщения = НСтр("ru = 'Имя элемента должно быть заполнено.'");
		
		СП = Новый СообщениеПользователю;
		СП.Текст = ТекстСообщения;
		СП.ПутьКДанным = "ЭлементФормы_Имя";
		СП.Сообщить();
		
		Отказ = Истина;
	КонецЕсли;
	
	
	// Проверка индивидуальных настроек шага и заполнение ДереваЗначения
	СцТ_ПодготовитьДанныеШагаДляСохранения_НаКлиенте(Отказ);
	
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СцТ_ОбновитьНаименование_НаКлиенте();
	ОбновитьПользовательскоеОписание();
	
	ВозвращаемаяСтруктура = СцТ_СформироватьСтруктуруданныхШага_НаКлиенте();
	
	Если ОА_ЭтоНовый Тогда
		
		ДанныеШага = СцТ_ГлавнаяФорма.СцТ_СохранитьНовыйШаг(ВызвавшаяФорма, ВозвращаемаяСтруктура);
		
		ИдентификаторУзла = ДанныеШага.ПолучитьИдентификатор();
		ОА_НомерШага      = ДанныеШага.НомерШага;
		ОА_ЭтоНовый       = Ложь;
		
	Иначе
		СцТ_ГлавнаяФорма.СцТ_ПрименитьИзмененияВШаге(ВызвавшаяФорма, ДанныеШага, ВозвращаемаяСтруктура);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

// Заполнение дерева значения данными формы.
// Проверка корректности специфических для этого шага данных
&НаКлиенте
Процедура СцТ_ПодготовитьДанныеШагаДляСохранения_НаКлиенте(Отказ)
	
	// Проверки
	
	// формирование дерева
	
	Если НаименованиеШагаИзмененоВручную Тогда
		СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
			ДеревоЗначения,
			"ИСТЗ_НаименованиеРучное",
			Истина);
	Иначе
		НайденныйУзел = СцТ_ГлавнаяФорма.СцТ_НайтиУзелДереваФормы(
			ДеревоЗначения,
			"Имя",
			"ИСТЗ_НаименованиеРучное");
			
		Если НайденныйУзел <> Неопределено Тогда
			ДеревоЗначения.ПолучитьЭлементы().Удалить(НайденныйУзел);
		КонецЕсли;
	КонецЕсли;
	
	///////////////////////////////////
	
	// Проверки
	Если  ПустаяСтрока(ЭлементФормы_Имя)
		И ПустаяСтрока(ЭлементФормы_Заголовок) Тогда
		СцТ_СообщитьПользователю(НСтр("ru = 'Не выбран табличный документ.'"),
			Отказ,
			"ЭлементФормы_Имя");
	КонецЕсли;
	
	Если ВариантХраненияТД = 0 Тогда
		// Хранится в виде дерева значений
		Если КоличествоСтрок = 0 ИЛИ КоличествоКолонок = 0 Тогда
			СцТ_СообщитьПользователю(НСтр("ru = 'Не задано количество колонок или количество строк.'"),
			Отказ,
			"КоличествоСтрок");
		КонецЕсли;
		
	ИначеЕсли ВариантХраненияТД = 1 Тогда
		// Хранится в виде файла в двоичных данных
		// Предположительно достаточно проверить заполненность  количества строк и колонок
		
		Если КоличествоСтрок = 0 ИЛИ КоличествоКолонок = 0 Тогда
			СцТ_СообщитьПользователю(НСтр("ru = 'Не загружен файл с эталонной табличным документом'"),
			Отказ,
			"КоличествоСтрок");
		КонецЕсли;
		
	ИначеЕсли ВариантХраненияТД = 2 Тогда
		// Путь к внешнему файлу или параметр с указанием пути
		Если ПустаяСтрока(ИмяВнешнегоФайла) Тогда
			СцТ_СообщитьПользователю(НСтр("ru = 'Не указано имя внешнего файла'"),
			Отказ,
			"ИмяВнешнегоФайла");
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Сохранение данных
	
	// В конечном итоге все данные по новой считываются с формы,
	// что позволит сохранить изменения сделанные пользователем
	
	УзелТД = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ТабличныйДокумент",
		ЭлементФормы_Имя,
		ЭлементФормы_Заголовок,,,,,
		"ТабличныйДокумент");
		
	Если ВариантХраненияТД = 1 Тогда
		// Т.е. загруженный файл. Сохраняем его в буфер, так как дерево будет очищено
		ДД64 = СцТ_ЗначениеВДереве(ДеревоЗначения, "ДвоичныеДанныеТД", "",, Истина);
		
	КонецЕсли;
	
	УзелТД.ПолучитьЭлементы().Очистить();
	
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		УзелТД,
		"ВариантХраненияТД",
		ВариантХраненияТД);
	
	Если ВариантХраненияТД = 0 Тогда
		// Хранение в виде дерева структуры значений
		
		// Запись координат области
		СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(УзелТД, "КоличествоКолонокТД", КоличествоКолонок);
		СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(УзелТД, "КоличествоСтрокТД"  , КоличествоСтрок);
		
		// Чтение данных таблицы
		ОбъединенныеОбласти = Новый Массив;
		
		Для НомерСтроки = 1 По КоличествоСтрок Цикл
			
			АдресСтроки = "R" + Формат(НомерСтроки, "ЧГ=0");
			
			УзелСтроки = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
				УзелТД,
				АдресСтроки,,,,,,,,,
				Ложь);
			
			Для НомерКолонки = 1 По КоличествоКолонок Цикл
				
				АдресЯчейки = СцТ_ГлавнаяФорма.СцТ_АдресЯчейки(НомерСтроки, НомерКолонки);
				ОбластьЯчеек = ТД.Область(АдресЯчейки);
				
				ИмяТекущейОбласти = ОбластьЯчеек.Имя;
				
				Если ОбъединенныеОбласти.Найти(ИмяТекущейОбласти) = Неопределено Тогда
					// Это левый верхний угол объединенной ячейки
					ТекстЯчейки = СокрЛП(ОбластьЯчеек.Текст);
					
					ОбъединенныеОбласти.Добавить(ИмяТекущейОбласти);
				Иначе
					// Это ячейка в объединении
					ТекстЯчейки = "";
				КонецЕсли;
				
				АдресКолонки = "C" + Формат(НомерКолонки, "ЧГ=0");
				
				СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
					УзелСтроки,
					АдресКолонки,
					ТекстЯчейки);
			КонецЦикла;
		КонецЦикла;
		
	ИначеЕсли ВариантХраненияТД = 1 Тогда
		// хранение файла
		СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
			УзелТД,
			"ДвоичныеДанныеТД",
			ДД64);
		
	Иначе
		СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
			УзелТД,
			"ФайлТабличногоДокумента",
			ИмяВнешнегоФайла);
		
	КонецЕсли;
	
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ВариантПоискаОбъекта",
		ВариантПоискаОбъекта);
		
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелПутиПоискаОбъекта(
			УзелТД,
			ПутьКЭлементуФормы);
	
	
	//////////////////////////////////
	
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ДействиеПриОшибке",
		ДействиеПриОшибке);
		
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ПеременнаяПриОшибке",
		ПеременнаяПриОшибке);
	
КонецПроцедуры

// Свертка данных шага в структуру
// и подготовка возвращаемой структуры отредактированного шага
&НаКлиенте
Функция СцТ_СформироватьСтруктуруданныхШага_НаКлиенте()
	
	// СДШ - структура данных шага
	СДШ = Новый Структура;
	
	СДШ.Вставить("ИдентификаторШага"           , ОА_ОписательПоложенияШагаВДереве.ИдентификаторШага);
	СДШ.Вставить("НаименованиеШага"            , ДШ_Наименование);
	СДШ.Вставить("ТипМетаданных"               , ДШ_ТипМетаданных);
	СДШ.Вставить("ИмяМетаданных"               , ДШ_ИмяМетаданных);
	СДШ.Вставить("Автоописание"                , ДШ_Автоописание);
	СДШ.Вставить("Описание"                    , ДШ_Описание);
	СДШ.Вставить("СкрытьАвтоописание"          , ДШ_СкрытьАвтоописание);
	СДШ.Вставить("ОбСсылка"                    , ДШ_ОбСсылка);
	СДШ.Вставить("ПредставлениеОбъекта"        , ДШ_ПредставлениеОбъекта);
	СДШ.Вставить("ВыполнитьВручную"            , ДШ_ВыполнитьВручную);
	СДШ.Вставить("Комментарий"                 , ДШ_Комментарий);
	СДШ.Вставить("ИмяФормы"                    , ДШ_ИмяФормы);
	СДШ.Вставить("ПредставлениеИмениМетаданных", ДШ_ПредставлениеИмениМетаданных);
	СДШ.Вставить("ЗаголовокФормы"              , ДШ_ЗаголовокФормы);
	СДШ.Вставить("ДанныеКартинки"              , ДШ_ДанныеКартинки);
	СДШ.Вставить("ДеревоЗначения"              , СцТ_ГлавнаяФорма.СцТ_ДеревоформыВДеревоСтруктур(ДеревоЗначения));
	
	
	// ВСД - возвращаемая структура данных
	ВСД = Новый Структура;
	ВСД.Вставить("ОписательПоложенияШагаВДереве", ОА_ОписательПоложенияШагаВДереве);
	ВСД.Вставить("Наименование"                 , ДШ_Наименование);
	ВСД.Вставить("Активность"                   , ОА_Активность);
	ВСД.Вставить("СтруктураДанныхШага"          , СДШ);
	ВСД.Вставить("ЭтоНовый"                     , ОА_ЭтоНовый);
	ВСД.Вставить("ТипШага"                      , ОА_ТипШага);
	
	Возврат ВСД;
	
КонецФункции

#КонецОбласти

#Область ФормированеОписанийИНаименований

// Формирование описания шага на клиенте
&НаКлиенте
Функция СцТ_СформироватьОписаниеШага_НаКлиенте(Краткое = Ложь)
	
	ВозвращаемоеОписание = НСтр("ru = 'Табличный документ%1'");
	
	УзелТаблДок = СцТ_ГлавнаяФорма.СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ТабличныйДокумент");
	Если УзелТаблДок = Неопределено Тогда
		ПредставлениеТаблДок = "";
	Иначе
		ПредставлениеТаблДок = " """
			+ ?(ПустаяСтрока(УзелТаблДок.Синоним), УзелТаблДок.Значение, УзелТаблДок.Синоним)
			+ """";
	КонецЕсли;
	
	ВозвращаемоеОписание = СтрЗаменить(ВозвращаемоеОписание, "%1", ПредставлениеТаблДок);
	
	Если Краткое Тогда
		Возврат ВозвращаемоеОписание;
	КонецЕсли;
	
	Если ВариантХраненияТД = 0 Тогда
		
		Дополнение = НСтр("ru = '. Эталонное значение табличного документа хранится в шаге'");
		
	ИначеЕсли ВариантХраненияТД = 1 Тогда
		// Хранится в виде файла в двоичных данных
		
		Дополнение = НСтр("ru = '. Эталонное значение хранится в виде двоичных данных'");
		
	ИначеЕсли ВариантХраненияТД = 2 Тогда
		// Хранится путь к внешнему файлу
		Если Лев(ИмяВнешнегоФайла, 1) = "&" Тогда
			Дополнение = НСтр("ru = '. Эталонное значение хранится во внешнем файле, путь к которому хранится в переменной ""%1""'");
			Дополнение = СтрЗаменить(Дополнение, "%1", Сред(ИмяВнешнегоФайла, 2));
		Иначе
			Дополнение = НСтр("ru = '. Эталонное значение хранится во внешнем файле ""%1""'");
			Дополнение = СтрЗаменить(Дополнение, "%1", ИмяВнешнегоФайла);
		КонецЕсли;
		
	КонецЕсли;
	
	ВозвращаемоеОписание = ВозвращаемоеОписание
		+ Дополнение;
	
	Возврат ВозвращаемоеОписание;
	
КонецФункции

&НаКлиенте
Процедура СцТ_ОбновитьНаименование_НаКлиенте(Принудительно = Ложь)
	
	Если НЕ НаименованиеШагаИзмененоВручную ИЛИ Принудительно Тогда
		//ДШ_Наименование = СцТ_ГлавнаяФорма.СцТ_ПолучитьОписаниеШага_НайтиФорму("", Истина);
	КонецЕсли;
	
	Если Принудительно Тогда
		НаименованиеШагаИзмененоВручную = Ложь;
		Модифицированность = Истина;
		// В остальных случаях менять модифицированность не нужно,
		// так как ее должно изменить изменение другого элемента формы
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСДеревомЗначения

// Возвращает значение заданного поля в дереве данных
//
// Параметры
//	ДерЗнач              - дерево с данными в котором ведется поиск
//	ИмяПараметра         - имя искомого параметра
//	ЕслиНеНайдено        - что вернуть, если значение не найдено
//	ВозвращаемыйРеквизит - имя колонки, которую нужно вернуть
//	Рекурсивно           - признак рекурсии
// Возвращаемое значение - значение нужного поля найденного узла
&НаСервереБезКонтекста
Функция СцТ_ЗначениеВДереве(
	Знач ДерЗнач,
	Знач ИмяПараметра,
	Знач ЕслиНеНайдено = Неопределено,
	ВозвращаемыйРеквизит = "Значение",
	Рекурсивно = Истина)
	
	УзелЗначения = СцТ_НайтиУзелДереваФормы(ДерЗнач, "Имя", ИмяПараметра, Рекурсивно);
	Если УзелЗначения = Неопределено Тогда
		Возврат ЕслиНеНайдено;
	Иначе
		Возврат УзелЗначения[ВозвращаемыйРеквизит];
	КонецЕсли;
	
КонецФункции

// Поиск нужного узла в дереве данных
//
// Параметры
//	ДеревоФормы       - дерево, в котором ведется поиск
//	Реквизит          - имя поля в котором ведется поиск
//	ЗначениеРеквизита - значение в поле
//	Рекурсивно        - признак рекурсивного поиска
// Возвращаемое значение - найденный узел
&НаСервереБезКонтекста
Функция СцТ_НайтиУзелДереваФормы(
	ДеревоФормы,
	Реквизит,
	ЗначениеРеквизита,
	Рекурсивно = Ложь)
	
	Если ДеревоФормы = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого Узел Из ДеревоФормы.ПолучитьЭлементы() Цикл
		Если Узел[Реквизит] = ЗначениеРеквизита Тогда
			Возврат Узел;
		КонецЕсли;
		Если Рекурсивно Тогда
			НайденыйУзел = СцТ_НайтиУзелДереваФормы(Узел, Реквизит, ЗначениеРеквизита, Истина);
			Если НайденыйУзел <> Неопределено Тогда
				Возврат НайденыйУзел;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Заполнение дерева значения данными из структуры
//
// параметры
//	ДеревоФормы - заполняемое дерево
//	ДеревоСтруктур - структура с данными
&НаСервереБезКонтекста
Процедура СцТ_ЗаполнитьДеревоФормыИзДереваСтруктур(ДеревоФормы, ДеревоСтруктур)
	
	СтрокиДереваФормы = ДеревоФормы.ПолучитьЭлементы();
	Для каждого СтрокаДереваСтруктур Из ДеревоСтруктур.ИСТЗ_Строки Цикл
		СтрокаДереваФормы = СтрокиДереваФормы.Добавить();
		Для каждого КлючЗначение Из СтрокаДереваСтруктур Цикл
			Если КлючЗначение.Ключ <> "ИСТЗ_Строки" Тогда
				СтрокаДереваФормы[КлючЗначение.Ключ] = КлючЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
		СцТ_ЗаполнитьДеревоФормыИзДереваСтруктур(СтрокаДереваФормы, СтрокаДереваСтруктур);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПреобразоватьЗначенияВДереве(ВеткаДерева, ДляОтображения = Истина)
	
	Для каждого СтрокаДерева Из ВеткаДерева.Строки Цикл
		
		Если ДляОтображения Тогда
			СтрокаДерева.Значение = ЗначениеИзСтрокиВнутр(СтрокаДерева.Значение);
		Иначе
			СтрокаДерева.Значение = ЗначениеВСтрокуВнутр(СтрокаДерева.Значение);
		КонецЕсли;
		
		ПреобразоватьЗначенияВДереве(СтрокаДерева, ДляОтображения);
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

// Обновление картинок на закладках
// Вызывается посредством обработки ожидания, чтобы форма нормально закрывалась после редактирования
&НаКлиенте
Процедура СцТ_ОбновитьЗаголовкиЗакладок()
	
	Элементы.Страница_РедактируемоеОписание.Картинка = ?(
		ПустаяСтрока(ДШ_Описание),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
	Элементы.Страница_Комментарий.Картинка = ?(
		ПустаяСтрока(ДШ_Комментарий),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
КонецПроцедуры

// Отображение родительского шага
// Параметры:
// ПриОткрытии - устанавливает признак, что настройка выполняется при открытии
// или (в противном случае) по обработке оповещения
&НаКлиенте
Процедура СцТ_СформироватьПредставлениеРодителя(ПриОткрытии = Истина) Экспорт
	
	Если ОА_ЭтоНовый И ПриОткрытии Тогда
		Если ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя = Неопределено Тогда
			ПредставлениеГруппы = "";
		Иначе
			// Поиск данных родительского узла в дереве по идентификатору шага
			СцТ_РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,
				ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
			
			Если СцТ_РодительУзла = Неопределено Тогда
				ПредставлениеГруппы = "";
			Иначе
				ОтобразитьПредставлениеСуществующегоРодительскогоШага(СцТ_РодительУзла);
			КонецЕсли;
		КонецЕсли;
		
		// Если родительский узел будет изменен у нового шага, то редактируемый шаг уже не имеет смысла,
		// так как неизвестно куда его вставить
		
	ИначеЕсли НЕ ОА_ЭтоНовый Тогда
		Если ДанныеШага = Неопределено Тогда
			ПредставлениеГруппы = "";
		Иначе
			СцТ_РодительУзла = ДанныеШага.ПолучитьРодителя();
			Если СцТ_РодительУзла = Неопределено Тогда
				ПредставлениеГруппы = "";
			Иначе
				ОтобразитьПредставлениеСуществующегоРодительскогоШага(СцТ_РодительУзла);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПредставлениеГруппы = "" Тогда
		Элементы.СтраницыГрупп.ТекущаяСтраница = Элементы.СтраницаПустойГруппы;
	Иначе
		Элементы.СтраницыГрупп.ТекущаяСтраница = Элементы.СтраницаЗаполненнойГруппы;
	КонецЕсли;
	
КонецПроцедуры

// Формирование представление существующего шага
// параметры
// РодительскийШаг - указатель на данные родительского шага.
// НЕ может быть Неопределено
&НаКлиенте
Процедура ОтобразитьПредставлениеСуществующегоРодительскогоШага(РодительскийШаг)
	
	ПредставлениеГруппы = НСтр("ru = 'Шаг №%1 ""%2""'");
	ПредставлениеГруппы = СтрЗаменить(
		ПредставлениеГруппы,
		"%1",
		Формат(РодительскийШаг.НомерШага, "ЧГ=0"));
	
	ПредставлениеГруппы = СтрЗаменить(
		ПредставлениеГруппы,
		"%2",
		РодительскийШаг.Наименование);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностьюПеременнойОшибкиГрупповогоШага(УпрФорма)
	
	Если УпрФорма.ДействиеПриОшибке = "Остановить" Тогда
		УпрФорма.Элементы.СтраницыПеременнойОшибки.ТекущаяСтраница = УпрФорма.Элементы.СтраницыПеременнойОшибки_Пустая;
	Иначе
		УпрФорма.Элементы.СтраницыПеременнойОшибки.ТекущаяСтраница = УпрФорма.Элементы.СтраницыПеременнойОшибки_Заполненная;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОбъектаМетаданных(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДШ_ИмяМетаданных = Результат.Значение;
	ДШ_ПредставлениеИмениМетаданных = Результат.Представление;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьПользовательскоеОписание()
	
	ТекстОписания = ДШ_Описание;
	
	Если НЕ ДШ_СкрытьАвтоописание Тогда
		ДШ_Автоописание = СцТ_СформироватьОписаниеШага_НаКлиенте();
		ТекстОписания = СцТ_ГлавнаяФорма.СцТ_ДобавитьПодстроку(ТекстОписания, ДШ_Автоописание, Символы.ПС);
	КонецЕсли;
	
	ПользовательскоеОписание = ТекстОписания;
	
КонецПроцедуры

&НаКлиенте
Процедура ДШ_СкрытьАвтоописаниеПриИзменении(Элемент)
	
	ОбновитьПользовательскоеОписание();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элемент.ТекущаяСтраница = Элементы.Страница_ОписаниеШага Тогда
		ОбновитьПользовательскоеОписание();
	КонецЕсли;
	
КонецПроцедуры


// Выполняет обработку выбора типа добавляемых элементов, по которым требуется
// искать форму в шаге "Найти форму"
//
// Параметры
//	ЭлементОписанияДобавляемых - выбранный элемент списка элементов;
//	ДопДанные - дополнительные данные
//
&НаКлиенте
Процедура СцТ_ОбработатьВыборТипаДобавляемыхЭлементовШагаНайтиФорму(ДобавляемыйЭлемент, ДопДанные) Экспорт
	
	Если ДобавляемыйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВидимостьКолонок = Новый Соответствие;
	ВидимостьКолонок["ДеревоОбъектовЗначение"] = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗапретитьВыборПодсвеченных", Истина);
	
	
	//ВладелецОбъектов,
	//Знач ТипыВыбираемыхОбъектов        = Неопределено,
	//Знач ТипыОтображаемыхОбъектов      = Неопределено,
	//ТекущиеЭлементы                    = Неопределено,
	//МножественныйВыбор                 = Ложь,
	//ВладелецФормыВыбора                = Неопределено,
	//ЗаголовокФормыВыбораЭлементов      = "",
	//Автоматически                      = Ложь,
	//НеВыбиратьПодчиненныеИзТаблиц      = Ложь,
	//ПодсвечиваемыеПриВыборе            = Неопределено,
	//ВидимостьКолонок                   = Неопределено,
	//ДополнительныеПараметры            = Неопределено,
	//ОписаниеОповещенияОЗакрытииФормы = Неопределено) Экспорт
	
КонецПроцедуры


// Получение контейнера тестируемого приложения
// Привязка
//
&НаКлиенте
Процедура СцТ_ПолучитьИнтерактивныйКонтейнер()
	
	// Так как шаг может быть еще не записан, то удобно получить форму для родительского шага
	Если ОА_ЭтоНовый Тогда
		РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
	Иначе
		РодительУзла = ДанныеШага.ПолучитьРодителя();
	КонецЕсли;
	
	// Сообщения об ошибках показывать не нужно, так как редактирование шага может
	// выполняться без использования тестируемой формы
	ОписаниеОшибкиПолученияКонтейнера = "";
	ИнтерактивныйКонтейнер = СцТ_ГлавнаяФорма.СцТ_ПолучитьИнтерактивныйКонтейнерДляШагаСценария(
		РодительУзла,
		ОписаниеОшибкиПолученияКонтейнера,
		Истина);
	
	Если НЕ ПустаяСтрока(ОписаниеОшибкиПолученияКонтейнера) Тогда
		ИнтерактивныйКонтейнер = Неопределено;
		//ПоказатьПредупреждение(, СцТ_ГлавнаяФорма.СцТ_РасшифроватьОшибку(ОписаниеОшибкиПолученияКонтейнера));
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ИнтерактивныйКонтейнер) <> Тип("ТестируемаяФорма") Тогда
		ИнтерактивныйКонтейнер = Неопределено;
		//ТП = НСтр("ru = 'Не найдена тестируемая форма'");
		//ПоказатьПредупреждение(, ТП);
		Возврат;
	КонецЕсли;
	
	// Проверить, открыта ли форма
	Попытка
		ФормаЗакрыта = ИнтерактивныйКонтейнер.ОжидатьЗакрытие(0);
	Исключение
		ФормаЗакрыта = Истина;
	КонецПопытки;
	
	Если ФормаЗакрыта Тогда
		ИнтерактивныйКонтейнер = Неопределено;
		//ПоказатьПредупреждение(, НСтр("ru = 'Не удалось обратиться к форме тестируемого приложения:
		//	|форма была закрыта, либо тестируемое приложение не запущено.'"));
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьАктивностьВыделенныхСтрок(Команда)
	
	УстановитИлиСброситьФлагиАктивности(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьАктивность(Команда)
	
	УстановитИлиСброситьФлагиАктивности(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитИлиСброситьФлагиАктивности(ЗначениеФлага)
	
	
КонецПроцедуры



&НаСервере
Процедура ЗаполнитьПутьКЭлементуФормы(РодительскийУзелПути, ДанныеПутиВФорме = Неопределено)
	
	Если ДанныеПутиВФорме = Неопределено Тогда
		ДанныеПутиВФорме = ПутьКЭлементуФормы;
	КонецЕсли;
	
	УзелПутиВДереве = СцТ_НайтиУзелДереваФормы(РодительскийУзелПути, "Имя", "ИСТЗ_ПутьКОбъекту");
	
	Если УзелПутиВДереве = Неопределено Тогда
		ДанныеПутиВФорме = Неопределено;
		Возврат;
	КонецЕсли;
	
	ДанныеПутиВФорме = Новый Структура;
	Счетчик = 1;
	
	Для каждого УзелЭлементаПути Из УзелПутиВДереве.Получитьэлементы() Цикл
		
		СтруктураСтроки = Новый Структура;
		СтруктураСтроки.Вставить("ИмяОбъекта"      , УзелЭлементаПути.Имя);
		СтруктураСтроки.Вставить("ЗаголовокОбъекта", УзелЭлементаПути.Синоним);
		
		// Для совместимости с предыдущими версиями
		Если ЗначениеЗаполнено(УзелЭлементаПути.ТипУзла) Тогда
			СтруктураСтроки.Вставить("ТипОбъекта", УзелЭлементаПути.ТипУзла);
		Иначе
			Если ТипЗнч(УзелЭлементаПути.Значение) = Тип("Число") Тогда
				Если УзелЭлементаПути.Значение = 0 Тогда
					НовыйТип = "ТестируемоеПриложение";
				ИначеЕсли УзелЭлементаПути.Значение = 1 Тогда
					НовыйТип = "ТестируемоеОкноКлиентскогоПриложения";
				ИначеЕсли УзелЭлементаПути.Значение = 2 Тогда
					НовыйТип = "ТестируемаяФорма";
				ИначеЕсли УзелЭлементаПути.Значение = 3 Тогда
					НовыйТип = "ТестируемоеПолеФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 4 Тогда
					НовыйТип = "ТестируемаяГруппаФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 5 Тогда
					НовыйТип = "ТестируемаяКнопкаФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 6 Тогда
					НовыйТип = "ТестируемаяДекорацияФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 7 Тогда
					НовыйТип = "ТестируемаяТаблицаФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 8 Тогда
					НовыйТип = "ТестируемыйКомандныйИнтерфейсОкна";
				ИначеЕсли УзелЭлементаПути.Значение = 9 Тогда
					НовыйТип = "ТестируемаяГруппаКомандногоИнтерфейса";
				ИначеЕсли УзелЭлементаПути.Значение = 10 Тогда
					НовыйТип = "ТестируемаяКнопкаКомандногоИнтерфейса";
				Иначе
					НовыйТип = "";
				КонецЕсли;
			КонецЕсли;
			
			СтруктураСтроки.Вставить("ТипОбъекта", НовыйТип);
		КонецЕсли;
		
		ИмяСтруктурыСтроки = "Str_" + Формат(Счетчик, "ЧГ=0");
		ДанныеПутиВФорме.Вставить(ИмяСтруктурыСтроки, СтруктураСтроки);
		
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПоиска(Команда)
	
	Если ПустаяСтрока(ЭлементФормы_Имя) И ПустаяСтрока(ЭлементФормы_Заголовок) Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выбран элемент формы'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ЗапретДоступаКПути = (ПутьКЭлементуФормы <> Неопределено);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВариантПоискаЭлемента"            , ВариантПоискаОбъекта);
	ПараметрыФормы.Вставить("ТипЭлемента"                      , "ТестируемоеПолеФормы");
	ПараметрыФормы.Вставить("ВидЭлемента"                      , "ТабличныйДокумент");
	ПараметрыФормы.Вставить("ИмяЭлемента"                      , ЭлементФормы_Имя);
	ПараметрыФормы.Вставить("ЗаголовокЭлемента"                , ЭлементФормы_Заголовок);
	ПараметрыФормы.Вставить("ЗапретДоступаКПути"               , ЗапретДоступаКПути);
	ПараметрыФормы.Вставить("ЗапретДоступаКПути"               , ЗапретДоступаКПути);
	ПараметрыФормы.Вставить("ПутьКЭлементуФормы"               , ПутьКЭлементуФормы);
	ПараметрыФормы.Вставить("ФормаОткрытаПодВнешнимУправлением", ФормаОткрытаПодВнешнимУправлением);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СцТ_ПослеЗакрытияФормыСпособаПоискаЭлементовФормы", ЭтотОбъект, Ложь);
	
	ФормаПравилаПоиска = ОткрытьФорму(ПутьКФормам + "СпособПоискаЭлементаФормы",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОповещениеОЗакрытии);
		
	ФормаПравилаПоиска.СцТ_ГлавнаяФорма = СцТ_ГлавнаяФорма;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_ПослеЗакрытияФормыСпособаПоискаЭлементовФормы(РезультатОкна, ДопПараметры) Экспорт
	
	Если ТипЗнч(РезультатОкна) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ВариантПоискаОбъекта   = РезультатОкна.ВариантПоискаЭлемента;
	ЭлементФормы_Имя       = РезультатОкна.ИмяЭлемента;
	ЭлементФормы_Заголовок = РезультатОкна.ЗаголовокЭлемента;
	ПутьКЭлементуФормы     = РезультатОкна.ПутьКЭлементуФормы;
	ЭлементФормы_Вид       = РезультатОкна.ВидЭлемента;
	
	//Если ПустаяСтрока(ИмяИскомогоОбъекта) Тогда
	//	ИмяИскомогоОбъекта = "$" + ЗаголовокИскомогоОбъекта;
	//ИначеЕсли ПустаяСтрока(ЗаголовокИскомогоОбъекта) Тогда
	//	ЗаголовокИскомогоОбъекта = ИмяИскомогоОбъекта;
	//КонецЕсли;
	
	//// Изменить имя и заголовок объекта
	//Если ТипШага = "ЗаполнитьОдинРеквизитФормы" ИЛИ ТипШага = "ЗаполнитьКолонкуТаблицы" Тогда
	//	
	//	УзелПоля = ДеревоЗначения.ПолучитьЭлементы()[0].ПолучитьЭлементы()[0];
	//	УзелПоля.Имя     = ИмяИскомогоОбъекта;
	//	УзелПоля.Синоним = ЗаголовокИскомогоОбъекта;
	//	
	//ИначеЕсли ТипШага = "ЗаполнитьРеквизитыФормы" ИЛИ ТипШага = "ЗаполнитьСтрокуТаблицы" Тогда
	//	
	//	ТекДанные = Элементы.ТаблицаРеквизитов.ТекущиеДанные;
	//	Если ТекДанные = Неопределено Тогда
	//		Возврат;
	//	КонецЕсли;
	//	
	//	ТекДанные.ДополнительныеДанные.ВариантПоискаОбъекта = ПравилоПоиска.ВариантПоискаОбъекта;
	//	ТекДанные.Имя     = ИмяИскомогоОбъекта;
	//	ТекДанные.Синоним = ЗаголовокИскомогоОбъекта;
	//	
	//	ПутьКОбъекту = Неопределено;
	//	Если ТекДанные.ДополнительныеДанные.Свойство("ПутьКОбъекту", ПутьКОбъекту) Тогда
	//		Если ПутьКОбъекту <> Неопределено И ПутьКОбъекту.Количество() > 0 Тогда
	//			ПоследнийЭлементПоискаПути = ПутьКОбъекту[ПутьКОбъекту.ВГраница()];
	//			ПоследнийЭлементПоискаПути.ИмяОбъекта       = ИмяИскомогоОбъекта;
	//			ПоследнийЭлементПоискаПути.ЗаголовокОбъекта = ЗаголовокИскомогоОбъекта;
	//		КонецЕсли;
	//	КонецЕсли;
	//	
	//	Возврат;
	//	
	//ИначеЕсли ТипШага = "НажатьКнопку" Тогда
	//	
	//	УзелИмениКнопки = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ИмяКнопки");
	//	УзелИмениКнопки.Значение = ИмяИскомогоОбъекта;
	//	УзелИмениКнопки.Синоним  = ЗаголовокИскомогоОбъекта;
	//	
	//ИначеЕсли ТипШага = "НайтиЭлементФормы" Тогда
	//	
	//	УзелЭлемента = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ИмяОбъекта");
	//	Если УзелЭлемента <> Неопределено Тогда
	//		УзелЭлемента.Значение = ИмяИскомогоОбъекта;
	//		УзелЭлемента.Синоним  = ЗаголовокИскомогоОбъекта;
	//	КонецЕсли;
	//	
	//	СцТ_ОбновитьАвтоОписаниеШага_НаКлиенте(Истина);
	//	
	//ИначеЕсли ТипШага = "НажатьКнопкуКИ" Тогда
	//	
	//	УзелОписанияКнопки = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ОписаниеКнопкиКИ");
	//	УзелОписанияКнопки.Синоним = ЗаголовокИскомогоОбъекта;
	//	
	//ИначеЕсли ТипШага = "АвтоГруппаТаблицаФормы" Тогда
	//	
	//	Если ПустаяСтрока(ПредставлениеТаблицы) Тогда
	//		Сообщение = Новый СообщениеПользователю;
	//		Сообщение.Текст = НСтр("ru = 'Не выбрана таблица формы.'");
	//		Сообщение.Поле = "ПредставлениеТаблицы";
	//		Сообщение.Сообщить();
	//		Возврат;
	//	КонецЕсли;
	//	
	//	УзелОписанияТаблицы = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ОписаниеТаблицы");
	//	УзелОписанияТаблицы.Значение = ИмяИскомогоОбъекта;
	//	УзелОписанияТаблицы.Синоним  = ЗаголовокИскомогоОбъекта;
	//	
	//Иначе
	//	
	//	Возврат;
	//	
	//КонецЕсли;
	
	/////////////////////
	
	//Если ПутьПоискаОбъекта <> Неопределено И ПутьПоискаОбъекта.Количество() > 0 Тогда
	//	ПоследнийЭлементПоискаПути = ПутьПоискаОбъекта[ПутьПоискаОбъекта.ВГраница()];
	//	ПоследнийЭлементПоискаПути.ИмяОбъекта = ЭлементФормы_Имя;
	//	ПоследнийЭлементПоискаПути.ЗаголовокОбъекта = ЭлементФормы_Заголовок;
	//КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКнопкуКИ(Команда)
	
	//Если НЕ СцТ_ГлавнаяФорма.СцТ_ТестируемоеПриложениеАктивно() Тогда
	//	ПоказатьПредупреждение(, НСтр("ru = 'Тестируемый клиент не запущен. Выбор кнопок командного интерфейса недоступен.'"));
	//	Возврат;
	//КонецЕсли;
	//
	//Если ОА_ЭтоНовый Тогда
	//	Если ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя <> Неопределено Тогда
	//		РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
	//	Иначе
	//		РодительУзла = Неопределено;
	//	КонецЕсли;
	//Иначе
	//	РодительУзла = ДанныеШага.ПолучитьРодителя();
	//КонецЕсли;
	
	СцТ_ПолучитьИнтерактивныйКонтейнер();
	
	Если ИнтерактивныйКонтейнер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТекущегоЭлементаФормы = Новый Структура;
	
	ОписаниеТекущегоЭлементаФормы.Вставить("Вид"      , ЭлементФормы_Тип);
	ОписаниеТекущегоЭлементаФормы.Вставить("Заголовок", ЭлементФормы_Заголовок);
	ОписаниеТекущегоЭлементаФормы.Вставить("Имя"      , ЭлементФормы_Имя);
	
	// Тип выбираемых объектов определяется типом шага
	ТипыВыбираемыхОбъектов = Новый СписокЗначений;
	ТипыОтображаемых       = Новый СписокЗначений;
	
	ТипыВыбираемыхОбъектов.Добавить("ПолеТабличногоДокумента");
	
	ТипыОтображаемых.Добавить("Страница");
	ТипыОтображаемых.Добавить("ОбычнаяГруппа");
	
	ОбработчикОповещения = Новый ОписаниеОповещения("СцТ_ПослеВыбораТекстируемогоЭлементаФормы", ЭтотОбъект);
	
	СцТ_ГлавнаяФорма.СцТ_ВыбратьОбъектыТестируемогоПриложения(
		ИнтерактивныйКонтейнер,
		ТипыВыбираемыхОбъектов,
		ТипыОтображаемых,
		ОписаниеТекущегоЭлементаФормы,,
		ЭтотОбъект,,,,,,,
		ОбработчикОповещения);
	
КонецПроцедуры


&НаКлиенте
Процедура СцТ_ПослеВыбораТекстируемогоЭлементаФормы(РезультатФормы, ДопПараметры) Экспорт
	
	Если ТипЗнч(РезультатФормы) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементФормы_Заголовок = РезультатФормы.Заголовок;
	ЭлементФормы_Имя       = РезультатФормы.Имя;
	
	Если РезультатФормы.Вид = "ПолеТабличногоДокумента" Тогда
		ЭлементФормы_Вид = "ТабличныйДокумент";
	Иначе
		ЭлементФормы_Вид    = РезультатФормы.Вид;
	КонецЕсли;
	
	ПутьКЭлементуФормы = Новый Структура;
	НомерСтроки = 1;
	Для каждого СтруктураМассива Из РезультатФормы.ПутьКОбъекту Цикл
		ИмяСтруктурыСтроки = "Str_" + Формат(НомерСтроки, "ЧГ=0");
		
		// Тип объекта не сериализуется на сервере
		Если СтруктураМассива.ТипОбъекта = Тип("ТестируемаяГруппаКомандногоИнтерфейса") Тогда
			НовыйТип = "ТестируемаяГруппаКомандногоИнтерфейса";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяГруппаФормы") Тогда
			НовыйТип = "ТестируемаяГруппаФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяДекорацияФормы") Тогда
			НовыйТип = "ТестируемаяДекорацияФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяКнопкаКомандногоИнтерфейса") Тогда
			НовыйТип = "ТестируемаяКнопкаКомандногоИнтерфейса";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяКнопкаФормы") Тогда
			НовыйТип = "ТестируемаяКнопкаФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
			НовыйТип = "ТестируемаяТаблицаФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемоеПолеФормы") Тогда
			НовыйТип = "ТестируемоеПолеФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяФорма") Тогда
			НовыйТип = "ТестируемаяФорма";
		Иначе
			Продолжить;
		КонецЕсли;
		СтруктураМассива.ТипОбъекта = НовыйТип;
		
		ПутьКЭлементуФормы.Вставить(ИмяСтруктурыСтроки, СтруктураМассива);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	
	//СцТ_ОбновитьАвтоОписаниеШага_НаКлиенте(Истина);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоДаннымФормы(Команда)
	
	СцТ_ПолучитьИнтерактивныйКонтейнер();
	Если ИнтерактивныйКонтейнер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ИнтерактивныйКонтейнер.Активизировать();
	Исключение
		Возврат;
	КонецПопытки;
	
	// попытка найти нужный элемент формы
	ПолеФормы = ИнтерактивныйКонтейнер.ПолучитьТекущийЭлемент();
	
	Если ПолеФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваОбъекта = СцТ_ГлавнаяФорма.СцТ_АтрибутыДоступностиИнтерактивногоОбъекта(ПолеФормы);
	
	Попытка
		СцТ_ТекстПодсказки = ПолеФормы.ПолучитьТекстПодсказки();
	Исключение
		СцТ_ТекстПодсказки = "";
	КонецПопытки;
	
	СвойстваОбъекта.Вставить("Заголовок"     , ПолеФормы.ТекстЗаголовка);
	СвойстваОбъекта.Вставить("ТекстПодсказки", СцТ_ТекстПодсказки);
	
	МассивОтредактированных = Новый Массив;
	СтруктураОтбора = Новый Структура("Имя", Неопределено);
	
	
КонецПроцедуры

&НаСервере
Процедура СцТ_ОбновитьТаблДокПоДаннымФормы_НаСервере(ИмяОбластиТаблДок, ИмяРеквизита = "ТД_Данные", ИмяУзлаТабличногоДокумента = "ДанныеТабличногоДокумента")
	
	ЭтотОбъект[ИмяРеквизита].Очистить();
	
	ЭтотОбъект[ИмяРеквизита].Область().РазмещениеТекста = ТипРазмещенияТекстаТабличногоДокумента.Переносить;
	
	УзелДанныхТаблДок = СцТ_НайтиУзелДереваФормы(
		ДеревоЗначения,
		"Имя",
		ИмяУзлаТабличногоДокумента);
	
	Если УзелДанныхТаблДок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИндексСтр = 0;
	Для каждого УзелСтр Из УзелДанныхТаблДок.ПолучитьЭлементы() Цикл
		Если Лев(УзелСтр.Имя, 1) <> "R" Тогда
			Продолжить;
		КонецЕсли;
		ИндексСтр  = ИндексСтр + 1;
		ИндексСтлб = 0;
		Для каждого УзелСтлб Из УзелСтр.ПолучитьЭлементы() Цикл
			ИндексСтлб = ИндексСтлб + 1;
			Если НЕ ПустаяСтрока(УзелСтлб.Значение) Тогда
				Обл = ЭтотОбъект[ИмяРеквизита].Область(ИндексСтр, ИндексСтлб, ИндексСтр, ИндексСтлб);
				Обл.Текст = УзелСтлб.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	УзелОбъединений = СцТ_НайтиУзелДереваФормы(
		ДеревоЗначения,
		"Имя",
		"ОбъединенныеОбласти");
	
	Если УзелОбъединений <> Неопределено Тогда
		
		Попытка
			КоординатыОбласти = СцТ_КоординатыОбласти(ИмяОбластиТаблДок);
			ОбластьОК = Истина;
		Исключение
			ОбластьОК = Ложь;
		КонецПопытки;
		
		Если ОбластьОК Тогда
			Для каждого УзелОбъединения Из УзелОбъединений.ПолучитьЭлементы() Цикл
				
				КоординатыОбъединения = СцТ_КоординатыОбласти(УзелОбъединения.Имя);
				КоординатыОбъединения.НачальнаяСтрока  =
					КоординатыОбъединения.НачальнаяСтрока - КоординатыОбласти.НачальнаяСтрока + 1;
				КоординатыОбъединения.НачальнаяКолонка =
					КоординатыОбъединения.НачальнаяКолонка - КоординатыОбласти.НачальнаяКолонка + 1;
				КоординатыОбъединения.КонечнаяСтрока   =
					КоординатыОбъединения.КонечнаяСтрока - КоординатыОбласти.НачальнаяСтрока + 1;
				КоординатыОбъединения.КонечнаяКолонка  =
					КоординатыОбъединения.КонечнаяКолонка - КоординатыОбласти.НачальнаяКолонка + 1;
				
				ОблОбъединения = ЭтотОбъект[ИмяРеквизита].Область(
					КоординатыОбъединения.НачальнаяСтрока,
					КоординатыОбъединения.НачальнаяКолонка,
					КоординатыОбъединения.КонечнаяСтрока,
					КоординатыОбъединения.КонечнаяКолонка);
				
				ОблОбъединения.Объединить();
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ПрочитатьДанныеЗагруженногоТД()
	
	ДД64 = СцТ_ЗначениеВДереве(ДеревоЗначения, "ДвоичныеДанныеТД", "",, Истина);
	
	Если НЕ ЗаполнитьОтображениеХранимогоТД(ДД64, ТД) Тогда
		#Если НЕ Сервер Тогда
			ТекстПредупреждения = НСтр("ru = 'Не удалось прочитать табличный документ. Убедитесь, что файл соответствует формату mxl'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		#КонецЕсли
	КонецЕсли;
	
	КоличествоСтрок   = ТД.Высотатаблицы;
	КоличествоКолонок = ТД.ШиринаТаблицы;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидиомстьюНаСтраницеАвтогруппыТД(Очистить = Истина)
	
	Если ВариантХраненияТД = 0 Тогда
		// Таблица в шаге
		
		СцТ_ОбновитьТаблДокПоДаннымФормы_НаСервере("", "ТД", "ТабличныйДокумент");
		
		КоличествоКолонок = СцТ_ЗначениеВДереве(ДеревоЗначения, "КоличествоКолонокТД", 1,, Истина);
		КоличествоСтрок   = СцТ_ЗначениеВДереве(ДеревоЗначения, "КоличествоСтрокТД"  , 1,, Истина);
		
		Элементы.КоличествоСтрок.Доступность   = Истина;
		Элементы.КоличествоКолонок.Доступность = Истина;
		
		Элементы.ТД.Редактирование = Истина;
		
		Элементы.ТД_НадписьПояснение.Заголовок    = НСтр("ru = 'определить автоматически по границе выделенной области'");
		Элементы.ТД_НадписьПояснение.Гиперссылка  = Истина;
		
		Элементы.СтраницыНастройкиТД.ТекущаяСтраница                  = Элементы.СтраницаНастройкиТД_ТаблицаВШаге;
		Элементы.АвтогруппаТД_СтраницыКоманднойПанели.ТекущаяСтраница = Элементы.АвтогруппаТД_СтраницаОбновить;
		
	ИначеЕсли ВариантХраненияТД = 1 Тогда
		// Двоичные данные в шаге
		
		Элементы.КоличествоСтрок.Доступность   = Ложь;
		Элементы.КоличествоКолонок.Доступность = Ложь;
		
		ПрочитатьДанныеЗагруженногоТД();
		
		Элементы.ТД.Редактирование = Ложь;
		
		Элементы.ТД_НадписьПояснение.Заголовок   = НСтр("ru = '(установлено автоматически по данным хранимого файла)'");
		Элементы.ТД_НадписьПояснение.Гиперссылка = Ложь;
		
		Элементы.СтраницыНастройкиТД.ТекущаяСтраница                  = Элементы.СтраницаНастройкиТД_ТаблицаВШаге;
		Элементы.АвтогруппаТД_СтраницыКоманднойПанели.ТекущаяСтраница = Элементы.АвтогруппаТД_СтраницаПрочитать;
		
	ИначеЕсли ВариантХраненияТД = 2 Тогда
		// внешний файл
		
		Элементы.КоличествоСтрок.Доступность = Ложь;
		Элементы.КоличествоКолонок.Доступность = Ложь;
		
		Если Очистить Тогда
			ТД.Очистить();
		КонецЕсли;
		
		КоличествоСтрок   = 0;
		КоличествоКолонок = 0;
		
		Элементы.ТД_НадписьПояснение.Заголовок = НСтр("ru = '(будет определено по факту в процессе выполнения)'");
		Элементы.ТД_НадписьПояснение.Гиперссылка = Ложь;
		
		Элементы.СтраницыНастройкиТД.ТекущаяСтраница = Элементы.СтраницаНастройкиТД_ВнешнийФайл;
		
	КонецЕсли;
	
КонецПроцедуры

// Определение координат тестируемой области в табличном документе
//
// Параметры
// АдресОбласти - адрес области табличного документа
// Возвращаемое значение - координаты области
&НаКлиентеНаСервереБезКонтекста
Функция СцТ_КоординатыОбласти(Знач АдресОбласти)
	
	АдресОбласти = СокрЛП(ВРег(АдресОбласти));
	
	Результат = Новый Структура("НачальнаяСтрока,НачальнаяКолонка,КонечнаяСтрока,КонечнаяКолонка", 0, 0, 0, 0);
	
	ПозицияДвоеточия = Найти(АдресОбласти, ":");
	Если ПозицияДвоеточия = 0 Тогда
		АдресНачала = АдресОбласти;
		АдресКонца  = АдресОбласти;
	Иначе
		АдресНачала = Лев(АдресОбласти, ПозицияДвоеточия - 1);
		АдресКонца  = Сред(АдресОбласти, ПозицияДвоеточия + 1);
	КонецЕсли;
	
	ПозицияС = Найти(АдресНачала, "C");
	Результат.НачальнаяСтрока  = Число(Сред(АдресНачала, 2, ПозицияС - 2));
	Результат.НачальнаяКолонка = Число(Сред(АдресНачала, ПозицияС + 1));
	
	ПозицияС = Найти(АдресКонца, "C");
	Результат.КонечнаяСтрока  = Число(Сред(АдресКонца, 2, ПозицияС - 2));
	Результат.КонечнаяКолонка = Число(Сред(АдресКонца, ПозицияС + 1));
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьОтображениеХранимогоТД(ДД64, ТД)
	
	ТД.Очистить();
	
	Если НЕ ПустаяСтрока(ДД64) Тогда
		ДД = Base64Значение(ДД64);
		ИмяВрФайла = ПолучитьИмяВременногоФайла("mxl");
		ДД.Записать(ИмяВрФайла);
		
		Попытка
			ТД.Прочитать(ИмяВрФайла);
		Исключение
			Возврат Ложь;
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


// Вывод сообщения пользователю
//
// Параметры
// ТекстСообщения - отображаемый текст
// ПолеДанных - поле, которое должно получить фокус
&НаКлиенте
Процедура СцТ_СообщитьПользователю(ТекстСообщения, Отказ = Неопределено, ПолеДанных = "")
	
	Отказ = Истина;
	Если ПустаяСтрока(ПолеДанных) Тогда
		ПоказатьПредупреждение(, ТекстСообщения);
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Поле  = ПолеДанных;
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантХраненияТДПриИзменении(Элемент)
	
	УправлениеВидиомстьюНаСтраницеАвтогруппыТД();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтогруппаТД_Обновить(Команда)
	
	ОбновитьДанныеТДПоДаннымФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеТДПоДаннымФормы()
	
	ТаблДокОбъект  = СцТ_ПолучитьТаблДокНаФорме();
	Если ТаблДокОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибкаАдреса = Ложь;
	Если КоличествоСтрок = 0 Тогда
		ЕстьОшибкаАдреса = Истина;
		СП = Новый СообщениеПользователю;
		СП.Текст = НСтр("ru = 'Количество строк должно быть больше 0'");
		СП.ПутьКДанным = "КоличествоСтрок";
		СП.Сообщить();
	КонецЕсли;
	
	Если КоличествоКолонок = 0 Тогда
		ЕстьОшибкаАдреса = Истина;
		СП = Новый СообщениеПользователю;
		СП.Текст = НСтр("ru = 'Количество колонок должно быть больше 0'");
		СП.ПутьКДанным = "КоличествоКолонок";
		СП.Сообщить();
	КонецЕсли;
	
	Если ЕстьОшибкаАдреса Тогда
		Возврат;
	КонецЕсли;
	
	КоординатыОбласти = Новый Структура("НачальнаяСтрока,НачальнаяКолонка,КонечнаяСтрока,КонечнаяКолонка", 1, 1, КоличествоСтрок, КоличествоКолонок);
	
	УзелДанных = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(ДеревоЗначения, "ТабличныйДокумент");
	
	СцТ_СкопироватьДанныеТабличногоДокументаВДанныеШага(
		УзелДанных,
		ТаблДокОбъект,
		КоординатыОбласти);
	
	// Запись координат области
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(УзелДанных, "КоличествоКолонокТД", КоличествоКолонок);
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(УзелДанных, "КоличествоСтрокТД"  , КоличествоСтрок);
	
	СцТ_ОбновитьТаблДокПоДаннымФормы_НаСервере("", "ТД", "ТабличныйДокумент");
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтогруппаТД_ПрочитатьИзФайла(Команда)
	
	ДиалогВФ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВФ.Фильтр             = "Таблицы 1С (*.mxl)|*.mxl|Все файлы (*.*)|*.*";
	ДиалогВФ.Расширение         = "mxl";
	ДиалогВФ.МножественныйВыбор = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораФайлаЗагружаемогоТД", ЭтотОбъект);
	
	ДиалогВФ.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлаЗагружаемогоТД(МассивВыбранныхФайлов, ДопПараметры) Экспорт
	
	Если МассивВыбранныхФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайлаТД = МассивВыбранныхФайлов[0];
	
	ДД = Новый ДвоичныеДанные(ПолноеИмяФайлаТД);
	ДД64 = Base64Строка(ДД);
	
	УзелТД = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ТабличныйДокумент");
	
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		УзелТД,
		"ДвоичныеДанныеТД",
		ДД64);
	
	// Заполнение данных формы по считанным данным
	ПрочитатьДанныеЗагруженногоТД();
	
	Модифицированность = Истина;
	
КонецПроцедуры

// Получение табличного документа на форме по данным шага
//
// Возвращаемое значение - указатель на табличный документ
&НаКлиенте
Функция СцТ_ПолучитьТаблДокНаФорме()
	
	Если ПустаяСтрока(ЭлементФормы_Имя) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Не выбран табличныйдокумент.'");
		Сообщение.Поле  = "ТД_ЭлементФормы";
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецЕсли;
	
	СцТ_ПолучитьИнтерактивныйКонтейнер();
	Если ИнтерактивныйКонтейнер = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТаблДокОбъект = СцТ_ГлавнаяФорма.СцТ_НайтиИнтерактивныйОбъектТестируемогоПриложения(
		ИнтерактивныйКонтейнер,
		ВариантПоискаОбъекта,
		Тип("ТестируемоеПолеФормы"),
		ЭлементФормы_Имя,
		ЭлементФормы_Заголовок,
		ПутьКЭлементуФормы,
		Истина);
		
	
	Если ТипЗнч(ТаблДокОбъект) = Тип("Строка") Тогда
		ПоказатьПредупреждение(, ТаблДокОбъект);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ТаблДокОбъект;
	
КонецФункции

&НаКлиенте
Процедура ТД_НадписьПояснениеНажатие(Элемент)
	
	ТаблДокОбъект  = СцТ_ПолучитьТаблДокНаФорме();
	Если ТаблДокОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдрОбласти = ТаблДокОбъект.ПолучитьАдресТекущейОбласти();
	
	ПозицияДвоеточия = Найти(АдрОбласти, ":");
	
	Если ПозицияДвоеточия <> 0 Тогда
		АдрОбласти = Сред(АдрОбласти, ПозицияДвоеточия + 1);
	КонецЕсли;
	
	ПозицияКолонки = Найти(АдрОбласти, "C");
	Если ПозицияКолонки = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Выделена не прямоугольная область'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПозицияСтроки = Найти(АдрОбласти, "R");
	Если ПозицияСтроки = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Выделена не прямоугольная область'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	КолонкаСтрокой = Сред(АдрОбласти, ПозицияКолонки +1);
	Попытка
		КоличествоКолонок = Число(КолонкаСтрокой);
	Исключение
		КоличествоКолонок = 0;
	КонецПопытки;
	
	
	СтрокаСтрокой = Сред(АдрОбласти, ПозицияСтроки + 1, ПозицияКолонки - 1 - ПозицияСтроки);
	Попытка
		КоличествоСтрок = Число(СтрокаСтрокой);
	Исключение
		КоличествоСтрок = 0;
	КонецПопытки;
	
	ТекстВопроса = Нстр("ru = 'Обновить данные табличного документа по данным формы?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВоапросОбОбновленииДанныхТД", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	Модифицированность = Истина;

	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВоапросОбОбновленииДанныхТД(Результат, Доппараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбновитьДанныеТДПоДаннымФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_СкопироватьДанныеТабличногоДокументаВДанныеШага(
	УзелДанных,
	ТаблДокОбъект,
	КоординатыОбласти,
	Обновление = Ложь)
	
	УзлыДанных = УзелДанных.ПолучитьЭлементы();
	
	ТекущееКолвоСтрокТД = УзлыДанных.Количество();
	
	Попытка
		
		ОбъединенныеОбласти = Новый Массив;
		
		Для НомерСтроки = КоординатыОбласти.НачальнаяСтрока По КоординатыОбласти.КонечнаяСтрока Цикл
			
			АдресСтроки = "R" + Формат(НомерСтроки, "ЧГ=0");
			
			УзелСтроки = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(УзелДанных,
				АдресСтроки,,,,,,,,,
				Ложь);
			
			Для НомерКолонки = КоординатыОбласти.НачальнаяКолонка По КоординатыОбласти.КонечнаяКолонка Цикл
				
				АдресЯчейки = СцТ_ГлавнаяФорма.СцТ_АдресЯчейки(НомерСтроки, НомерКолонки);
				ТаблДокОбъект.УстановитьТекущуюОбласть(АдресЯчейки);
				
				АдресТекущейОбл = ТаблДокОбъект.ПолучитьАдресТекущейОбласти();
				
				Если ОбъединенныеОбласти.Найти(АдресТекущейОбл) = Неопределено Тогда
					// Это левый верхний угол объединенной ячейки
					ТекстЯчейки = СокрЛП(ТаблДокОбъект.ПолучитьТекстОбласти(АдресЯчейки));
					
					ОбъединенныеОбласти.Добавить(АдресТекущейОбл);
				Иначе
					// Это ячейка в объединении
					ТекстЯчейки = "";
				КонецЕсли;
				
				АдресКолонки = "C" + Формат(НомерКолонки, "ЧГ=0");
				
				СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
					УзелСтроки,
					АдресКолонки,
					ТекстЯчейки);
			КонецЦикла;
		КонецЦикла;
		
		
		// Удалить прежние строки данных
		Для Итератор = 1 По ТекущееКолвоСтрокТД Цикл
			УзлыДанных.Удалить(0);
		КонецЦикла;
		
	Исключение
		
		// Удалить новые строки данных, если они были добавлены
		КолвоСтрокТД = УзлыДанных.Количество();
		Пока КолвоСтрокТД > ТекущееКолвоСтрокТД Цикл
			УзлыДанных.Удалить(КолвоСтрокТД - 1);
			КолвоСтрокТД = КолвоСтрокТД - 1;
		КонецЦикла;
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

