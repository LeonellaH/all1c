

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОБРАБОТЧИКИ_СОБЫТИЙ_ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимСовместимости838 Тогда
		УстановитьДействие("ПередЗакрытием", "ПередЗакрытиеБезРежимаСовместимости");
	КонецЕсли;
	
	Параметры.Свойство("Заголовок", Заголовок);
	Параметры.Свойство("ТекстРедактирования", РедактируемыйТекст);
	
	Если Параметры.ТолькоПросмотр Тогда
		Элементы.ФормаОтмена.Видимость             = Ложь;
		Элементы.РедактируемыйТекст.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = Нстр("ru = 'Данные изменены. Сохранить изменения?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОСохранении", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиеБезРежимаСовместимости(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		Если НЕ ЗавершениеРаботы Тогда
			
			ТекстВопроса = Нстр("ru = 'Данные изменены. Сохранить изменения?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОСохранении", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Внесены изменения в многострочный текст. При закрытии изменения будут утеряны.'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД И ОПОВЕЩЕНИЙ


#Область ОБРАБОТЧИКИ_КОМАНД_И_ОПОВЕЩЕНИЙ

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Модифицированность = Ложь;
	
	Если Элементы.РедактируемыйТекст.ТолькоПросмотр Тогда
		Закрыть();
	Иначе
		Закрыть(РедактируемыйТекст);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПослеВопросаОСохранении(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		Закрыть(РедактируемыйТекст);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	Иначе
		// Ничего делать не нужно
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

