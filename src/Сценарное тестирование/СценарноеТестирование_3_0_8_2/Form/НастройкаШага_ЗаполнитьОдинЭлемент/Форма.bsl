
#Область Переменные

// Форма - владелец текущей формы (для краткости и наглядности вместо "ЭтаФорма.ВладелецФормы")
&НаКлиенте
Перем СцТ_ГлавнаяФорма Экспорт;

// Указатель на форму (главная или макрошага), из которой была открыта эта форма
&НаКлиенте
Перем ВызвавшаяФорма Экспорт;

// Указатель на ветку в дереве. Заполненяется, если шаг не новый
&НаКлиенте
Перем ДанныеШага Экспорт;

// Идентификатор узла в дереве. Заполнено если шаг не новый
&НаКлиенте
Перем ИдентификаторУзла Экспорт;

// Содержит интерактивный контейнер, соответствующий интерактивному шагу:
//	форма или таблица формы
&НаКлиенте
Перем ИнтерактивныйКонтейнер Экспорт;

// Окно-объект, которому подчинена текущая форма
&НаКлиенте
Перем ОкноВыбраннойФормы Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимСовместимости838 Тогда
		УстановитьДействие("ПередЗакрытием", "ПередЗакрытиеБезРежимаСовместимости");
	КонецЕсли;
	
	// Если форма открыта под внешним управлением, то не вся функциональность
	// должна работать
	Если Параметры.ОткрытаПодВнешнимУправлением Тогда
		
		ФормаОткрытаПодВнешнимУправлением = Истина;
		
		Элементы.ВыбратьЭлемент.Доступность         = Ложь;
		Элементы.ОбновитьПоДаннымФормы1.Доступность = Ложь;
		Элементы.ОбновитьПоДаннымФормы.Доступность  = Ложь;
		
	КонецЕсли;
	///

	
	ЭтаОбработка = РеквизитФормыВЗначение("Объект");
	ПутьКФормам  = ЭтаОбработка.Метаданные().ПолноеИмя() + ".Форма.";
	
	// Управление доступностью
	ЭтотОбъект.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Элементы.ПрименитьИЗакрыть.Доступность  = НЕ Параметры.ТолькоПросмотр;
	Элементы.ПрименитьИзменения.Доступность = НЕ Параметры.ТолькоПросмотр;
	
	// основные атрибуты шага (ОА)
	// атрибуты, для редактирования, которых не требуется открытие формы настройки
	// или свернутые для хранения атрибуты
	ОА_ТипШага                          = Параметры.ТипШага;
	ОА_НомерШага                        = Параметры.НомерШага;
	ОА_Активность                       = Параметры.Активность;
	ОА_ОписательПоложенияШагаВДереве    = Параметры.ОписательПоложенияШагаВДереве;
	ОА_ЭтоНовый                         = Параметры.ЭтоНовый;
	ОА_СтруктураДанныхШага              = Параметры.СтруктураДанныхШага;
	
	// 1. Раскрытие структуры шага и определение типа шага
	Если ОА_СтруктураДанныхШага = Неопределено  Тогда
		Возврат;
	КонецЕсли;
	
	ДШ_Наименование                 = ОА_СтруктураДанныхШага.Наименование;
	ДШ_ТипМетаданных                = ОА_СтруктураДанныхШага.ТипМетаданных;
	ДШ_ИмяМетаданных                = ОА_СтруктураДанныхШага.ИмяМетаданных;
	ДШ_Автоописание                 = ОА_СтруктураДанныхШага.Автоописание;
	ДШ_Описание                     = ОА_СтруктураДанныхШага.Описание;
	ДШ_СкрытьАвтоописание           = ОА_СтруктураДанныхШага.СкрытьАвтоописание;
	ДШ_ОбСсылка                     = ОА_СтруктураДанныхШага.ОбСсылка;
	ДШ_ПредставлениеОбъекта         = ОА_СтруктураДанныхШага.ПредставлениеОбъекта;
	ДШ_ПредставлениеИмениМетаданных = ОА_СтруктураДанныхШага.ПредставлениеИмениМетаданных;
	ДШ_ИмяФормы                     = ОА_СтруктураДанныхШага.ИмяФормы;
	ДШ_ВыполнитьВручную             = ОА_СтруктураДанныхШага.ВыполнитьВручную;
	ДШ_Комментарий                  = ОА_СтруктураДанныхШага.Комментарий;
	ДШ_ЗаголовокФормы               = ОА_СтруктураДанныхШага.ЗаголовокФормы;
	ДШ_ДанныеКартинки               = ОА_СтруктураДанныхШага.ДанныеКартинки;
	
	Если ТипЗнч(ОА_СтруктураДанныхШага.ДеревоЗначения) = Тип("Структура") Тогда
		// Данные дерева переданы в виде структуры из главной формы обработки
		СцТ_ЗаполнитьДеревоФормыИзДереваСтруктур(ДеревоЗначения,
			ОА_СтруктураДанныхШага.ДеревоЗначения);
	ИначеЕсли ТипЗнч(ОА_СтруктураДанныхШага.ДеревоЗначения) = Тип("ХранилищеЗначения") Тогда
		// Данные из конфигурации могут поступить в виде данных ХЗ
		Попытка
			ДЗ_НаСервере = ОА_СтруктураДанныхШага.ДеревоЗначения.Получить();
			ПреобразоватьЗначенияВДереве(ДЗ_НаСервере, Истина);
			ЗначениеВРеквизитФормы(ДЗ_НаСервере, "ДеревоЗначения");
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	
	// Признак, что наименование установлено вручную
	НаименованиеШагаИзмененоВручную = СцТ_ЗначениеВДереве(
		ДеревоЗначения,
		"ИСТЗ_НаименованиеРучное",
		Ложь);
		
	// Картинки для закладок описания и комментария
	АдресКартинкиКомментария = ПоместитьВоВременноеХранилище(ЭтаОбработка.ПолучитьМакет("КартинкаКомментария"));
	КартинкаКомментария = Новый Картинка(ПолучитьИзВременногоХранилища(АдресКартинкиКомментария));
	
	Элементы.Страница_РедактируемоеОписание.Картинка = ?(
		ПустаяСтрока(ДШ_Описание),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
	Элементы.Страница_Комментарий.Картинка = ?(
		ПустаяСтрока(ДШ_Комментарий),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
	СцТ_НастроитьФормуДляШага();
	
	// Заполнение поведения при ошибке для группового шага
	СцТ_ЗаполнитьДействиеГрупповогоШагаПриОшибке(ЭтотОбъект);
	
	// Модифицированность
	Модифицированность = ОА_ЭтоНовый;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.ЭлементФормы_Заголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_УдалениеШаговСценария" Тогда
		
		Если ОА_ЭтоНовый Тогда
			Возврат;
		КонецЕсли;
		
		Если Параметр.НайтиПоЗначению(ДанныеШага.ПолучитьИдентификатор()) <> Неопределено Тогда
			Модифицированность = Ложь;
			Если Открыта() Тогда
				Закрыть();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли  ИмяСобытия = "СцТ_ПеремещениеУзловСценария" Тогда
		
		Если ОА_ЭтоНовый Тогда
			Возврат;
		КонецЕсли;
		
		НовыйУзел = Параметр[ДанныеШага];
		Если НовыйУзел <> Неопределено Тогда
			ДанныеШага = НовыйУзел;
			ИдентификаторУзла = ДанныеШага.ПолучитьИдентификатор();
			ДШ_НомерШага      = ДанныеШага.НомерШага;
			СцТ_СформироватьПредставлениеРодителя();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "СцТ_ОбновленыДанныеГрупповогоШага" Тогда
		
		Если Параметр = ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя Тогда
			СцТ_СформироватьПредставлениеРодителя();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "СцТ_ИзмениласьНумерацияШагов" Тогда
		
		Если ОА_ЭтоНовый Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			ДанныеШага = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ИдентификаторУзла);
			ОА_НомерШага = ДанныеШага.НомерШага;
			СцТ_СформироватьПредставлениеРодителя();
		Исключение
		КонецПопытки;
		
	ИначеЕсли ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("СцТ_ОбработатьЗапросОСохраненииДанныхШага", ЭтотОбъект),
			НСтр("ru = 'Данные шага были изменены. Применить изменения?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиеБезРежимаСовместимости(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		Если НЕ ЗавершениеРаботы Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("СцТ_ОбработатьЗапросОСохраненииДанныхШага", ЭтотОбъект),
			НСтр("ru = 'Данные шага были изменены. Применить изменения?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
			
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Данные шага ""элемент формы"" были изменены. При закрытии все изменения будут утеряны'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Попытка
		СцТ_ГлавнаяФорма.СцТ_ОткрытыеФормыНастройкиШагов.Удалить(ОА_ОписательПоложенияШагаВДереве.ИдентификаторШага);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура СохранитьИЗакрытьФорму(Команда)
	
	Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	
	Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Сохранение шага'"), , НСтр("ru = 'Данные шага сохранены.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАвтоназвание(Команда)
	
	СцТ_ОбновитьНаименование_НаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ХранимыеДанныеШага(Команда)
	
	// Всегда отображаются имеено хранимые данные, которые сохранены в дереве шагов
	Если ОА_ЭтоНовый Тогда
		ТекстПредупреждения = НСтр("ru = 'Данные шага еще не записаны. Отобразить структуру хранения данных невозможно. Сохранить?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("СцТ_ПослеОтветаНаВопросОСохраненииНового", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстПредупреждения, РежимДиалогаВопрос.ДаНет);
		Возврат;
	ИначеЕсли НЕ ОА_ЭтоНовый И Модифицированность Тогда
		ТекстПредупреждения = НСтр("ru = 'Данные шага были изменены. Сохранить изменения перед отображением структуры?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("СцТ_ПослеОтветаНаВопросОСохраненииСуществующего", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстПредупреждения, РежимДиалогаВопрос.ДаНетОтмена);
		Возврат;
	КонецЕсли;
	
	СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоДаннымФормы(Команда)
	
	СцТ_ПолучитьИнтерактивныйКонтейнер();
	Если ИнтерактивныйКонтейнер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ИнтерактивныйКонтейнер.Активизировать();
	Исключение
		Возврат;
	КонецПопытки;
	
	// попытка найти нужный элемент формы
	ПолеФормы = ИнтерактивныйКонтейнер.ПолучитьТекущийЭлемент();
	
	Если ПолеФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ЭлементФормы_Значение = ПолеФормы.ПолучитьПредставлениеДанных();
	Исключение
		ЭлементФормы_Значение = "";
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПоиска(Команда)
	
	Если ПустаяСтрока(ЭлементФормы_Имя) И ПустаяСтрока(ЭлементФормы_Заголовок) Тогда
		ТекстПредупреждения = НСтр("ru = 'Не выбран элемент формы'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ЗапретДоступаКПути = (ПутьКЭлементуФормы <> Неопределено);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВариантПоискаЭлемента"          , ВариантПоискаОбъекта);
	ПараметрыФормы.Вставить("ТипЭлемента"                    , ЭлементФормы_Тип);
	ПараметрыФормы.Вставить("ВидЭлемента"                    , ЭлементФормы_Вид);
	ПараметрыФормы.Вставить("ИмяЭлемента"                    , ЭлементФормы_Имя);
	ПараметрыФормы.Вставить("ЗаголовокЭлемента"              , ЭлементФормы_Заголовок);
	ПараметрыФормы.Вставить("ЗапретДоступаКПути"             , ЗапретДоступаКПути);
	ПараметрыФормы.Вставить("ЗапретДоступаКПути"             , ЗапретДоступаКПути);
	ПараметрыФормы.Вставить("ПутьКЭлементуФормы"             , ПутьКЭлементуФормы);
	ПараметрыФормы.Вставить("ФормаОткрытаПодВнешнимУправлением", ФормаОткрытаПодВнешнимУправлением);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("СцТ_ПослеЗакрытияФормыСпособаПоискаЭлементовФормы", ЭтотОбъект, Ложь);
	
	ФормаПравилаПоиска = ОткрытьФорму(ПутьКФормам + "СпособПоискаЭлементаФормы",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОповещениеОЗакрытии);
		
	ФормаПравилаПоиска.СцТ_ГлавнаяФорма = СцТ_ГлавнаяФорма;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКнопкуКИ(Команда)
	
	Если ИнтерактивныйКонтейнер = Неопределено Тогда
		СцТ_ПолучитьИнтерактивныйКонтейнер();
	КонецЕсли;
	
	Если ИнтерактивныйКонтейнер = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось получить тестируемую форму. Возможно не установлено подключение к тестируемому приложению'");
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОписаниеТекущегоЭлементаФормы = Новый Структура;
	
	ОписаниеТекущегоЭлементаФормы.Вставить("Тип"      , "ТестируемоеПолеФормы");
	ОписаниеТекущегоЭлементаФормы.Вставить("Заголовок", ЭлементФормы_Заголовок);
	ОписаниеТекущегоЭлементаФормы.Вставить("Имя"      , ЭлементФормы_Имя);
	
	ТипыОтображаемых       = Новый СписокЗначений;
	
	ТипыОтображаемых = Новый СписокЗначений;
	ТипыОтображаемых.Добавить("ГруппаКнопок");
	ТипыОтображаемых.Добавить("ГруппаКолонок");
	ТипыОтображаемых.Добавить("КоманднаяПанель");
	ТипыОтображаемых.Добавить("КонекстноеМеню");
	ТипыОтображаемых.Добавить("ОбычнаяГруппа");
	ТипыОтображаемых.Добавить("Подменю");
	ТипыОтображаемых.Добавить("Страница");
	ТипыОтображаемых.Добавить("Страницы");
	
	ТипыВыбираемыхОбъектов = Новый СписокЗначений;
	ТипыВыбираемыхОбъектов.Добавить("ПолеВвода");
	ТипыВыбираемыхОбъектов.Добавить("ПолеФлажка");
	ТипыВыбираемыхОбъектов.Добавить("ПолеПереключателя");
	ТипыВыбираемыхОбъектов.Добавить("ПолеКалендаря");
	ТипыВыбираемыхОбъектов.Добавить("ПолеТекстовогоДокумента");
	ТипыВыбираемыхОбъектов.Добавить(
		Новый Структура("ТипОбъекта, ВидОбъекта",
						Тип("ТестируемоеПолеФормы"),
						"Надпись"));
	
	
	ОбработчикОповещения = Новый ОписаниеОповещения("СцТ_ПослеВыбораТекстируемогоЭлементаФормы", ЭтотОбъект);
	
	СцТ_ГлавнаяФорма.СцТ_ВыбратьОбъектыТестируемогоПриложения(
		ИнтерактивныйКонтейнер,
		ТипыВыбираемыхОбъектов,
		ТипыОтображаемых,
		ОписаниеТекущегоЭлементаФормы,,
		ЭтотОбъект,,, 
		Истина,,,,
		ОбработчикОповещения);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АктивностьПриИзменении(Элемент)
	
	Если ОА_Активность = 2 Тогда
		ОА_Активность = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ПроверочноеНаименование = СцТ_СформироватьОписаниеШага_НаКлиенте(Истина);
	
	НаименованиеШагаИзмененоВручную = (ДШ_Наименование <> ПроверочноеНаименование);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("СцТ_ОбновитьЗаголовкиЗакладок", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("СцТ_ОбновитьЗаголовкиЗакладок", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеГруппыНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ОА_ЭтоНовый Тогда
		РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
	Иначе
		РодительУзла = ДанныеШага.ПолучитьРодителя();
	КонецЕсли;
	
	Если РодительУзла <> Неопределено Тогда
		СцТ_ГлавнаяФорма.СцТ_ОткрытьФормуНастройкиШага(РодительУзла, ВызвавшаяФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПриОшибкеВПодчиненныхШагахПриИзменении(Элемент)
	
	УправлениеДоступностьюПеременнойОшибкиГрупповогоШага(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеСЭлементомПриИзменении(Элемент)
	
	НастроитьВидЭлементаФормыЗначение(ЭтотОбъект);
	
	СцТ_ОбновитьНаименование_НаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДШ_СкрытьАвтоописаниеПриИзменении(Элемент)
	
	ОбновитьПользовательскоеОписание();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элемент.ТекущаяСтраница = Элементы.Страница_ОписаниеШага Тогда
		ОбновитьПользовательскоеОписание();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлементФормы_ВидПриИзменении(Элемент)
	
	НастроитьДействиеВЗависимостиОтВида();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиОповещений

// Обрабатывает нажатие пользователем кнопки в диалоге вопроса о сохранении
// данных шага перед закрытием формы
// Параметры
//	КодВозврата - код нажатой кнопки (Да, Нет, Отмена);
//	ДопДанные - дополнительные данные;
//
&НаКлиенте
Процедура СцТ_ОбработатьЗапросОСохраненииДанныхШага(КодВозврата, ДопДанные) Экспорт
	
	Если КодВозврата = КодВозвратаДиалога.Да Тогда
		Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли КодВозврата = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
		
		// При Отмене ничего не выполняется
		
	КонецЕсли;
	
КонецПроцедуры

// После ответа на вопрос диалога о Сохранении нового шага перед
// отображением хранимых данных
&НаКлиенте
Процедура СцТ_ПослеОтветаНаВопросОСохраненииНового(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	// Обработка нажатия "ДА"
	Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
		СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Не удалось сохранить шаг'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

// После ответа на вопрос диалога о Сохранении уже существующего модифицированного шага перед
// отображением хранимых данных
&НаКлиенте
Процедура СцТ_ПослеОтветаНаВопросОСохраненииСуществующего(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		// Показываем, что хранится
		СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
		
	Иначе
		
		Если СцТ_ПрименитьИзменения_НаКлиенте() Тогда
			СцТ_ГлавнаяФорма.ОтобразитьХранимыеДанныеШага(ИдентификаторУзла, ВызвавшаяФорма);
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Не удалось сохранить изменения в шаге'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Отображение родительского шага
// Параметры:
// ПриОткрытии - устанавливает признак, что настройка выполняется при открытии
// или (в противном случае) по обработке оповещения
&НаКлиенте
Процедура СцТ_СформироватьПредставлениеРодителя(ПриОткрытии = Истина) Экспорт
	
	Если ОА_ЭтоНовый И ПриОткрытии Тогда
		Если ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя = Неопределено Тогда
			ПредставлениеГруппы = "";
		Иначе
			// Поиск данных родительского узла в дереве по идентификатору шага
			СцТ_РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,
				ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
			
			Если СцТ_РодительУзла = Неопределено Тогда
				ПредставлениеГруппы = "";
			Иначе
				ОтобразитьПредставлениеСуществующегоРодительскогоШага(СцТ_РодительУзла);
			КонецЕсли;
		КонецЕсли;
		
		// Если родительский узел будет изменен у нового шага, то редактируемый шаг уже не имеет смысла,
		// так как неизвестно куда его вставить
		
	ИначеЕсли НЕ ОА_ЭтоНовый Тогда
		Если ДанныеШага = Неопределено Тогда
			ПредставлениеГруппы = "";
		Иначе
			СцТ_РодительУзла = ДанныеШага.ПолучитьРодителя();
			Если СцТ_РодительУзла = Неопределено Тогда
				ПредставлениеГруппы = "";
			Иначе
				ОтобразитьПредставлениеСуществующегоРодительскогоШага(СцТ_РодительУзла);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПредставлениеГруппы = "" Тогда
		Элементы.СтраницыГрупп.ТекущаяСтраница = Элементы.СтраницаПустойГруппы;
	Иначе
		Элементы.СтраницыГрупп.ТекущаяСтраница = Элементы.СтраницаЗаполненнойГруппы;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_ПослеЗакрытияФормыСпособаПоискаЭлементовФормы(РезультатОкна, ДопПараметры) Экспорт
	
	Если ТипЗнч(РезультатОкна) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ВариантПоискаОбъекта   = РезультатОкна.ВариантПоискаЭлемента;
	ЭлементФормы_Имя       = РезультатОкна.ИмяЭлемента;
	ЭлементФормы_Заголовок = РезультатОкна.ЗаголовокЭлемента;
	ПутьКЭлементуФормы     = РезультатОкна.ПутьКЭлементуФормы;
	ЭлементФормы_Вид       = РезультатОкна.ВидЭлемента;
	
	//Если ПустаяСтрока(ИмяИскомогоОбъекта) Тогда
	//	ИмяИскомогоОбъекта = "$" + ЗаголовокИскомогоОбъекта;
	//ИначеЕсли ПустаяСтрока(ЗаголовокИскомогоОбъекта) Тогда
	//	ЗаголовокИскомогоОбъекта = ИмяИскомогоОбъекта;
	//КонецЕсли;
	
	//// Изменить имя и заголовок объекта
	//Если ТипШага = "ЗаполнитьОдинРеквизитФормы" ИЛИ ТипШага = "ЗаполнитьКолонкуТаблицы" Тогда
	//	
	//	УзелПоля = ДеревоЗначения.ПолучитьЭлементы()[0].ПолучитьЭлементы()[0];
	//	УзелПоля.Имя     = ИмяИскомогоОбъекта;
	//	УзелПоля.Синоним = ЗаголовокИскомогоОбъекта;
	//	
	//ИначеЕсли ТипШага = "ЗаполнитьРеквизитыФормы" ИЛИ ТипШага = "ЗаполнитьСтрокуТаблицы" Тогда
	//	
	//	ТекДанные = Элементы.ТаблицаРеквизитов.ТекущиеДанные;
	//	Если ТекДанные = Неопределено Тогда
	//		Возврат;
	//	КонецЕсли;
	//	
	//	ТекДанные.ДополнительныеДанные.ВариантПоискаОбъекта = ПравилоПоиска.ВариантПоискаОбъекта;
	//	ТекДанные.Имя     = ИмяИскомогоОбъекта;
	//	ТекДанные.Синоним = ЗаголовокИскомогоОбъекта;
	//	
	//	ПутьКОбъекту = Неопределено;
	//	Если ТекДанные.ДополнительныеДанные.Свойство("ПутьКОбъекту", ПутьКОбъекту) Тогда
	//		Если ПутьКОбъекту <> Неопределено И ПутьКОбъекту.Количество() > 0 Тогда
	//			ПоследнийЭлементПоискаПути = ПутьКОбъекту[ПутьКОбъекту.ВГраница()];
	//			ПоследнийЭлементПоискаПути.ИмяОбъекта       = ИмяИскомогоОбъекта;
	//			ПоследнийЭлементПоискаПути.ЗаголовокОбъекта = ЗаголовокИскомогоОбъекта;
	//		КонецЕсли;
	//	КонецЕсли;
	//	
	//	Возврат;
	//	
	//ИначеЕсли ТипШага = "НажатьКнопку" Тогда
	//	
	//	УзелИмениКнопки = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ИмяКнопки");
	//	УзелИмениКнопки.Значение = ИмяИскомогоОбъекта;
	//	УзелИмениКнопки.Синоним  = ЗаголовокИскомогоОбъекта;
	//	
	//ИначеЕсли ТипШага = "НайтиЭлементФормы" Тогда
	//	
	//	УзелЭлемента = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ИмяОбъекта");
	//	Если УзелЭлемента <> Неопределено Тогда
	//		УзелЭлемента.Значение = ИмяИскомогоОбъекта;
	//		УзелЭлемента.Синоним  = ЗаголовокИскомогоОбъекта;
	//	КонецЕсли;
	//	
	//	СцТ_ОбновитьАвтоОписаниеШага_НаКлиенте(Истина);
	//	
	//ИначеЕсли ТипШага = "НажатьКнопкуКИ" Тогда
	//	
	//	УзелОписанияКнопки = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ОписаниеКнопкиКИ");
	//	УзелОписанияКнопки.Синоним = ЗаголовокИскомогоОбъекта;
	//	
	//ИначеЕсли ТипШага = "АвтоГруппаТаблицаФормы" Тогда
	//	
	//	Если ПустаяСтрока(ПредставлениеТаблицы) Тогда
	//		Сообщение = Новый СообщениеПользователю;
	//		Сообщение.Текст = НСтр("ru = 'Не выбрана таблица формы.'");
	//		Сообщение.Поле = "ПредставлениеТаблицы";
	//		Сообщение.Сообщить();
	//		Возврат;
	//	КонецЕсли;
	//	
	//	УзелОписанияТаблицы = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "ОписаниеТаблицы");
	//	УзелОписанияТаблицы.Значение = ИмяИскомогоОбъекта;
	//	УзелОписанияТаблицы.Синоним  = ЗаголовокИскомогоОбъекта;
	//	
	//Иначе
	//	
	//	Возврат;
	//	
	//КонецЕсли;
	
	/////////////////////
	
	//Если ПутьПоискаОбъекта <> Неопределено И ПутьПоискаОбъекта.Количество() > 0 Тогда
	//	ПоследнийЭлементПоискаПути = ПутьПоискаОбъекта[ПутьПоискаОбъекта.ВГраница()];
	//	ПоследнийЭлементПоискаПути.ИмяОбъекта = ЭлементФормы_Имя;
	//	ПоследнийЭлементПоискаПути.ЗаголовокОбъекта = ЭлементФормы_Заголовок;
	//КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_ПослеВыбораТекстируемогоЭлементаФормы(РезультатФормы, ДопПараметры) Экспорт
	
	Если ТипЗнч(РезультатФормы) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ИзменилсяВидЭлемента = (ЭлементФормы_Вид <> РезультатФормы.Вид);
	
	ЭлементФормы_Заголовок = РезультатФормы.Заголовок;
	ЭлементФормы_Имя       = РезультатФормы.Имя;
	ЭлементФормы_Значение  = РезультатФормы.Значение;
	ЭлементФормы_Вид       = РезультатФормы.Вид;
	
	ПутьКЭлементуФормы = Новый Структура;
	НомерСтроки = 1;
	Для каждого СтруктураМассива Из РезультатФормы.ПутьКОбъекту Цикл
		ИмяСтруктурыСтроки = "Str_" + Формат(НомерСтроки, "ЧГ=0");
		
		// Тип объекта не сериализуется на сервере
		Если СтруктураМассива.ТипОбъекта = Тип("ТестируемаяГруппаКомандногоИнтерфейса") Тогда
			НовыйТип = "ТестируемаяГруппаКомандногоИнтерфейса";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяГруппаФормы") Тогда
			НовыйТип = "ТестируемаяГруппаФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяДекорацияФормы") Тогда
			НовыйТип = "ТестируемаяДекорацияФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяКнопкаКомандногоИнтерфейса") Тогда
			НовыйТип = "ТестируемаяКнопкаКомандногоИнтерфейса";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяКнопкаФормы") Тогда
			НовыйТип = "ТестируемаяКнопкаФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
			НовыйТип = "ТестируемаяТаблицаФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемоеПолеФормы") Тогда
			НовыйТип = "ТестируемоеПолеФормы";
		ИначеЕсли СтруктураМассива.ТипОбъекта = Тип("ТестируемаяФорма") Тогда
			НовыйТип = "ТестируемаяФорма";
		Иначе
			Продолжить;
		КонецЕсли;
		СтруктураМассива.ТипОбъекта = НовыйТип;
		
		ПутьКЭлементуФормы.Вставить(ИмяСтруктурыСтроки, СтруктураМассива);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Если ИзменилсяВидЭлемента Тогда
		НастроитьДействиеВЗависимостиОтВида();
	КонецЕсли;
	
	//СцТ_ОбновитьАвтоОписаниеШага_НаКлиенте(Истина);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти


#Область ВспомогательныеПроцедурыИФункции

#Область НастрокаПриЗагрузкеДанныхШага

// Индивидуальная настройка формы для шага
//
&НаСервере
Процедура СцТ_НастроитьФормуДляШага()
	
	// СВТ - список выбора типа
	СВТ = Элементы.ЭлементФормы_Вид.СписокВыбора;
	СВТ.Добавить("ПолеВвода"              , НСтр("ru = 'Поле ввода'"));
	СВТ.Добавить("ПолеФлажка"             , НСтр("ru = 'Флажок'"));
	СВТ.Добавить("ПолеПереключателя"      , НСтр("ru = 'Переключатель'"));
	СВТ.Добавить("ПолеКалендаря"          , НСтр("ru = 'Календарь'"));
	СВТ.Добавить("ПолеТекстовогоДокумента", НСтр("ru = 'Текстовый документ'"));
	СВТ.Добавить("Надпись"                , НСтр("ru = 'Надпись'"));
	
	//СВ = Элементы.ЭлементФормы_Тип.СписокВыбора;
	//СВ.Добавить("Гиперссылка"                         , "Гиперссылка");
	//СВ.Добавить("ГруппаКнопок"                        , "Группа кнопок");
	//СВ.Добавить("ГруппаКолонок"                       , "Группа колонок");
	//СВ.Добавить("Картинка"                            , "Картинка");
	//СВ.Добавить("КнопкаКоманднойПанели"               , "Кнопка командной панели");
	//СВ.Добавить("КоманднаяПанель"                     , "Командная панель");
	//СВ.Добавить("КонтекстноеМеню"                     , "Контекстное меню");
	//СВ.Добавить("Надпись"                             , "Надпись");
	//СВ.Добавить("ОбычнаяГруппа"                       , "Группа элементов");
	//СВ.Добавить("ОбычнаяКнопка"                       , "Кнопка");
	//СВ.Добавить("Подменю"                             , "Подменю");
	//СВ.Добавить("ПолеHTMLДокумента"                   , "Поле HTML-документа");
	//СВ.Добавить("ПолеВвода"                           , "Поле ввода");
	//СВ.Добавить("ПолеГеографическойСхемы"             , "Географическая схема");
	//СВ.Добавить("ПолеГрафическойСхемы"                , "Графическая схема");
	//СВ.Добавить("ПолеДендрограммы"                    , "Дендрограма");
	//СВ.Добавить("ПолеДиаграммы"                       , "Диаграмма");
	//СВ.Добавить("ПолеДиаграммыГанта"                  , "Диаграмма Ганта");
	//СВ.Добавить("ПолеИндикатора"                      , "Индикатор");
	//СВ.Добавить("ПолеКалендаря"                       , "Календарь");
	//СВ.Добавить("ПолеПереключателя"                   , "Переключатель");
	//СВ.Добавить("ПолеПолосыРегулирования"             , "Полоса регулирования");
	//СВ.Добавить("ПолеТабличногоДокумента"             , "Табличный документ");
	//СВ.Добавить("ПолеТекстовогоДокумента"             , "Текстовый документ");
	//СВ.Добавить("ПолеФлажка"                          , "Флажок");
	//СВ.Добавить("ПолеФорматированногоДокумента"       , "Форматированный документ");
	//СВ.Добавить("Страница"                            , "Страница");
	//СВ.Добавить("Страницы"                            , "Набор страниц");
	//СВ.Добавить("ТаблицаФормы"                        , "Таблица");
	//СВ.Добавить("ТестируемаяФорма"                    , "Форма");
	//СВ.Добавить("ТестируемоеОкноКлиентскогоПриложения", "Окно");
	//СВ.Добавить("ТестируемоеПриложение"               , "Тестирумеое приложение");
	//СВ.Добавить("ТестируемоеПриложение"               , "Тестирумеое приложение");
	//СВ.Добавить("КомандныйИнтерфейс"                  , "Командный интерфейс");
	//СВ.Добавить("ГруппаКомандногоИнтерфейса"          , "Группа интерфейса");
	//СВ.Добавить("КнопкаКомандногоИнтерфейса"          , "Кнопка командного интерфейса");
	
	
	ЭлементФормы_Имя           = "";
	ЭлементФормы_Заголовок     = "";
	ЭлементФормы_Вид           = "";
	ВариантПоискаОбъекта       = "100";
	ЭлементФормы_Значение      = "";
	ЭлементФормы_Тип           = "ТестируемоеПолеФормы";
	
	УзелРеквизита = СцТ_НайтиУзелДереваФормы(ДеревоЗначения, "Имя", "РеквизитФормы");
	Если УзелРеквизита <> Неопределено Тогда
		Если УзелРеквизита.ПолучитьЭлементы().Количество() > 0 Тогда
			УзелОписанияЭлемента = УзелРеквизита.ПолучитьЭлементы()[0];
			
			ТипЭлемента = СцТ_ЗначениеВДереве(УзелОписанияЭлемента, "ТипОбъекта", "ТестируемоеПолеФормы");
			Если ТипЗнч(ТипЭлемента) = Тип("Число") Тогда
				Если ТипЭлемента = 3 Тогда
					ЭлементФормы_Тип = "ТестируемоеПолеФормы";
				ИначеЕсли ТипЭлемента = 4 Тогда
					ЭлементФормы_Тип = "ТестируемаяГруппаФормы";
				ИначеЕсли ТипЭлемента = 5 Тогда
					ЭлементФормы_Тип = "ТестируемаяКнопкаФормы";
				ИначеЕсли ТипЭлемента = 6 Тогда
					ЭлементФормы_Тип = "ТестируемаяДекорацияФормы";
				ИначеЕсли ТипЭлемента = 7 Тогда
					ЭлементФормы_Тип = "ТестируемаяТаблицаФормы";
				Иначе
					ЭлементФормы_Тип = "ТестируемоеПолеФормы";
					// Это значение по умолчанию
				КонецЕсли;
				
			Иначе
				ЭлементФормы_Тип = ТипЭлемента;
			КонецЕсли;
			
			ЭлементФормы_Имя           = УзелОписанияЭлемента.Имя;
			ЭлементФормы_Заголовок     = УзелОписанияЭлемента.Синоним;
			ЭлементФормы_Вид           = УзелОписанияЭлемента.ТипУзла;
			ДействиеСЭлементом_Вр      = УзелОписанияЭлемента.Действие;
			ЭлементФормы_Значение      = УзелОписанияЭлемента.Значение;
			
			ЗаполнитьПутьКЭлементуФормы(УзелОписанияЭлемента);
			
			ВариантПоискаОбъекта = СцТ_ЗначениеВДереве(УзелОписанияЭлемента, "ВариантПоискаОбъекта", "100");
			
		КонецЕсли;
		
	КонецЕсли;
	
	
	// Заполнение. Продолжение
	ДействиеСЭлементом = СцТ_ЗначениеВДереве(ДеревоЗначения, "ДействиеСОбъектом"   , "");
	
	НастроитьВидЭлементаФормыЗначение(ЭтотОбъект);
	
	СцТ_ЗаполнитьСписокВыбораДействийДляПоляВвода(
			Элементы.ДействиеСЭлементом.СписокВыбора,
			ЭлементФормы_Вид,
			Ложь);
	
	Если ЗначениеЗаполнено(ДействиеСЭлементом_Вр) Тогда
		ДействиеСЭлементом = ДействиеСЭлементом_Вр;
	Иначе
		ДействиеСЭлементом = Элементы.ДействиеСЭлементом.СписокВыбора[0].Значение;
	КонецЕсли;
	
	НастроитьВидЭлементаФормыЗначение(ЭтотОбъект);
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.Страница_ДанныеШага;
	
КонецПроцедуры

&НаСервере
Процедура СцТ_ЗаполнитьДействиеГрупповогоШагаПриОшибке(ЭтотОбъект)
	
	СписокВыбора = Элементы.ДействиеПриОшибке.СписокВыбора;
	СписокВыбора.Добавить("Остановить", НСтр("ru = 'Остановить выполнение'"));
	СписокВыбора.Добавить("Продолжить", НСтр("ru = 'Продолжить выполнение'"));
	
	// Параметры действий при ошибке находятся в корне
	ДействиеПриОшибке = СцТ_ЗначениеВДереве(
		ДеревоЗначения,
		"ДействиеПриОшибке",
		"Остановить");
		
	ПеременнаяПриОшибке = СцТ_ЗначениеВДереве(
		ДеревоЗначения,
		"ПеременнаяПриОшибке",
		"");
	
	УправлениеДоступностьюПеременнойОшибкиГрупповогоШага(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СохранениеШагаВСценарии

// Сохранение реализовано в виде функции, т.к. потребуется
// ее выполнение в обработчике ПередЗакрытием
//
&НаКлиенте
Функция СцТ_ПрименитьИзменения_НаКлиенте() Экспорт
	
	// Проверка, что владелец формы еще не закрыт
	Если НЕ ВладелецФормы.Открыта() Тогда
		ТекстСообщения = НСтр("ru = 'Форма с деревом шагов уже закрыта. Применить изменения невозможно'");
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	// Проверка правильности данных
	Отказ = Ложь;
	
	Если ПустаяСтрока(ДШ_Наименование) Тогда
		
		ТекстСообщения = НСтр("ru = 'Не заполнено наименование.'");
		
		СП = Новый СообщениеПользователю;
		СП.Текст = ТекстСообщения;
		СП.ПутьКДанным = "ДШ_Наименование";
		СП.Сообщить();
		
		Отказ = Истина;
			
	КонецЕсли;
	
	Если ПустаяСтрока(ЭлементФормы_Имя) Тогда
		ТекстСообщения = НСтр("ru = 'Имя элемента должно быть заполнено.'");
		
		СП = Новый СообщениеПользователю;
		СП.Текст = ТекстСообщения;
		СП.ПутьКДанным = "ЭлементФормы_Имя";
		СП.Сообщить();
		
		Отказ = Истина;
	КонецЕсли;
	
	Если ПустаяСтрока(ДействиеСЭлементом) Тогда
		ТекстСообщения = НСтр("ru = 'Действие должно быть заполнено'");
		
		СП = Новый СообщениеПользователю;
		СП.Текст = ТекстСообщения;
		СП.ПутьКДанным = "ДействиеСЭлементом";
		СП.Сообщить();
		
		Отказ = Истина;
	КонецЕсли;
	
	// Проверка индивидуальных настроек шага и заполнение ДереваЗначения
	СцТ_ПодготовитьДанныеШагаДляСохранения_НаКлиенте(Отказ);
	
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СцТ_ОбновитьНаименование_НаКлиенте();
	ОбновитьПользовательскоеОписание();
	
	ВозвращаемаяСтруктура = СцТ_СформироватьСтруктуруданныхШага_НаКлиенте();
	
	Если ОА_ЭтоНовый Тогда
		
		ДанныеШага = СцТ_ГлавнаяФорма.СцТ_СохранитьНовыйШаг(ВызвавшаяФорма, ВозвращаемаяСтруктура);
		
		ИдентификаторУзла = ДанныеШага.ПолучитьИдентификатор();
		ОА_НомерШага      = ДанныеШага.НомерШага;
		ОА_ЭтоНовый       = Ложь;
		
	Иначе
		СцТ_ГлавнаяФорма.СцТ_ПрименитьИзмененияВШаге(ВызвавшаяФорма, ДанныеШага, ВозвращаемаяСтруктура);
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Возврат Истина;
	
КонецФункции

// Заполнение дерева значения данными формы.
// Проверка корректности специфических для этого шага данных
&НаКлиенте
Процедура СцТ_ПодготовитьДанныеШагаДляСохранения_НаКлиенте(Отказ)
	
	// Проверки
	
	// формирование дерева
	
	Если НаименованиеШагаИзмененоВручную Тогда
		СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
			ДеревоЗначения,
			"ИСТЗ_НаименованиеРучное",
			Истина);
	Иначе
		НайденныйУзел = СцТ_ГлавнаяФорма.СцТ_НайтиУзелДереваФормы(
			ДеревоЗначения,
			"Имя",
			"ИСТЗ_НаименованиеРучное");
			
		Если НайденныйУзел <> Неопределено Тогда
			ДеревоЗначения.ПолучитьЭлементы().Удалить(НайденныйУзел);
		КонецЕсли;
	КонецЕсли;
	
	///////////////////////////////////
	
	УзелРеквизитов = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"РеквизитФормы",
		ЭлементФормы_Тип);
		
	УзелРеквизитов.ПолучитьЭлементы().Очистить();
		
	УзелРеквизита = СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		УзелРеквизитов,
		ЭлементФормы_Имя,
		ЭлементФормы_Значение,
		ЭлементФормы_Заголовок,,,
		ДействиеСЭлементом,,
		ЭлементФормы_Вид);
		
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		УзелРеквизита,
		"ТипОбъекта",
		ЭлементФормы_Тип);
		
	// Способ поиска объекта
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелПутиПоискаОбъекта(
		УзелРеквизита,
		ПутьКЭлементуФормы);
	
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		УзелРеквизита,
		"ВариантПоискаОбъекта",
		ВариантПоискаОбъекта);
		
	//////////////////////////////////
	
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ДействиеПриОшибке",
		ДействиеПриОшибке);
		
	СцТ_ГлавнаяФорма.СцТ_ДобавитьУзелВДеревоЗначения(
		ДеревоЗначения,
		"ПеременнаяПриОшибке",
		ПеременнаяПриОшибке);
	
КонецПроцедуры

// Свертка данных шага в структуру
// и подготовка возвращаемой структуры отредактированного шага
&НаКлиенте
Функция СцТ_СформироватьСтруктуруданныхШага_НаКлиенте()
	
	// СДШ - структура данных шага
	СДШ = Новый Структура;
	
	СДШ.Вставить("ИдентификаторШага"           , ОА_ОписательПоложенияШагаВДереве.ИдентификаторШага);
	СДШ.Вставить("НаименованиеШага"            , ДШ_Наименование);
	СДШ.Вставить("ТипМетаданных"               , ДШ_ТипМетаданных);
	СДШ.Вставить("ИмяМетаданных"               , ДШ_ИмяМетаданных);
	СДШ.Вставить("Автоописание"                , ДШ_Автоописание);
	СДШ.Вставить("Описание"                    , ДШ_Описание);
	СДШ.Вставить("СкрытьАвтоописание"          , ДШ_СкрытьАвтоописание);
	СДШ.Вставить("ОбСсылка"                    , ДШ_ОбСсылка);
	СДШ.Вставить("ПредставлениеОбъекта"        , ДШ_ПредставлениеОбъекта);
	СДШ.Вставить("ВыполнитьВручную"            , ДШ_ВыполнитьВручную);
	СДШ.Вставить("Комментарий"                 , ДШ_Комментарий);
	СДШ.Вставить("ИмяФормы"                    , ДШ_ИмяФормы);
	СДШ.Вставить("ПредставлениеИмениМетаданных", ДШ_ПредставлениеИмениМетаданных);
	СДШ.Вставить("ЗаголовокФормы"              , ДШ_ЗаголовокФормы);
	СДШ.Вставить("ДанныеКартинки"              , ДШ_ДанныеКартинки);
	СДШ.Вставить("ДеревоЗначения"              , СцТ_ГлавнаяФорма.СцТ_ДеревоформыВДеревоСтруктур(ДеревоЗначения));
	
	
	// ВСД - возвращаемая структура данных
	ВСД = Новый Структура;
	ВСД.Вставить("ОписательПоложенияШагаВДереве", ОА_ОписательПоложенияШагаВДереве);
	ВСД.Вставить("Наименование"                 , ДШ_Наименование);
	ВСД.Вставить("Активность"                   , ОА_Активность);
	ВСД.Вставить("СтруктураДанныхШага"          , СДШ);
	ВСД.Вставить("ЭтоНовый"                     , ОА_ЭтоНовый);
	ВСД.Вставить("ТипШага"                      , ОА_ТипШага);
	
	Возврат ВСД;
	
КонецФункции

#КонецОбласти

#Область ФормированеОписанийИНаименований

// Формирование описания шага на клиенте
&НаКлиенте
Функция СцТ_СформироватьОписаниеШага_НаКлиенте(Краткое = Ложь)
	
	СцТ_Видимость      = Неопределено;
	СцТ_Доступность    = Неопределено;
	СцТ_ТолькоПросмотр = Неопределено;
	СцТ_Заголовок      = "";
	СцТ_ТекстПодсказки = "";
	
	СДО = Новый Структура;
	СДО.Вставить("ДействиеСОбъектом", ДействиеСЭлементом);
	СДО.Вставить("Имя"              , ЭлементФормы_Имя);
	СДО.Вставить("Заголовок"        , ЭлементФормы_Заголовок);
	СДО.Вставить("Значение"         , ЭлементФормы_Значение);
	СДО.Вставить("ВариантПоиска"    , ВариантПоискаОбъекта);
	
	ВозвращаемоеОписание = СцТ_ГлавнаяФорма.СцТ_ПолучитьОписаниеШага_ЗаполнитьЭлементФормы(СДО, Краткое);
	
	Возврат ВозвращаемоеОписание;
	
КонецФункции

&НаКлиенте
Процедура СцТ_ОбновитьНаименование_НаКлиенте(Принудительно = Ложь)
	
	Если НЕ НаименованиеШагаИзмененоВручную ИЛИ Принудительно Тогда
		ДШ_Наименование = СцТ_СформироватьОписаниеШага_НаКлиенте(Истина);
	КонецЕсли;
	
	Если Принудительно Тогда
		НаименованиеШагаИзмененоВручную = Ложь;
		Модифицированность = Истина;
		// В остальных случаях менять модифицированность не нужно,
		// так как ее должно изменить изменение другого элемента формы
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСДеревомЗначения

// Возвращает значение заданного поля в дереве данных
//
// Параметры
//	ДерЗнач              - дерево с данными в котором ведется поиск
//	ИмяПараметра         - имя искомого параметра
//	ЕслиНеНайдено        - что вернуть, если значение не найдено
//	ВозвращаемыйРеквизит - имя колонки, которую нужно вернуть
//	Рекурсивно           - признак рекурсии
// Возвращаемое значение - значение нужного поля найденного узла
&НаСервереБезКонтекста
Функция СцТ_ЗначениеВДереве(
	ДерЗнач,
	ИмяПараметра,
	ЕслиНеНайдено = Неопределено,
	ВозвращаемыйРеквизит = "Значение",
	Рекурсивно = Истина)
	
	УзелЗначения = СцТ_НайтиУзелДереваФормы(ДерЗнач, "Имя", ИмяПараметра, Рекурсивно);
	Если УзелЗначения = Неопределено Тогда
		Возврат ЕслиНеНайдено;
	Иначе
		Возврат УзелЗначения[ВозвращаемыйРеквизит];
	КонецЕсли;
	
КонецФункции

// Поиск нужного узла в дереве данных
//
// Параметры
//	ДеревоФормы       - дерево, в котором ведется поиск
//	Реквизит          - имя поля в котором ведется поиск
//	ЗначениеРеквизита - значение в поле
//	Рекурсивно        - признак рекурсивного поиска
// Возвращаемое значение - найденный узел
&НаСервереБезКонтекста
Функция СцТ_НайтиУзелДереваФормы(
	ДеревоФормы,
	Реквизит,
	ЗначениеРеквизита,
	Рекурсивно = Ложь)
	
	Если ДеревоФормы = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого Узел Из ДеревоФормы.ПолучитьЭлементы() Цикл
		Если Узел[Реквизит] = ЗначениеРеквизита Тогда
			Возврат Узел;
		КонецЕсли;
		Если Рекурсивно Тогда
			НайденыйУзел = СцТ_НайтиУзелДереваФормы(Узел, Реквизит, ЗначениеРеквизита, Истина);
			Если НайденыйУзел <> Неопределено Тогда
				Возврат НайденыйУзел;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Заполнение дерева значения данными из структуры
//
// параметры
//	ДеревоФормы - заполняемое дерево
//	ДеревоСтруктур - структура с данными
&НаСервереБезКонтекста
Процедура СцТ_ЗаполнитьДеревоФормыИзДереваСтруктур(ДеревоФормы, ДеревоСтруктур)
	
	СтрокиДереваФормы = ДеревоФормы.ПолучитьЭлементы();
	Для каждого СтрокаДереваСтруктур Из ДеревоСтруктур.ИСТЗ_Строки Цикл
		СтрокаДереваФормы = СтрокиДереваФормы.Добавить();
		Для каждого КлючЗначение Из СтрокаДереваСтруктур Цикл
			Если КлючЗначение.Ключ <> "ИСТЗ_Строки" Тогда
				СтрокаДереваФормы[КлючЗначение.Ключ] = КлючЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
		СцТ_ЗаполнитьДеревоФормыИзДереваСтруктур(СтрокаДереваФормы, СтрокаДереваСтруктур);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПреобразоватьЗначенияВДереве(ВеткаДерева, ДляОтображения = Истина)
	
	Для каждого СтрокаДерева Из ВеткаДерева.Строки Цикл
		
		Если ДляОтображения Тогда
			СтрокаДерева.Значение = ЗначениеИзСтрокиВнутр(СтрокаДерева.Значение);
		Иначе
			СтрокаДерева.Значение = ЗначениеВСтрокуВнутр(СтрокаДерева.Значение);
		КонецЕсли;
		
		ПреобразоватьЗначенияВДереве(СтрокаДерева, ДляОтображения);
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

// Обновление картинок на закладках
// Вызывается посредством обработки ожидания, чтобы форма нормально закрывалась после редактирования
&НаКлиенте
Процедура СцТ_ОбновитьЗаголовкиЗакладок()
	
	Элементы.Страница_РедактируемоеОписание.Картинка = ?(
		ПустаяСтрока(ДШ_Описание),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
	Элементы.Страница_Комментарий.Картинка = ?(
		ПустаяСтрока(ДШ_Комментарий),
		Новый Картинка,
		БиблиотекаКартинок.РежимПросмотраСпискаСписок);
	
КонецПроцедуры

// Формирование представление существующего шага
// параметры
// РодительскийШаг - указатель на данные родительского шага.
// НЕ может быть Неопределено
&НаКлиенте
Процедура ОтобразитьПредставлениеСуществующегоРодительскогоШага(РодительскийШаг)
	
	ПредставлениеГруппы = НСтр("ru = 'Шаг №%1 ""%2""'");
	ПредставлениеГруппы = СтрЗаменить(
		ПредставлениеГруппы,
		"%1",
		Формат(РодительскийШаг.НомерШага, "ЧГ=0"));
	
	ПредставлениеГруппы = СтрЗаменить(
		ПредставлениеГруппы,
		"%2",
		РодительскийШаг.Наименование);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностьюПеременнойОшибкиГрупповогоШага(УпрФорма)
	
	Если УпрФорма.ДействиеПриОшибке = "Остановить" Тогда
		УпрФорма.Элементы.СтраницыПеременнойОшибки.ТекущаяСтраница = УпрФорма.Элементы.СтраницыПеременнойОшибки_Пустая;
	Иначе
		УпрФорма.Элементы.СтраницыПеременнойОшибки.ТекущаяСтраница = УпрФорма.Элементы.СтраницыПеременнойОшибки_Заполненная;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПользовательскоеОписание()
	
	ТекстОписания = ДШ_Описание;
	
	Если НЕ ДШ_СкрытьАвтоописание Тогда
		ДШ_Автоописание = СцТ_СформироватьОписаниеШага_НаКлиенте();
		ТекстОписания = СцТ_ГлавнаяФорма.СцТ_ДобавитьПодстроку(ТекстОписания, ДШ_Автоописание, Символы.ПС);
	КонецЕсли;
	
	ПользовательскоеОписание = ТекстОписания;
	
КонецПроцедуры

// Получение контейнера тестируемого приложения
// Привязка
//
&НаКлиенте
Процедура СцТ_ПолучитьИнтерактивныйКонтейнер()
	
	// Так как шаг может быть еще не записан, то удобно получить форму для родительского шага
	Если ОА_ЭтоНовый Тогда
		РодительУзла = СцТ_ГлавнаяФорма.СцТ_ПолучитьДанныеШагаПоИдентификатору(ВызвавшаяФорма,ОА_ОписательПоложенияШагаВДереве.ИдентификаторУзлаРодителя);
	Иначе
		РодительУзла = ДанныеШага.ПолучитьРодителя();
	КонецЕсли;
	
	// Сообщения об ошибках показывать не нужно, так как редактирование шага может
	// выполняться без использования тестируемой формы
	ОписаниеОшибкиПолученияКонтейнера = "";
	ИнтерактивныйКонтейнер = СцТ_ГлавнаяФорма.СцТ_ПолучитьИнтерактивныйКонтейнерДляШагаСценария(
		РодительУзла,
		ОписаниеОшибкиПолученияКонтейнера,
		Истина);
	
	Если НЕ ПустаяСтрока(ОписаниеОшибкиПолученияКонтейнера) Тогда
		ИнтерактивныйКонтейнер = Неопределено;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ИнтерактивныйКонтейнер) <> Тип("ТестируемаяФорма") И ОА_ТипШага = "ЗаполнитьОдинРеквизитФормы"
		ИЛИ ТипЗнч(ИнтерактивныйКонтейнер) <> Тип("ТестируемаяТаблицаФормы") И ОА_ТипШага = "ЗаполнитьКолонкуТаблицы" Тогда
		ИнтерактивныйКонтейнер = Неопределено;
		Возврат;
	КонецЕсли;
	
	Если ОА_ТипШага = "ЗаполнитьОдинРеквизитФормы" Тогда
		
		// Проверить, открыта ли форма
		Попытка
			ФормаЗакрыта = ИнтерактивныйКонтейнер.ОжидатьЗакрытие(0);
		Исключение
			ФормаЗакрыта = Истина;
		КонецПопытки;
		
		Если ФормаЗакрыта Тогда
			ИнтерактивныйКонтейнер = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПутьКЭлементуФормы(РодительскийУзелПути)
	
	УзелПутиВДереве = СцТ_НайтиУзелДереваФормы(РодительскийУзелПути, "Имя", "ИСТЗ_ПутьКОбъекту");
	
	Если УзелПутиВДереве = Неопределено Тогда
		ПутьКЭлементуФормы = Неопределено;
		Возврат;
	КонецЕсли;
	
	ПутьКЭлементуФормы = Новый Структура;
	Счетчик = 1;
	
	Для каждого УзелЭлементаПути Из УзелПутиВДереве.Получитьэлементы() Цикл
		
		СтруктураСтроки = Новый Структура;
		СтруктураСтроки.Вставить("ИмяОбъекта"      , УзелЭлементаПути.Имя);
		СтруктураСтроки.Вставить("ЗаголовокОбъекта", УзелЭлементаПути.Синоним);
		
		// Для совместимости с предыдущими версиями
		Если ЗначениеЗаполнено(УзелЭлементаПути.ТипУзла) Тогда
			СтруктураСтроки.Вставить("ТипОбъекта", УзелЭлементаПути.ТипУзла);
		Иначе
			Если ТипЗнч(УзелЭлементаПути.Значение) = Тип("Число") Тогда
				Если УзелЭлементаПути.Значение = 0 Тогда
					НовыйТип = "ТестируемоеПриложение";
				ИначеЕсли УзелЭлементаПути.Значение = 1 Тогда
					НовыйТип = "ТестируемоеОкноКлиентскогоПриложения";
				ИначеЕсли УзелЭлементаПути.Значение = 2 Тогда
					НовыйТип = "ТестируемаяФорма";
				ИначеЕсли УзелЭлементаПути.Значение = 3 Тогда
					НовыйТип = "ТестируемоеПолеФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 4 Тогда
					НовыйТип = "ТестируемаяГруппаФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 5 Тогда
					НовыйТип = "ТестируемаяКнопкаФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 6 Тогда
					НовыйТип = "ТестируемаяДекорацияФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 7 Тогда
					НовыйТип = "ТестируемаяТаблицаФормы";
				ИначеЕсли УзелЭлементаПути.Значение = 8 Тогда
					НовыйТип = "ТестируемыйКомандныйИнтерфейсОкна";
				ИначеЕсли УзелЭлементаПути.Значение = 9 Тогда
					НовыйТип = "ТестируемаяГруппаКомандногоИнтерфейса";
				ИначеЕсли УзелЭлементаПути.Значение = 10 Тогда
					НовыйТип = "ТестируемаяКнопкаКомандногоИнтерфейса";
				Иначе
					НовыйТип = "";
				КонецЕсли;
			КонецЕсли;
			
			СтруктураСтроки.Вставить("ТипОбъекта", НовыйТип);
		КонецЕсли;
		
		ИмяСтруктурыСтроки = "Str_" + Формат(Счетчик, "ЧГ=0");
		ПутьКЭлементуФормы.Вставить(ИмяСтруктурыСтроки, СтруктураСтроки);
		
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполнение списка выбора действий для поля ввода
//
// Параметры
//	СписокВыбора - заполняемый список выбора
//	ВидПоляФормы - вид поля формы
//	Кратко - признак сокращенного заполнения
//	ПредставлениеПредставление - представление
&НаКлиентеНаСервереБезКонтекста
Процедура СцТ_ЗаполнитьСписокВыбораДействийДляПоляВвода(
	СписокВыбора,
	ВидПоляФормы,
	ПредставлениеПредставление = Истина)
	
	СписокВыбора.Очистить();
	Если ВидПоляФормы = "ПолеВвода" Тогда
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Ввести текст'"), "ВвестиТекст"),
			НСтр("ru = 'Ввести текст'"));
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Ввод по строке'"), "ВводПоСтроке"),
			НСтр("ru = 'Ввод по строке'"));
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Открыть'")
			, "Открыть")
			, НСтр("ru = 'Открыть'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Нажать кнопку выбора (F4)'")
			, "НажатьКнопкуВыбора")
			, НСтр("ru = 'Нажать кнопку выбора (F4)'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Создать'")
			, "Создать")
			, НСтр("ru = 'Создать'"));
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Выбрать из списка выбора'")
			, "ВыбратьИзСпискаВыбора")
			, НСтр("ru = 'Выбрать из списка выбора'"));
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Выбрать из выпадающего списка'")
			, "ВыбратьИзВыпадающегоСписка")
			, НСтр("ru = 'Выбрать из выпадающего списка'"));
			
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Очистить'")
			, "Очистить")
			, НСтр("ru = 'Очистить'"));
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Увеличить значение'")
			, "УвеличитьЗначение")
			, НСтр("ru = 'Увеличить значение'"));
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Уменьшить значение'")
			, "УменьшитьЗначение")
			, НСтр("ru = 'Уменьшить значение'"));
		
	ИначеЕсли ВидПоляФормы = "ПолеКалендаря" Тогда
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Перейти к дате'")
			, "ПерейтиКДате")
			, НСтр("ru = 'Перейти к дате'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Выбрать'")
			, "Выбрать")
			, НСтр("ru = 'Выбрать'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Перейти на год вперед'")
			, "ПерейтиНаГодВперед")
			, НСтр("ru = 'Перейти на год вперед'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Перейти на год назад'")
			, "ПерейтиНаГодНазад")
			, НСтр("ru = 'Перейти на год назад'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Перейти на месяц вперед'")
			, "ПерейтиНаМесяцВперед")
			, НСтр("ru = 'Перейти на месяц вперед'"));
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Перейти на месяц назад'")
			, "ПерейтиНаМесяцНазад")
			, НСтр("ru = 'Перейти на месяц назад'"));
		
	ИначеЕсли ВидПоляФормы = "ПолеПереключателя" Тогда
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Выбрать вариант'")
			, "ВыбратьВариант")
			, НСтр("ru = 'Выбрать вариант'"));
		
	ИначеЕсли ВидПоляФормы = "ПолеФлажка" Тогда
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Установить отметку'")
			, "УстановитьОтметку")
			, НСтр("ru = 'Установить отметку'"));
			
	ИначеЕсли ВидПоляФормы = "Надпись" Тогда
		
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Нажать гиперссылку'")
			, "НажатьГиперссылку")
			, НСтр("ru = 'Нажать гиперссылку'"));
			
		СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Проверить текст'")
			, "ПроверитьТекст")
			, НСтр("ru = 'Проверить текст'"));
		
	КонецЕсли;
	
	СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Активизировать'")
		, "Активизировать")
		, НСтр("ru = 'Активизировать'"));
	
	СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Проверить'")
		, "Проверить")
		, НСтр("ru = 'Проверить'"));
	СписокВыбора.Добавить(?(ПредставлениеПредставление, НСтр("ru = 'Получить сведения'")
		, "ПолучитьСведения")
		, НСтр("ru = 'Получить сведения'"));
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДействиеВЗависимостиОтВида()
	
	ДействиеСЭлементом_Вр = ДействиеСЭлементом;
	
	СцТ_ЗаполнитьСписокВыбораДействийДляПоляВвода(
			Элементы.ДействиеСЭлементом.СписокВыбора,
			ЭлементФормы_Вид,
			Ложь);
			
	Если Элементы.ДействиеСЭлементом.СписокВыбора.НайтиПоЗначению(ДействиеСЭлементом_Вр) <> Неопределено Тогда
		ДействиеСЭлементом = ДействиеСЭлементом_Вр;
	Иначе
		ДействиеСЭлементом = Элементы.ДействиеСЭлементом.СписокВыбора[0].Значение;
		
		НастроитьВидЭлементаФормыЗначение(ЭтотОбъект);
	КонецЕсли;
	
	СцТ_ОбновитьНаименование_НаКлиенте();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВидЭлементаФормыЗначение(УпрФорма)
	
	Если    УпрФорма.ДействиеСЭлементом = "Открыть"
		ИЛИ УпрФорма.ДействиеСЭлементом = "НажатьКнопкуВыбора"
		ИЛИ УпрФорма.ДействиеСЭлементом = "Создать"
		ИЛИ УпрФорма.ДействиеСЭлементом = "Очистить"
		ИЛИ УпрФорма.ДействиеСЭлементом = "Выбрать"
		//ИЛИ УпрФорма.ДействиеСЭлементом = "УстановитьОтметку"
		ИЛИ УпрФорма.ДействиеСЭлементом = "НажатьГиперссылку"
		ИЛИ УпрФорма.ДействиеСЭлементом = "Активизировать" Тогда
		
		УпрФорма.Элементы.СтраницыЗначения.ТекущаяСтраница = УпрФорма.Элементы.СтраницыЗначения_Пустая;
		
		
	ИначеЕсли УпрФорма.ДействиеСЭлементом = "УвеличитьЗначение"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "УменьшитьЗначение"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ПерейтиНаГодВперед"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ПерейтиНаГодНазад"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ПерейтиНаМесяцВперед"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ПерейтиНаМесяцНазад" Тогда
		
		УпрФорма.Элементы.СтраницыЗначения.ТекущаяСтраница = УпрФорма.Элементы.СтраницыЗначения_КоличествоРаз;
		
	ИначеЕсли УпрФорма.ДействиеСЭлементом = "ВводПоСтроке"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ВыбратьИзСпискаВыбора"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ВыбратьИзВыпадающегоСписка"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ПерейтиКДате"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ВыбратьВариант"
		ИЛИ   УпрФорма.ДействиеСЭлементом = "ПроверитьТекст" Тогда
		
		УпрФорма.Элементы.СтраницыЗначения.ТекущаяСтраница = УпрФорма.Элементы.СтраницыЗначения_Строка;
		
	ИначеЕсли (УпрФорма.ДействиеСЭлементом = "ВвестиТекст"
		ИЛИ    УпрФорма.ДействиеСЭлементом = "Проверить"
		ИЛИ    УпрФорма.ДействиеСЭлементом = "ПолучитьСведения")
		И ТипЗнч(УпрФорма.ЭлементФормы_Значение) = Тип("Строка") Тогда
		
		УпрФорма.Элементы.СтраницыЗначения.ТекущаяСтраница = УпрФорма.Элементы.СтраницыЗначения_Многострочный;
		
	Иначе
		
		// Страница по умолчанию
		УпрФорма.Элементы.СтраницыЗначения.ТекущаяСтраница = УпрФорма.Элементы.СтраницыЗначения_Строка;
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

