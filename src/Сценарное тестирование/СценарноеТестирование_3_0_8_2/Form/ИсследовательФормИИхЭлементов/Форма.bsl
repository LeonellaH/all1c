#Область Переменные

&НаКлиенте
Перем ВладелецОбъектов Экспорт;

&НаКлиенте
Перем ТипыОтображаемыхОбъектов Экспорт;

&НаКлиенте
Перем ТипыВыбираемыхОбъектов Экспорт;

&НаКлиенте
Перем ПодсвечиваемыеПриВыборе Экспорт;

&НаКлиенте
Перем ТекущиеЭлементы Экспорт;

&НаКлиенте
Перем ОбъектыТестируемогоПриложения;

&НаКлиенте
Перем ПутиПоискаОбъектов;

&НаКлиенте
Перем Автоматически Экспорт;

&НаКлиенте
Перем НеВыбиратьПодчиненныеИзТаблиц Экспорт;

&НаКлиенте
Перем ЗапретитьВыборПодсвеченных Экспорт;

&НаКлиенте
Перем СцТ_ГлавнаяФорма Экспорт;


#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаОбработка = РеквизитФормыВЗначение("Объект");
	
	ПутьКФормам         = ЭтаОбработка.Метаданные().ПолноеИмя() + ".Форма.";
	
	МножественныйВыбор = Параметры.МножественныйВыбор;
	Элементы.ДеревоОбъектовОтметка.Видимость            = МножественныйВыбор;
	Элементы.ДеревоОбъектовОтметитьВсе.Видимость        = МножественныйВыбор;
	Элементы.ДеревоОбъектовСнятьОтметкуСоВсех.Видимость = МножественныйВыбор;
	Элементы.КМОтметитьВыделенные.Видимость             = МножественныйВыбор;
	Элементы.КМСнятьОтметкуСВыделенных.Видимость        = МножественныйВыбор;
	
	Если ТипЗнч(Параметры.ВидимостьКолонок) = Тип("Соответствие") Тогда
		Для каждого КолонкаВидимость Из Параметры.ВидимостьКолонок Цикл
			Элементы[КолонкаВидимость.Ключ].Видимость = КолонкаВидимость.Значение;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СцТ_ВыполнитьДействияПриОткрытии();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_ДеревоОбъектов

&НаКлиенте
Процедура ДеревоОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СцТ_ВыбратьОбъектыИЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура АктивизироватьВТестируемомПриложении(Команда)
	
	Попытка
		ОбъектыТестируемогоПриложения[Элементы.ДеревоОбъектов.ТекущаяСтрока].Активизировать();
	Исключение
		ПоказатьПредупреждение(, ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	СцТ_УстановитьОтметкиВДеревеОбъектов(ДеревоОбъектов, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуСоВсех(Команда)
	
	СцТ_УстановитьОтметкиВДеревеОбъектов(ДеревоОбъектов, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВыделенные(Команда)
	
	Для каждого ИдентификаторВыделеннойСтроки Из Элементы.ДеревоОбъектов.ВыделенныеСтроки Цикл
		Узел = ДеревоОбъектов.НайтиПоИдентификатору(ИдентификаторВыделеннойСтроки);
		Если Узел.ДоступенДляВыбора Тогда
			Узел.Отметка = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметкуСВыделенных(Команда)
	
	Для каждого ИдентификаторВыделеннойСтроки Из Элементы.ДеревоОбъектов.ВыделенныеСтроки Цикл
		Узел = ДеревоОбъектов.НайтиПоИдентификатору(ИдентификаторВыделеннойСтроки);
		Если Узел.ДоступенДляВыбора Тогда
			Узел.Отметка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ИнициализироватьПостроениеДереваОбъектов();
	
	//ИдентификаторТекущеСтроки = Элементы.ДеревоОбъектов.ТекущаяСтрока;
	//ДеревоОбъектов.ПолучитьЭлементы().Очистить();
	//СцТ_ОтобразитьОбъектыТестируемогоПриложения();
	//СцТ_ВыполнитьДействияПриОткрытии();
	//
	//Попытка
	//	Элементы.ДеревоОбъектов.ТекущаяСтрока = ИдентификаторТекущеСтроки;
	//Исключение
	//КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	СцТ_ВыбратьОбъектыИЗакрытьФорму();
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции


&НаКлиенте
Процедура ИнициализироватьПостроениеДереваОбъектов()
	
	// Попытка получить элемент тестируемого приложения
	Если СцТ_ГлавнаяФорма = Неопределено Тогда
		ТекстПредупреждения = НСтр("ru = 'Не удалось соединиться с основной формой обработки'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	// Проверка, что запущен менеджер тестирования
	Если НЕ СцТ_ГлавнаяФорма.ЭтоМенеджерТестирования Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Текущий клиент не является менеджером тестирования.
											|Для запуска клиента в режиме менеджера тестирования необходимо использовать
											|дополнительный параметр запуска информационной базы ""/TESTMANAGER""'"));
		Возврат;
	КонецЕсли;
	
	
	// Попытка соединиться с тестируемым приложением
	Если НЕ СцТ_ГлавнаяФорма.СцТ_ТестируемоеПриложениеАктивно() Тогда
		// Нужно подключить тестируемое приложение
		ПоказатьПредупреждение(, НСтр("ru = 'Тестируемое приложение не запущено.
											|Для запуска тестируемого приложения воспользуйтесь командой ""Запустить тестируемое приложение""
											|в меню обработки Сценарного тестирования ""Сценарий""'"));
		Возврат;
	КонецЕсли;
	
	ТестируемоеПриложение = СцТ_ГлавнаяФорма.СцТ_Контроллер;
	
	ДеревоОбъектов.ПолучитьЭлементы().Очистить();
	
	//СцТ_ОтобразитьОбъектыТестируемогоПриложения();
	//СцТ_ПостроитьДеревоОбъектовРекурсивно(ВладелецОбъектов, ДеревоОбъектов);
	
	СцТ_РекурсивноеЗаполнениеДереваОбъектов(ТестируемоеПриложение, ДеревоОбъектов);
	
КонецПроцедуры



&НаКлиенте
Процедура СцТ_ОтобразитьОбъектыТестируемогоПриложения() Экспорт
	
	Попытка
		
		ОбъектыТестируемогоПриложения = Новый Соответствие;
		ПутиПоискаОбъектов            = Новый Соответствие;
		Если ТипЗнч(ВладелецОбъектов) <> Тип("Массив") Тогда
			СцТ_ПостроитьДеревоОбъектовРекурсивно(ВладелецОбъектов, ДеревоОбъектов);
		Иначе
			Для каждого ОписаниеВладельца Из ВладелецОбъектов Цикл
				СцТ_ПостроитьДеревоОбъектовРекурсивно(ОписаниеВладельца, ДеревоОбъектов);
			КонецЦикла;
		КонецЕсли;
		
	Исключение
		
		Если Автоматически <> Истина Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Не удалось отобразить список объектов: %1'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", ОписаниеОшибки());
			ПоказатьПредупреждение(, ТекстСообщения);
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_ВыполнитьДействияПриОткрытии()
	
	// Развернуть все элементы
	Для каждого УзелОбъекта Из ДеревоОбъектов.ПолучитьЭлементы() Цикл
		Элементы.ДеревоОбъектов.Развернуть(УзелОбъекта.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
	Элементы.ДеревоОбъектов.ВыделенныеСтроки.Очистить();
	Если ТекущиеЭлементы <> Неопределено Тогда
		СцТ_ОтметитьЭлементы(ДеревоОбъектов, ТекущиеЭлементы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СцТ_ИмяТекущегоЭлементаФормы(ТекущаяФормаТестируемогоПриложения)
	
	Попытка
		
		ТекущийЭлементФормы = ТекущаяФормаТестируемогоПриложения.ПолучитьТекущийЭлемент();
		Если ТекущийЭлементФормы = Неопределено Тогда
			Возврат "$";
		КонецЕсли;
		
		ИмяЭлемента = СцТ_ГлавнаяФорма.СцТ_ИмяИнтерактивногоОбъекта(ТекущийЭлементФормы);
		Возврат ?(ПустаяСтрока(ИмяЭлемента), "$", ИмяЭлемента);
		
	Исключение
		Возврат "$";
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Функция СцТ_ИмяКнопкиФормыПоУмолчанию(ТекущаяФормаТестируемогоПриложения)
	
	Попытка
		
		КнопкаФормыПоУмолчанию = ТекущаяФормаТестируемогоПриложения.НайтиКнопкуПоУмолчанию();
		Если КнопкаФормыПоУмолчанию = Неопределено Тогда
			Возврат "$";
		КонецЕсли;
		
		ИмяЭлемента = СцТ_ГлавнаяФорма.СцТ_ИмяИнтерактивногоОбъекта(КнопкаФормыПоУмолчанию);
		
		Возврат ?(ПустаяСтрока(ИмяЭлемента), "$", ИмяЭлемента);
		
	Исключение
		Возврат "$";
	КонецПопытки;
	
КонецФункции

// Параметры:
//	ТипОкна (Число):  0 - без окна, 1 - основное окно, 2 - клиентское окно
&НаКлиенте
Процедура СцТ_ПостроитьДеревоОбъектовРекурсивно(
	ТекущийОбъект,
	РодительскаяВетвь,
	Знач ИмяТекущегоЭлемента = "$",
	Знач ИмяКнопкиПоУмолчанию = "$",
	Знач ТипОкна             = 0,
	Знач ТаблицаНаФорме      = Неопределено,
	Знач ПутьКОбъекту        = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = СцТ_ГлавнаяФорма.СцТ_Контроллер;
	КонецЕсли;
	
	Если ТипЗнч(ТекущийОбъект) = Тип("Структура") Тогда
		
		// Текущий объект должен быть отображен в дереве своим узлом
		РодительскийУзел = РодительскаяВетвь.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(РодительскийУзел, ТекущийОбъект);
		ОбъектыТестируемогоПриложения[РодительскийУзел.ПолучитьИдентификатор()] = ТекущийОбъект.ОбъектТестируемогоПриложения;
		СцТ_ПостроитьДеревоОбъектовРекурсивно(
			ТекущийОбъект.ОбъектТестируемогоПриложения,
			РодительскийУзел,
			,
			,
			РодительскийУзел.ТипОкна);
		
		Возврат;
		
	КонецЕсли;
	
	// Заглушка для объектов, не имеющих подчиненных объектов
	Если НЕ СцТ_ГлавнаяФорма.СцТ_ИнтерактивныйОбъектДопускаетНаличиеПодчиненныхОбъектов(ТекущийОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ТипТекущегоОбъекта = ТипЗнч(ТекущийОбъект);
	Если ТипТекущегоОбъекта = Тип("ТестируемаяФорма") ИЛИ ТипТекущегоОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
		ИмяТекущегоЭлемента  = СцТ_ИмяТекущегоЭлементаФормы(ТекущийОбъект);
		ИмяКнопкиПоУмолчанию = СцТ_ИмяКнопкиФормыПоУмолчанию(ТекущийОбъект);
	КонецЕсли;
	
	Если ПутьКОбъекту = Неопределено Тогда
		ПутьКОбъекту = Новый Массив;
	КонецЕсли;
	
	ЗаголовокОбъекта = СцТ_ГлавнаяФорма.СцТ_ЗаголовокИнтерактивногоОбъекта(ТекущийОбъект);
	ИмяОбъекта       = СцТ_ГлавнаяФорма.СцТ_ИмяИнтерактивногоОбъекта(ТекущийОбъект);
	Если ПустаяСтрока(ИмяОбъекта) Тогда
		ИмяОбъекта = "$" + ЗаголовокОбъекта;
	КонецЕсли;
	
	ПутьКОбъекту.Добавить(Новый Структура("ТипОбъекта,ИмяОбъекта,ЗаголовокОбъекта",
										  ТипТекущегоОбъекта,
										  ИмяОбъекта,
										  ЗаголовокОбъекта));
	
	Если ТаблицаНаФорме = Неопределено
		И ТипТекущегоОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
		ТаблицаНаФорме = ТекущийОбъект;
		Попытка
			Если ТаблицаНаФорме.ТекущийРежимРедактирование() Тогда
				ТаблицаНаФорме.ЗакончитьРедактированиеСтроки();
			КонецЕсли;
		Исключение
		КонецПопытки;
	Иначе
		ТаблицаНаФормеДляПередачиДочерним = ТаблицаНаФорме;
	КонецЕсли;
	
	Если ТипТекущегоОбъекта = Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
		Если СцТ_ВСпискеПрисутствуетОбъект(ТипыОтображаемыхОбъектов, Тип("ТестируемыйКомандныйИнтерфейсОкна"), "КомандныйИнтерфейс")
			ИЛИ СцТ_ВСпискеПрисутствуетОбъект(ТипыОтображаемыхОбъектов, Тип("ТестируемаяГруппаКомандногоИнтерфейса"), "ГруппаКомандногоИнтерфейса")
			ИЛИ СцТ_ВСпискеПрисутствуетОбъект(ТипыОтображаемыхОбъектов, Тип("ТестируемаяКнопкаКомандногоИнтерфейса"), "КнопкаКомандногоИнтерфейса") Тогда
			
			Попытка
				
				КомандныйИнтерфейсОбъект = ТекущийОбъект.ПолучитьКомандныйИнтерфейс();
				УзелКомандногоИнтерфейса = РодительскаяВетвь.ПолучитьЭлементы().Добавить();
				УзелКомандногоИнтерфейса.ПредставлениеОбъектаПриложения = НСтр("ru = 'Командный интерфейс'");
				УзелКомандногоИнтерфейса.ИндексКартинки                 = 3;
				УзелКомандногоИнтерфейса.Тип = Тип("ТестируемыйКомандныйИнтерфейсОкна");
				ОбъектыТестируемогоПриложения[УзелКомандногоИнтерфейса.ПолучитьИдентификатор()] = КомандныйИнтерфейсОбъект;
				УзелКомандногоИнтерфейса.ТипОкна = РодительскаяВетвь.ТипОкна;
				СцТ_ПостроитьДеревоОбъектовРекурсивно(
					КомандныйИнтерфейсОбъект,
					УзелКомандногоИнтерфейса,
					,
					,
					УзелКомандногоИнтерфейса.ТипОкна);
				
			Исключение
			КонецПопытки;
			
		КонецЕсли;
	КонецЕсли;
	
	ЭлементыРодительскойВетви = РодительскаяВетвь.ПолучитьЭлементы();
	ПодчиненныеОбъекты = РодительскаяВетвь.ПолучитьЭлементы();
	//ПодчиненныеОбъекты        = Неопределено;
	//Попытка
	//	ПодчиненныеОбъекты = ТекущийОбъект.ПолучитьПодчиненныеОбъекты();
	//Исключение
	//	Возврат;
	//КонецПопытки;
	//
	//Если ТипЗнч(ПодчиненныеОбъекты) <> Тип("ФиксированныйМассив") Тогда
	//	Возврат;
	//КонецЕсли;
	
	Для каждого ПодчиненныйОбъект Из ПодчиненныеОбъекты Цикл
		
		ТипОбъекта = ТипЗнч(ПодчиненныйОбъект);
		
		// Определение типа окна для всех дочерних объектов - в каком окне они находятся - 
		// без окна (0), в основном (1) или в клиентском (2)
		ТипТекущегоОкна = ?(ТипОбъекта = Тип("ТестируемоеОкноКлиентскогоПриложения"),
							?(ПодчиненныйОбъект.Основное, 1, 2),
							ТипОкна);
		
		ОписаниеВидаОбъекта = СцТ_ГлавнаяФорма.СцТ_ПолучитьВидОбъекта(ПодчиненныйОбъект);
		
		Если НЕ СцТ_ВСпискеПрисутствуетОбъект(ТипыОтображаемыхОбъектов, ТипОбъекта, ОписаниеВидаОбъекта.Вид) Тогда
			Если НеВыбиратьПодчиненныеИзТаблиц = Истина
				И ТипОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
				//ПутьКОбъекту.Удалить(ПутьКОбъекту.ВГраница());
				Продолжить;
			КонецЕсли;
			
			СцТ_ПостроитьДеревоОбъектовРекурсивно(
				ПодчиненныйОбъект,
				РодительскаяВетвь,
				ИмяТекущегоЭлемента,
				ИмяКнопкиПоУмолчанию,
				ТипТекущегоОкна,
				ТаблицаНаФорме,
				ПутьКОбъекту);
			
		Иначе
			
			СтрокаДереваОбъектов = ЭлементыРодительскойВетви.Добавить();
			ИдентификаторСтроки = СтрокаДереваОбъектов.ПолучитьИдентификатор();
			СтрокаДереваОбъектов.ДоступенДляВыбора = СцТ_ВСпискеПрисутствуетОбъект(
				ТипыВыбираемыхОбъектов,
				ТипОбъекта,
				ОписаниеВидаОбъекта.Вид);
			
			// Определение видимости, доступности, только просмотр
			//АтрибутыДоступности = СцТ_ГлавнаяФорма.СцТ_АтрибутыДоступностиИнтерактивногоОбъекта(ПодчиненныйОбъект);
			//ЗаполнитьЗначенияСвойств(СтрокаДереваОбъектов, АтрибутыДоступности);
			
			ОбъектыТестируемогоПриложения[ИдентификаторСтроки] = ПодчиненныйОбъект;
			
			СтрокаДереваОбъектов.Имя = СцТ_ГлавнаяФорма.СцТ_ИмяИнтерактивногоОбъекта(ПодчиненныйОбъект);
			СтрокаДереваОбъектов.Заголовок = СцТ_ГлавнаяФорма.СцТ_ЗаголовокИнтерактивногоОбъекта(ПодчиненныйОбъект);
			Если ПустаяСтрока(СтрокаДереваОбъектов.Имя) Тогда
				СтрокаДереваОбъектов.Имя = "$" + СтрокаДереваОбъектов.Заголовок;
			КонецЕсли;
			
			СтрокаДереваОбъектов.Тип       = ТипОбъекта;
			
			СтрокаДереваОбъектов.Вид         = ОписаниеВидаОбъекта.Вид;
			СтрокаДереваОбъектов.СинонимВида = ОписаниеВидаОбъекта.Синоним;
			
			СтрокаДереваОбъектов.ПредставлениеОбъектаПриложения = "";
				//СцТ_ГлавнаяФорма.СцТ_ПолучитьПредставлениеОбъекта(СтрокаДереваОбъектов, (ТаблицаНаФорме <> Неопределено));
			
			СтрокаДереваОбъектов.ИндексКартинки = СцТ_ПолучитьИндексКартинки(СтрокаДереваОбъектов);
			
			Если ТипОбъекта = Тип("ТестируемоеОкноКлиентскогоПриложения") И ТипТекущегоОкна = 1 Тогда
				СтрокаДереваОбъектов.ВыделитьЖирным = Истина;
				СтрокаДереваОбъектов.ПредставлениеОбъектаПриложения =
					СтрокаДереваОбъектов.ПредставлениеОбъектаПриложения + НСтр("ru = ' (главное окно)'");
			КонецЕсли;
			
			СцТ_ЗакрепитьПутьКОбъекту(
				ИдентификаторСтроки,
				ПутьКОбъекту,
				ТипОбъекта,
				СтрокаДереваОбъектов.Имя,
				СтрокаДереваОбъектов.Заголовок);
			
			СтрокаДереваОбъектов.ПредставлениеДанных = СцТ_ГлавнаяФорма.СцТ_ДанныеИнтерактивногоОбъекта(
				ПодчиненныйОбъект,
				ОписаниеВидаОбъекта.Вид,
				ТаблицаНаФорме,
				СтрокаДереваОбъектов.Значение
			);
			
			Если ПодсвечиваемыеПриВыборе <> Неопределено Тогда
				СтрокаДереваОбъектов.Подсветить = СцТ_ВСпискеПрисутствуетОбъект(
					ПодсвечиваемыеПриВыборе,
					ТипОбъекта,
					ОписаниеВидаОбъекта.Вид,
					СтрокаДереваОбъектов.Имя,
					СтрокаДереваОбъектов.Заголовок,
					ПутиПоискаОбъектов[ИдентификаторСтроки]);
			КонецЕсли;
			
			Если СтрокаДереваОбъектов.Подсветить Тогда
				Если ЗапретитьВыборПодсвеченных = Истина Тогда
					СтрокаДереваОбъектов.ДоступенДляВыбора = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаДереваОбъектов.ЭтоТекущийЭлемент = (СтрокаДереваОбъектов.Имя = ИмяТекущегоЭлемента);
			СтрокаДереваОбъектов.КнопкаПоУмолчанию = (ТипОбъекта = Тип("ТестируемаяКнопкаФормы")
				И СтрокаДереваОбъектов.Имя = ИмяКнопкиПоУмолчанию);
			СтрокаДереваОбъектов.ТипОкна = ТипТекущегоОкна;
			
			Если НеВыбиратьПодчиненныеИзТаблиц = Истина
				И ТипОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
				//ПутьКОбъекту.Удалить(ПутьКОбъекту.ВГраница());
				Продолжить;
			КонецЕсли;
			
			СцТ_ПостроитьДеревоОбъектовРекурсивно(
				ПодчиненныйОбъект,
				СтрокаДереваОбъектов,
				ИмяТекущегоЭлемента,
				ИмяКнопкиПоУмолчанию,
				ТипТекущегоОкна,
				ТаблицаНаФорме,
				ПутьКОбъекту);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПутьКОбъекту.Удалить(ПутьКОбъекту.ВГраница());
	
	
	//// Удаление элементов, не содержащих требуемых элементов
	//КоличествоЭлементов = ЭлементыРодительскойВетви.Количество();
	//Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
	//	
	//	УзелОбъекта = ЭлементыРодительскойВетви.Получить(КоличествоЭлементов - ОбратныйИндекс);
	//	Если УзелОбъекта.ПолучитьЭлементы().Количество() = 0
	//		И НЕ УзелОбъекта.ДоступенДляВыбора
	//		И НЕ УзелОбъекта.Подсветить Тогда
	//		
	//		ЭлементыРодительскойВетви.Удалить(УзелОбъекта);
	//		
	//	КонецЕсли;
	//	
	//КонецЦикла;
	//
КонецПроцедуры


&НаКлиенте
Процедура СцТ_РекурсивноеЗаполнениеДереваОбъектов(ИнтерактивныйОбъект, РодительскаяВеткаДерева)
	
	// Если интерактивный объект не может иметь подчиненных, то выход
	Если НЕ СцТ_ГлавнаяФорма.СцТ_ИнтерактивныйОбъектДопускаетНаличиеПодчиненныхОбъектов(ИнтерактивныйОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ПодчиненныйИО Из ИнтерактивныйОбъект.ПолучитьПодчиненныеОбъекты() Цикл
		НовыйУзел = РодительскаяВеткаДерева.ПолучитьЭлементы().Добавить();
		
		Если ТипЗнч(ПодчиненныйИО) = Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
			
			НовыйУзел.Заголовок = ПодчиненныйИО.Заголовок;
			
			Если ПодчиненныйИО.Основное И ПодчиненныйИО.НачальнаяСтраница Тогда
				НовыйУзел.ТипОкна = 3;
			ИначеЕсли ПодчиненныйИО.Основное И НЕ ПодчиненныйИО.НачальнаяСтраница Тогда
				НовыйУзел.ТипОкна = 2;
			ИначеЕсли НЕ ПодчиненныйИО.Основное И ПодчиненныйИО.НачальнаяСтраница Тогда
				НовыйУзел.ТипОкна = 1;
			ИначеЕсли НЕ ПодчиненныйИО.Основное И НЕ ПодчиненныйИО.НачальнаяСтраница Тогда
				НовыйУзел.ТипОкна = 0;
			КонецЕсли;
			
			НовыйУзел.Тип = Тип("ТестируемоеОкноКлиентскогоПриложения");
			
			
			КомИнтерфейс = ПодчиненныйИО.ПолучитьКомандныйИнтерфейс();
			ТекстыСообщений = ПодчиненныйИО.ПолучитьТекстыСообщенийПользователю();
			
			
			// Подчиненные элементы есть
			СцТ_РекурсивноеЗаполнениеДереваОбъектов(ПодчиненныйИО, НовыйУзел);
			
			
		ИначеЕсли ТипЗнч(ПодчиненныйИО) = Тип("ТестируемаяФорма") Тогда
			
			НовыйУзел.Имя       = ПодчиненныйИО.ИмяФормы;
			НовыйУзел.Заголовок = ПодчиненныйИО.ТекстЗаголовка;
			
			НовыйУзел.Тип = Тип("ТестируемаяФорма");
			
			
			КомПанель = ПодчиненныйИО.ПолучитьКоманднуюПанель();
			
			// Подчиненные элементы есть
			СцТ_РекурсивноеЗаполнениеДереваОбъектов(ПодчиненныйИО, НовыйУзел);
			
		ИначеЕсли ТипЗнч(ПодчиненныйИО) = Тип("ТестируемаяГруппаФормы") Тогда
			
			НовыйУзел.Имя       = ПодчиненныйИО.Имя;
			НовыйУзел.Заголовок = ПодчиненныйИО.ТекстЗаголовка;
			
			НовыйУзел.Вид       = ПодчиненныйИО.Вид;
			
			НовыйУзел.Тип = Тип("ТестируемаяГруппаФормы");
			
			
			КомПанель = ПодчиненныйИО.ПолучитьКоманднуюПанель();
			КонтМеню  = ПодчиненныйИО.ПолучитьКонтекстноеМеню();
			
			// Подчиненные элементы есть
			СцТ_РекурсивноеЗаполнениеДереваОбъектов(ПодчиненныйИО, НовыйУзел);
			
			
		ИначеЕсли ТипЗнч(ПодчиненныйИО) = Тип("ТестируемоеПолеФормы") Тогда
			
			НовыйУзел.Имя       = ПодчиненныйИО.Имя;
			НовыйУзел.Заголовок = ПодчиненныйИО.ТекстЗаголовка;
			
			НовыйУзел.Вид       = ПодчиненныйИО.Вид;
			
			НовыйУзел.Тип = Тип("ТестируемоеПолеФормы");
			
			
			КомПанель = ПодчиненныйИО.ПолучитьКоманднуюПанель();
			КонтМеню  = ПодчиненныйИО.ПолучитьКонтекстноеМеню();
			
			// Подчиненные элементы есть
			СцТ_РекурсивноеЗаполнениеДереваОбъектов(ПодчиненныйИО, НовыйУзел);
			
		ИначеЕсли ТипЗнч(ПодчиненныйИО) = Тип("ТестируемаяКнопкаФормы") Тогда
			
			НовыйУзел.Имя       = ПодчиненныйИО.Имя;
			НовыйУзел.Заголовок = ПодчиненныйИО.ТекстЗаголовка;
			
			НовыйУзел.Вид       = ПодчиненныйИО.Вид;
			
			НовыйУзел.Тип = Тип("ТестируемаяКнопкаФормы");
			
		ИначеЕсли ТипЗнч(ПодчиненныйИО) = Тип("ТестируемаяТаблицаФормы") Тогда
			
			НовыйУзел.Имя       = ПодчиненныйИО.Имя;
			НовыйУзел.Заголовок = ПодчиненныйИО.ТекстЗаголовка;
			
			
			НовыйУзел.Тип = Тип("ТестируемаяТаблицаФормы");
			
			
			КомПанель = ПодчиненныйИО.ПолучитьКоманднуюПанель();
			КонтМеню  = ПодчиненныйИО.ПолучитьКонтекстноеМеню();
			
			// Подчиненные элементы есть
			СцТ_РекурсивноеЗаполнениеДереваОбъектов(ПодчиненныйИО, НовыйУзел);
			
		ИначеЕсли ТипЗнч(ПодчиненныйИО) = Тип("ТестируемаяДекорацияФормы") Тогда
			
			НовыйУзел.Имя       = ПодчиненныйИО.Имя;
			НовыйУзел.Заголовок = ПодчиненныйИО.ТекстЗаголовка;
			
			НовыйУзел.Вид       = ПодчиненныйИО.Вид;
			
			НовыйУзел.Тип = Тип("ТестируемаяДекорацияФормы");
			
			
			КомПанель = ПодчиненныйИО.ПолучитьКоманднуюПанель();
			КонтМеню  = ПодчиненныйИО.ПолучитьКонтекстноеМеню();
			
			// Подчиненные элементы есть
			СцТ_РекурсивноеЗаполнениеДереваОбъектов(ПодчиненныйИО, НовыйУзел);
			
		Иначе
			
			
		КонецЕсли;
		
		
	КонецЦикла;
	
	
КонецПроцедуры


&НаКлиенте
Процедура СцТ_ВыбратьОбъектыИЗакрытьФорму()
	
	Если Элементы.ДеревоОбъектов.ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран объект тестируемого приложения'"));
		Возврат;
	КонецЕсли;
	
	УзелОбъекта = ДеревоОбъектов.НайтиПоИдентификатору(Элементы.ДеревоОбъектов.ТекущаяСтрока);
	Если НЕ УзелОбъекта.ДоступенДляВыбора Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Текущий объект не может быть выбран!'"));
		Возврат;
	КонецЕсли;
	
	//Закрыть(СцТ_ПолучитьОписаниеОбъекта(УзелОбъекта));
	ОповеститьОВыборе(СцТ_ПолучитьОписаниеОбъекта(УзелОбъекта));
	
КонецПроцедуры

&НаКлиенте
Функция СцТ_ПолучитьОписаниеОбъекта(Знач УзелДереваОбъектов) Экспорт
	
	Если ТипЗнч(УзелДереваОбъектов) = Тип("Число") Тогда
		УзелДереваОбъектов = ДеревоОбъектов.НайтиПоИдентификатору(УзелДереваОбъектов);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Имя"                           , УзелДереваОбъектов.Имя);
	Результат.Вставить("Заголовок"                     , УзелДереваОбъектов.Заголовок);
	Результат.Вставить("Тип"                           , УзелДереваОбъектов.Тип);
	Результат.Вставить("Вид"                           , УзелДереваОбъектов.Вид);
	Результат.Вставить("Доступность"                   , УзелДереваОбъектов.Доступность);
	Результат.Вставить("Видимость"                     , УзелДереваОбъектов.Видимость);
	Результат.Вставить("ТолькоПросмотр"                , УзелДереваОбъектов.ТолькоПросмотр);
	Результат.Вставить("ПредставлениеОбъектаПриложения", УзелДереваОбъектов.ПредставлениеОбъектаПриложения);
	Результат.Вставить("Значение"                      , УзелДереваОбъектов.Значение);
	Результат.Вставить("ПредставлениеДанных"           , УзелДереваОбъектов.ПредставлениеДанных);
	Результат.Вставить("Объект"                        , ОбъектыТестируемогоПриложения[УзелДереваОбъектов.ПолучитьИдентификатор()]);
	Результат.Вставить("ОписаниеРодительскогоОбъекта"  , Неопределено);
	Результат.Вставить("ОкноОбъект"                    , Неопределено);
	Результат.Вставить("ЭтоТекущийЭлемент"             , УзелДереваОбъектов.ЭтоТекущийЭлемент);
	Результат.Вставить("КнопкаПоУмолчанию"             , УзелДереваОбъектов.КнопкаПоУмолчанию);
	Результат.Вставить("ТипОкна"                       , УзелДереваОбъектов.ТипОкна);
	Результат.Вставить("ПутьКОбъекту"                  , ПутиПоискаОбъектов[УзелДереваОбъектов.ПолучитьИдентификатор()]);
	
	РодительскийУзел = УзелДереваОбъектов.ПолучитьРодителя();
	
	Если РодительскийУзел <> Неопределено Тогда
		Результат.ОписаниеРодительскогоОбъекта = СцТ_ПолучитьОписаниеОбъекта(РодительскийУзел);
		
		Если Результат.Тип = Тип("ТестируемаяФорма") Тогда
			Если Результат.ОписаниеРодительскогоОбъекта.Тип = Тип("ТестируемоеОкноКлиентскогоПриложения") Тогда
				Результат.ОкноОбъект = Результат.ОписаниеРодительскогоОбъекта.Объект;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СцТ_ЗаполнитьСписокВыбранныхОбъектов(УзелДерева, СписокВыбранныхОбъектов)
	
	Для каждого Узел Из УзелДерева.ПолучитьЭлементы() Цикл
		Если Узел.Отметка Тогда
			СписокВыбранныхОбъектов.Добавить(СцТ_ПолучитьОписаниеОбъекта(Узел));
		КонецЕсли;
		СцТ_ЗаполнитьСписокВыбранныхОбъектов(Узел, СписокВыбранныхОбъектов);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_ОтметитьЭлементы(ВетвьДерева, ТекущиеЭлементы)
	
	Для каждого Узел Из ВетвьДерева.ПолучитьЭлементы() Цикл
		
		ОтметитьУзел = СцТ_ВСпискеПрисутствуетОбъект(
				ТекущиеЭлементы,
				Узел.Тип,
				Узел.Вид,
				Узел.Имя,
				Узел.Заголовок,
				ПутиПоискаОбъектов[Узел.ПолучитьИдентификатор()]);
		
		Если ОтметитьУзел Тогда
			ИдентификаторУзла = Узел.ПолучитьИдентификатор();
			Элементы.ДеревоОбъектов.ВыделенныеСтроки.Добавить(ИдентификаторУзла);
			Элементы.ДеревоОбъектов.ТекущаяСтрока = ИдентификаторУзла;
		КонецЕсли;
		
		СцТ_ОтметитьЭлементы(Узел, ТекущиеЭлементы);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СцТ_ПолучитьИндексКартинки(СтруктураОбъект)
	
	ТипОбъекта = СтруктураОбъект.Тип;
	Если ТипОбъекта = Тип("ТестируемоеОкноКлиентскогоПриложения")
		ИЛИ ТипОбъекта = Тип("ТестируемыйКомандныйИнтерфейсОкна") Тогда
		Возврат 0;
	ИначеЕсли ТипОбъекта = Тип("ТестируемаяФорма") Тогда
		Возврат 1;
	ИначеЕсли ТипОбъекта = Тип("ТестируемаяТаблицаФормы") Тогда
		Возврат 2;
	ИначеЕсли ТипОбъекта = Тип("ТестируемаяГруппаФормы")
		ИЛИ ТипОбъекта = Тип("ТестируемаяГруппаКомандногоИнтерфейса") Тогда
		Возврат 3;
	ИначеЕсли ТипОбъекта = Тип("ТестируемаяКнопкаФормы")
		ИЛИ ТипОбъекта = Тип("ТестируемаяКнопкаКомандногоИнтерфейса") Тогда
		Возврат 4;
	ИначеЕсли ТипОбъекта = Тип("ТестируемоеПолеФормы") Тогда
		Возврат 5;
	ИначеЕсли ТипОбъекта = Тип("ТестируемаяДекорацияФормы") Тогда
		Возврат 6;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

&НаКлиенте
Функция СцТ_ПолучитьВсеВыбираемыеОбъекты() Экспорт
	
	Результат = Новый Массив;
	СцТ_ЗаполнитьВсеВыбираемыеОбъекты(Результат, ДеревоОбъектов);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СцТ_ЗаполнитьВсеВыбираемыеОбъекты(МассивОбъектов, УзелДерева)
	
	Для каждого Узел Из УзелДерева.ПолучитьЭлементы() Цикл
		
		Если Узел.ДоступенДляВыбора Тогда
			МассивОбъектов.Добавить(СцТ_ПолучитьОписаниеОбъекта(Узел));
		КонецЕсли;
		
		СцТ_ЗаполнитьВсеВыбираемыеОбъекты(МассивОбъектов, Узел);
		
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура СцТ_УстановитьОтметкиВДеревеОбъектов(УзелДерева, ЗначениеОтметки)
	
	Если УзелДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Узел Из УзелДерева.ПолучитьЭлементы() Цикл
		
		Если Узел.ДоступенДляВыбора Тогда
			Узел.Отметка = ЗначениеОтметки;
		КонецЕсли;
		
		СцТ_УстановитьОтметкиВДеревеОбъектов(Узел, ЗначениеОтметки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СцТ_ВСпискеПрисутствуетОбъект(
	СписокВидов,
	ТипОбъекта,
	ВидОбъекта,
	ИмяОбъекта = Неопределено,
	ЗаголовокОбъекта = Неопределено,
	ПутьПоискаОбъекта = Неопределено)
	
	Если СписокВидов = Неопределено Тогда
		// Список не задан
		Возврат Истина;
	КонецЕсли;
	
	Если СписокВидов.НайтиПоЗначению(ТипОбъекта) <> Неопределено
		ИЛИ СписокВидов.НайтиПоЗначению(ВидОбъекта) <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	НайденоСовпадение = Ложь;
	Для каждого ЭлементСписка Из СписокВидов Цикл
		
		ЗначениеЭлемента = ЭлементСписка.Значение;
		Если ТипЗнч(ЗначениеЭлемента) = Тип("Структура") Тогда
			
			ЗначениеСтруктуре = Неопределено;
			Если ЗначениеЭлемента.Свойство("ТипОбъекта", ЗначениеСтруктуре)
				И ТипОбъекта <> ЗначениеСтруктуре Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЭлемента.Свойство("ВидОбъекта", ЗначениеСтруктуре)
				И ВидОбъекта <> ЗначениеСтруктуре Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИмяОбъекта <> Неопределено
				И ЗначениеЭлемента.Свойство("ИмяОбъекта", ЗначениеСтруктуре)
				И ИмяОбъекта <> ЗначениеСтруктуре Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗаголовокОбъекта <> Неопределено
				И ЗначениеЭлемента.Свойство("ЗаголовокОбъекта", ЗначениеСтруктуре)
				И ЗаголовокОбъекта <> ЗначениеСтруктуре Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПутьПоискаОбъекта <> Неопределено
				И ЗначениеЭлемента.Свойство("ПутьКОбъекту", ЗначениеСтруктуре)
				И НЕ СцТ_ПутиОбъектовСовпадают(ПутьПоискаОбъекта, ЗначениеСтруктуре) Тогда
				Продолжить;
			КонецЕсли;
			
			НайденоСовпадение = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НайденоСовпадение;
	
КонецФункции

&НаКлиенте
Функция СцТ_ПутиОбъектовСовпадают(Путь1, Путь2)
	
	ВГраница = Путь1.ВГраница();
	Если ВГраница <> Путь2.ВГраница() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Итератор = 0 По ВГраница Цикл
		
		ОбъектВПути1 = Путь1[Итератор];
		ОбъектВПути2 = Путь2[Итератор];
		
		Если ОбъектВПути1.ТипОбъекта <> ОбъектВПути2.ТипОбъекта Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если Лев(ОбъектВПути2.ИмяОбъекта, 1) = "$" Тогда
			
			Если ОбъектВПути1.ЗаголовокОбъекта <> Сред(ОбъектВПути2.ИмяОбъекта, 2) Тогда
				Возврат Ложь;
			КонецЕсли;
			
		Иначе
			
			Если ОбъектВПути1.ИмяОбъекта <> ОбъектВПути2.ИмяОбъекта Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Если ОбъектВПути1.ЗаголовокОбъекта <> ОбъектВПути2.ЗаголовокОбъекта Тогда
				Возврат Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция СцТ_ЗакрепитьПутьКОбъекту(ИдентификаторУзла, ПолныйПуть, ТипОбъекта, ИмяОбъекта, ЗаголовокОбъекта)
	
	ТипПриложение = Тип("ТестируемоеПриложение");
	ТипОкно       = Тип("ТестируемоеОкноКлиентскогоПриложения");
	ТипФорма      = Тип("ТестируемаяФорма");
	ТипТаблица    = Тип("ТестируемаяТаблицаФормы");
	
	КоличествоЭлементовВПути = ПолныйПуть.Количество();
	ИндексОбъекта = 0;
	Для ОбратныйИндекс = 1 По КоличествоЭлементовВПути Цикл
		
		ИндекстТекущегоОбъекта = КоличествоЭлементовВПути - ОбратныйИндекс;
		ОписательОбъекта = ПолныйПуть[ИндекстТекущегоОбъекта];
		Если ОписательОбъекта.ТипОбъекта = ТипПриложение
			ИЛИ ОписательОбъекта.ТипОбъекта = ТипОкно
			ИЛИ ОписательОбъекта.ТипОбъекта = ТипФорма
			ИЛИ ОписательОбъекта.ТипОбъекта = ТипТаблица
				И ОбратныйИндекс <> 1 Тогда
			
			ИндексОбъекта = ИндекстТекущегоОбъекта;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОтносительныйПуть = Новый Массив; // относительно формы или таблицы формы
	
	Для Итератор = ИндексОбъекта + 1 По КоличествоЭлементовВПути - 1 Цикл
		ОписательОбъекта = ПолныйПуть[Итератор];
		ОтносительныйПуть.Добавить(ОписательОбъекта);
	КонецЦикла;
	
	ОтносительныйПуть.Добавить(Новый Структура("ТипОбъекта,ИмяОбъекта,ЗаголовокОбъекта",
											   ТипОбъекта,
											   ИмяОбъекта,
											   ЗаголовокОбъекта));
	
	ПутиПоискаОбъектов[ИдентификаторУзла] = ОтносительныйПуть;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
