
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПутьКФормам         = Параметры.ПутьКФормам;
	
	Если НЕ ПустаяСтрока(Параметры.ЗаголовокФормы) Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	ТекстСообщения = Параметры.ТекстСообщения;
	СписокРазличий = Параметры.СписокРазличий;
	
	Элементы.ФормаСравнитьОтличияВизуально.Видимость = (СписокРазличий.Количество() > 0);
	
	Если СписокРазличий.Количество() = 1 Тогда
		Элементы.ФормаСравнитьОтличияВизуально.Заголовок =
			СписокРазличий[0].Представление;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Для каждого ЭлементСписка Из СписокРазличий Цикл
		УдалитьИзВременногоХранилища(ЭлементСписка.Значение.АдресЭталоннойТЗ);
		УдалитьИзВременногоХранилища(ЭлементСписка.Значение.АдресТестовойТЗ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьОтличияВизуально(Команда)
	
	Если СписокРазличий.Количество() = 1 Тогда
		ПараметрыФормы = СписокРазличий[0].Значение;
		//ПараметрыФормы.Вставить("РежимСовместимости838", СцТ_ГлавнаяФорма.РежимСовместимости838);
		ОткрытьФорму(ПутьКФормам + "ВизуальноеСравнениеТаблиц",
			ПараметрыФормы,
			ЭтотОбъект);
	Иначе
		СписокРазличий.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("СцТ_ОбработатьВыборЭлементаРазличий", ЭтотОбъект),
			НСтр("ru = 'Таблица для визуального сравнения'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура СцТ_ОбработатьВыборЭлементаРазличий(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ПараметрыФормы = ВыбранныйЭлемент.Значение;
		ОткрытьФорму(ПутьКФормам + "ВизуальноеСравнениеТаблиц",
			ПараметрыФормы,
			ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
