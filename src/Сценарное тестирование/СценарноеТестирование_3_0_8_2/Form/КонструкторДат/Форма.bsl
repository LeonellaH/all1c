
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриВыборе = Ложь;
	
	Если Параметры.ВходящаяДата = Неопределено Тогда
		ВходящаяДата = ТекущаяДата();
	Иначе
		ВходящаяДата = Параметры.ВходящаяДата;
	КонецЕсли;
		
	Если ТипЗнч(ВходящаяДата) = Тип("Дата") Тогда
		АбсолютнаяДата = Истина;
		РасчетнаяДата  = Ложь;
		ВидДаты = "ДатаНачала";
		ФиксированнаяДата = ВходящаяДата;
	ИначеЕсли ТипЗнч(Параметры.ВходящаяДата) = Тип("Строка") Тогда
		АбсолютнаяДата    = Ложь;
		РасчетнаяДата     = Истина;
		
		СцТ_РасшифроватьФорматнуюСтрокуНаСервере(ВходящаяДата);
	КонецЕсли;
	
	НачальнаяДата = Параметры.НачальнаяДата;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	СцТ_УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы_Шапка

&НаКлиенте
Процедура ВидДатыПриИзменении(Элемент)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	СцТ_УправлениеДоступностьюЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ФиксированнаяДатаПриИзменении(Элемент)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	
КонецПроцедуры

&НаКлиенте
Процедура ГодПриИзменении(Элемент)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцПриИзменении(Элемент)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеньПриИзменении(Элемент)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтносительноПериодаПриИзменении(Элемент)
	
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ОК(Команда)
	
	ОповеститьОВыборе(ИтоговаяФормула);
	Закрыть(ИтоговаяФормула);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеПроцедурыИФункции

&НаКлиенте
Процедура СцТ_ПолучитьФорматнуюСтроку()
	
	Если АбсолютнаяДата Тогда
		ИтоговаяФормула = ФиксированнаяДата;
		
	Иначе
	
		ФорматнаяСтрока = "";
		
		Если ВидДаты = "ТекущаяДата" Тогда
			ФорматнаяСтрока = "ТекущаяДата";
		ИначеЕсли ВидДаты = "ДатаНачала" Тогда
			ФорматнаяСтрока = "ДатаНачала";
		КонецЕсли;
		
		Если Месяц = 0 Тогда
			СтрокаМесяц = "ММ+0";
		ИначеЕсли Месяц < 0 Тогда
			СтрокаМесяц = "ММ-" + СтрЗаменить(Строка((-1) * Месяц), Символ(160),"");
		Иначе
			СтрокаМесяц = "ММ+" + СтрЗаменить(Строка(Месяц), Символ(160),"");
		КонецЕсли;
			
		Если Год = 0 Тогда
			СтрокаГод = "ГГ+0";
		ИначеЕсли Год < 0 Тогда
			СтрокаГод = "ГГ-" + СтрЗаменить(Строка((-1) * Год), Символ(160),"");
		Иначе
			СтрокаГод = "ГГ+" + СтрЗаменить(Строка(Год), Символ(160),"");
		КонецЕсли;
		
		Если День = 0 Тогда
			СтрокаДень = "ДД+0";
		ИначеЕсли День < 0 Тогда
			СтрокаДень = "ДД-" + СтрЗаменить(Строка((-1) * День), Символ(160),"");
		Иначе
			СтрокаДень = "ДД+" + СтрЗаменить(Строка(День), Символ(160),"");
		КонецЕсли;
		
		ФорматнаяСтрока = ФорматнаяСтрока + "," + СтрокаГод + "," + СтрокаМесяц + "," + СтрокаДень;
		
		Если НЕ ПустаяСтрока(ДатаОтносительноПериода) Тогда
			ФорматнаяСтрока = ДатаОтносительноПериода + "(" + ФорматнаяСтрока + ")";
		КонецЕсли;
		
		ИтоговаяФормула = "Дата = " + ФорматнаяСтрока;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_РассчитатьРезультатПоФорматнойСтроке()
	
	Г = 1;
	М = 1;
	Д = 1;
	
	Если ТипЗнч(ИтоговаяФормула) = Тип("Дата") Тогда
		РезультирующаяДата =  ИтоговаяФормула;
	Иначе
		
		ВходящаяФорматнаяСтрока = СтрЗаменить(ИтоговаяФормула, "Дата = ", "");
		
		// Выделение Приведения
		Если Найти(ВходящаяФорматнаяСтрока, "НачалоНедели") Тогда
			ПриведениеДаты = "НачалоНедели";
			ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "НачалоНедели(", "");
			ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
		ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "КонецНедели") Тогда
			ПриведениеДаты = "КонецНедели";
			ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "КонецНедели(", "");
			ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
			
		ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "НачалоМесяца") Тогда
			ПриведениеДаты = "НачалоМесяца";
			ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "НачалоМесяца(", "");
			ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
			
		ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "КонецМесяца") Тогда
			ПриведениеДаты = "КонецМесяца";
			ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "КонецМесяца(", "");
			ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
			
		ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "НачалоКвартала") Тогда
			ПриведениеДаты = "НачалоКвартала";
			ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "НачалоКвартала(", "");
			ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
			
		ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "КонецКвартала") Тогда
			ПриведениеДаты = "КонецКвартала";
			ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "КонецКвартала(", "");
			ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
			
		Иначе
			ПриведениеДаты = "";
		КонецЕсли;
		
		// Выделение вида даты
		ПозицияЗапятой = Найти(ВходящаяФорматнаяСтрока, ",");
		Если ПозицияЗапятой <= 1 Тогда
			Возврат;
		КонецЕсли;
		
		СлужебныйВидДаты = Сред(ВходящаяФорматнаяСтрока, 1, ПозицияЗапятой - 1);
		Если Найти(СлужебныйВидДаты, "ТекущаяДата") Тогда
			// ВидДаты = "ТекущаяДата";
			ИсходнаяДата = НачалоДня(ТекущаяДата());
		Иначе
			// ВидДаты = "ДатаНачала";
			ИсходнаяДата = НачальнаяДата;
		КонецЕсли;
		
		ВходящаяФорматнаяСтрока = Сред(ВходящаяФорматнаяСтрока, ПозицияЗапятой + 1);
		
		// Год смещение
		ПозицияЗапятой = Найти(ВходящаяФорматнаяСтрока, ",");
		Если ПозицияЗапятой <= 1 Тогда
			Возврат;
		КонецЕсли;
		
		ГГ = Сред(ВходящаяФорматнаяСтрока, 1, ПозицияЗапятой - 1);
		ГГ = СтрЗаменить(ГГ, "ГГ", "");
		ГГОтрицательный = Найти(ГГ,"-");
		ГГ = СтрЗаменить(СтрЗаменить(ГГ, "+", ""), "-","");
		СмещениеГода = Число(ГГ);
		
		ВходящаяФорматнаяСтрока = Сред(ВходящаяФорматнаяСтрока, ПозицияЗапятой + 1);
		
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока," ", "");
		
		// Месяц смещение
		ПозицияЗапятой = Найти(ВходящаяФорматнаяСтрока, ",");
		Если ПозицияЗапятой <= 1 Тогда
			Возврат;
		КонецЕсли;
		
		ММ = Сред(ВходящаяФорматнаяСтрока, 1, ПозицияЗапятой - 1);
		ММ = СтрЗаменить(ММ, "ММ", "");
		ММОтрицательный = Найти(ММ,"-");
		ММ = СтрЗаменить(СтрЗаменить(ММ, "+", ""), "-","");
		СмещениеМесяца = Число(ММ);
		
		ВходящаяФорматнаяСтрока = Сред(ВходящаяФорматнаяСтрока, ПозицияЗапятой + 1);
		
		// День смещение
		ДД = ВходящаяФорматнаяСтрока;
		ДД = СтрЗаменить(ДД, "ДД", "");
		ДДОтрицательный = Найти(ДД, "-");
		ДД = СтрЗаменить(СтрЗаменить(ДД, "+", ""), "-","");
		СмещениеДня = Число(ДД);
		
		Если СмещениеДня <> 0 ИЛИ СмещениеМесяца <> 0 ИЛИ СмещениеГода <> 0 Тогда
			Д = День(ИсходнаяДата);
			ЭтоПоследнийДеньМесяца = (Д = День(КонецМесяца(ИсходнаяДата)));
			М = Месяц(ИсходнаяДата);
			Г = Год(ИсходнаяДата);
			
			// Год
			Если ГГОтрицательный Тогда
				Г = Г - СмещениеГода;
			Иначе
				Г = Г + СмещениеГода;
			КонецЕсли;
			
			// Месяц
			Если ММОтрицательный Тогда
				М = М - СмещениеМесяца;
			Иначе
				М = М + СмещениеМесяца;
			КонецЕсли;
			
			КоррекцияГода = Цел(М/12);
			М = М - КоррекцияГода*12;
			
			Если М <= 0 Тогда
				КоррекцияГода = КоррекцияГода - 1;
				М = М + 12;
			КонецЕсли;
			
			Г = Г + КоррекцияГода;
			
			// Приведение даты к новому месяцу
			ПоследнийДеньНовогоМесяца = День(КонецМесяца(Дата(Г,М,15)));
			Если ЭтоПоследнийДеньМесяца Тогда
				Д = ПоследнийДеньНовогоМесяца;
			Иначе
				Если Д > ПоследнийДеньНовогоМесяца Тогда
					Д = ПоследнийДеньНовогоМесяца;
				КонецЕсли;
			КонецЕсли;
			
			// Применение смещения дня
			Пока СмещениеДня <> 0 Цикл
				ПоследнийДеньТекущегоМесяца = День(КонецМесяца(Дата(Г,М,15)));
				Если ДДОтрицательный Тогда
					Если СмещениеДня > Д Тогда
						М = М - 1;
						Если М = 0 Тогда
							М = 12;
							Г = Г - 1;
						КонецЕсли;
						Д = День(КонецМесяца(Дата(Г,М,15)));
						СмещениеДня = СмещениеДня - Д - 1;
					Иначе
						Д = Д - СмещениеДня;
						СмещениеДня = 0;
					КонецЕсли;
				Иначе
					ОсталосьДоКонцаМесяца = ПоследнийДеньТекущегоМесяца - Д;
					Если СмещениеДня > ОсталосьДоКонцаМесяца Тогда
						М = М + 1;
						Если М > 12 Тогда
							Г = Г + 1;
							М = 1;
						КонецЕсли;
						Д = 1;
						СмещениеДня = СмещениеДня - (ОсталосьДоКонцаМесяца + 1);
					Иначе
						Д = Д + СмещениеДня;
						СмещениеДня = 0;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ПредварительнаяДата = Дата(Г,М,Д);
		Иначе
			ПредварительнаяДата = ИсходнаяДата;
			
		КонецЕсли;
		
		// Применение формулы приведения
		Если ПриведениеДаты = "НачалоНедели" Тогда
			РезультирующаяДата = НачалоНедели(ПредварительнаяДата);
		ИначеЕсли ПриведениеДаты = "КонецНедели" Тогда
			РезультирующаяДата = КонецНедели(ПредварительнаяДата);
		ИначеЕсли ПриведениеДаты = "НачалоМесяца" Тогда
			РезультирующаяДата = НачалоМесяца(ПредварительнаяДата);
		ИначеЕсли ПриведениеДаты = "КонецМесяца" Тогда
			РезультирующаяДата = КонецМесяца(ПредварительнаяДата);
		ИначеЕсли ПриведениеДаты = "НачалоКвартала" Тогда
			РезультирующаяДата = НачалоКвартала(ПредварительнаяДата);
		ИначеЕсли ПриведениеДаты = "КонецКвартала" Тогда
			РезультирующаяДата = КонецКвартала(ПредварительнаяДата);
		Иначе
			РезультирующаяДата = ПредварительнаяДата;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СцТ_РасшифроватьФорматнуюСтрокуНаСервере(Знач ВходящаяФорматнаяСтрока)
	
	ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "Дата = ", "");
	
	// Выделение Приведения
	Если Найти(ВходящаяФорматнаяСтрока, "НачалоНедели") Тогда
		ДатаОтносительноПериода = "НачалоНедели";
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "НачалоНедели(", "");
		ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
	ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "КонецНедели") Тогда
		ДатаОтносительноПериода = "КонецНедели";
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "КонецНедели(", "");
		ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
		
	ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "НачалоМесяца") Тогда
		ДатаОтносительноПериода = "НачалоМесяца";
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "НачалоМесяца(", "");
		ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
		
	ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "КонецМесяца") Тогда
		ДатаОтносительноПериода = "КонецМесяца";
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "КонецМесяца(", "");
		ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
		
	ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "НачалоКвартала") Тогда
		ДатаОтносительноПериода = "НачалоКвартала";
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "НачалоКвартала(", "");
		ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
		
	ИначеЕсли Найти(ВходящаяФорматнаяСтрока, "КонецКвартала") Тогда
		ДатаОтносительноПериода = "КонецКвартала";
		ВходящаяФорматнаяСтрока = СтрЗаменить(ВходящаяФорматнаяСтрока, "КонецКвартала(", "");
		ВходящаяФорматнаяСтрока = Лев(ВходящаяФорматнаяСтрока, СтрДлина(ВходящаяФорматнаяСтрока) - 1);
		
	Иначе
		ДатаОтносительноПериода = "";
	КонецЕсли;
	
	// Выделение вида даты
	ПозицияЗапятой = Найти(ВходящаяФорматнаяСтрока, ",");
	Если ПозицияЗапятой <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	СлужебныйВидДаты = Сред(ВходящаяФорматнаяСтрока, 1, ПозицияЗапятой - 1);
	Если Найти(СлужебныйВидДаты, "ТекущаяДата") Тогда
		ВидДаты = "ТекущаяДата";
	ИначеЕсли Найти(СлужебныйВидДаты, "ДатаНачала") Тогда
		ВидДаты = "ДатаНачала";
	КонецЕсли;
	
	ВходящаяФорматнаяСтрока = Сред(ВходящаяФорматнаяСтрока, ПозицияЗапятой + 1);
	
	// Год смещение
	ПозицияЗапятой = Найти(ВходящаяФорматнаяСтрока, ",");
	Если ПозицияЗапятой <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	ГГ = Сред(ВходящаяФорматнаяСтрока, 1, ПозицияЗапятой - 1);
	ГГ = СтрЗаменить(ГГ, "ГГ", "");
	Год = Число(ГГ);
	
	ВходящаяФорматнаяСтрока = Сред(ВходящаяФорматнаяСтрока, ПозицияЗапятой + 1);
	
	// Месяц смещение
	ПозицияЗапятой = Найти(ВходящаяФорматнаяСтрока, ",");
	Если ПозицияЗапятой <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	ММ = Сред(ВходящаяФорматнаяСтрока, 1, ПозицияЗапятой - 1);
	ММ = СтрЗаменить(ММ, "ММ", "");
	Месяц = Число(ММ);
	
	ВходящаяФорматнаяСтрока = Сред(ВходящаяФорматнаяСтрока, ПозицияЗапятой + 1);
	
	// День смещение
	ДД = ВходящаяФорматнаяСтрока;
	ДД = СтрЗаменить(ДД, "ДД", "");
	День = Число(ДД);
	
КонецПроцедуры

&НаКлиенте
Процедура АбсолютнаяДатаПриИзменении(Элемент)
	
	// Должно работать вместе с расчетной датой как радиокнопка
	Если НЕ АбсолютнаяДата Тогда
		АбсолютнаяДата = Истина;
	Иначе
		РасчетнаяДата = Ложь;
	КонецЕсли;
	
	СцТ_УправлениеДоступностьюЭлементовФормы();
	СцТ_ПолучитьФорматнуюСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетнаяДатаПриИзменении(Элемент)
	
	// Должно работать вместе с расчетной датой как радиокнопка
	Если НЕ РасчетнаяДата Тогда
		РасчетнаяДата = Истина;
	Иначе
		АбсолютнаяДата = Ложь;
	КонецЕсли;
	
	СцТ_УправлениеДоступностьюЭлементовФормы();
	СцТ_ПолучитьФорматнуюСтроку();
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	
КонецПроцедуры

&НаКлиенте
Процедура СцТ_УправлениеДоступностьюЭлементовФормы()
	
	Если АбсолютнаяДата Тогда
		Элементы.ФиксированнаяДата.Доступность       = Истина;
		
		Элементы.ВидДаты.Доступность                 = Ложь;
		Элементы.НачальнаяДата.Доступность           = Ложь;
		Элементы.Год.Доступность                     = Ложь;
		Элементы.Месяц.Доступность                   = Ложь;
		Элементы.День.Доступность                    = Ложь;
		Элементы.ДатаОтносительноПериода.Доступность = Ложь;
		
	Иначе
		Элементы.ФиксированнаяДата.Доступность       = Ложь;
		
		Элементы.ВидДаты.Доступность                 = Истина;
		
		Если ВидДаты = "ТекущаяДата" Тогда
			Элементы.НачальнаяДата.Доступность       = Ложь;
		Иначе
			Элементы.НачальнаяДата.Доступность       = Истина;
		КонецЕсли;
		
		Элементы.НачальнаяДата.Доступность           = Истина;
		Элементы.Год.Доступность                     = Истина;
		Элементы.Месяц.Доступность                   = Истина;
		Элементы.День.Доступность                    = Истина;
		Элементы.ДатаОтносительноПериода.Доступность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачальнаяДатаПриИзменении(Элемент)
	
	СцТ_РассчитатьРезультатПоФорматнойСтроке();
	ТекстВопроса = НСтр("ru = 'Изменит начальную дату в настройках сценария?'");
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриИзмененииДаты", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриИзмененииДаты(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Оповестить("ИзменениеНачальнойДатыСценария", НачальнаяДата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СцТ_ПринудительноеЗакрытиеВсехФормОбработки" Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти




